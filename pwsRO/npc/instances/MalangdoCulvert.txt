//===== rAthena Script ======================================= 
//= Malangdo Culvert
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Clean the culverts and defeat the Coelacanth.
//= Daily and weekly quests are available.
//= Contains a normal mode and hard mode.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//=     Letters "n" and "h" in NPC names indicate difficulty.
//= 1.0a There is no minimum party size on official servers.
//= 1.0b Fixed incorrect use of 'close'. [Joseph]
//= 1.1 Instance system rewrite. [Euphy]
//============================================================ 

// Quest NPCs :: mal_yong
//============================================================
mal_in01,172,28,2	script	阿寶#mal	561,{
	if (checkweight(1201,1) == 0) {
		mes "你身上有太多物品，請減少之後再來。";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你身上太重了，請減少重量之後再來。";
		close;
	}
	if (BaseLevel < 90) {
		mes "[Albo]";
		mes "你是誰?";
		mes "我們不需要被喚醒!!";
		close;
	}
	setarray .@quests[0],12271,12272,12273,12274;
	setarray .@names$[0],
		"排水溝 普通模式 每日任務","^990000排水溝 困難模式 每日任務^000000",
		"排水溝 普通模式 週間任務","^990000排水溝 困難模式 週間任務^000000";
	set .@menu$,"為什麼要做這份工作?:";
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		if (checkquest(.@quests[.@i],PLAYTIME) > -1) {
			set .@status[.@i],2;
			set .@menu$, .@menu$+"^aaaaaa- 沒有新的任務 -^000000:";
		} else if (.@i%2 && BaseLevel < 140) {
			set .@status[.@i],0;
			set .@menu$, .@menu$+"^aaaaaa因為等級不夠，無法接此任務。^000000:";
		} else {
			set .@status[.@i],1;
			set .@menu$, .@menu$+.@names$[.@i]+":";
		}
	}
	mes "[阿寶]";
	mes "讓我們乘風破浪去吧!";
	mes "我們是偉大的排水溝英雄!我們要以拖把和清潔劑之名懲罰覬覦排水溝的臭海鮮!";
	next;
	set .@i, select(.@menu$)-1;
	if (.@i == 0) {
		mes "[阿寶]";
		mes "我們曾經是清潔員 小草的心腹! 我們幾乎變成了一個永久的衛兵。";
		next;
		mes "[阿寶]";
		mes "然而,當海水淹沒了所有珍貴資源，我的夢想... 我的夢想破滅了...";
		next;
		mes "[阿寶]";
		mes "我們不能原諒海鮮.它們入侵了我們的領土，我會吃他們來洩憤!!!!";
		next;
		mes "^0000ff愛寶發出憤怒的聲音抖動，他們迫不及待的想吃那些海鮮。^000000";
		close;
	}
	switch(.@status[.@i-1]) {
	case 0:
		mes "[阿寶]";
		mes "這個任務對你來說很困難，所以我不能告訴你關於這任務，你為何不找其他的呢?";
		close;
	case 1:
		break;
	case 2:
		mes "[阿寶]";
		mes "我很抱歉，現在沒有新的工作! 如果我找到任何工作，我將會告訴你!";
		close;
	}
	switch(.@i) {
	case 1: // General Culvert Daily Service
		switch(rand(1,6)) {
			case 1: callsub L_GiveQuest,.@i,12255,12271,"消滅深海大蟹";
			case 2: callsub L_GiveQuest,.@i,12256,12271,"消滅深海魷魚";
			case 3: callsub L_GiveQuest,.@i,12257,12271,"消滅遠古甲殼類";
			case 4: callsub L_GiveQuest,.@i,12258,12271,"消滅深海貝殼";
			case 5: callsub L_GiveQuest,.@i,12259,12271,"消滅遠古庫克雷";
			case 6: callsub L_GiveQuest,.@i,12260,12271,"消滅深海海螺";
		}
	case 2: // Hard Culvert Daily Service
		switch(rand(1,6)) {
			case 1: callsub L_GiveQuest,.@i,12261,12272,"消滅遠古海馬";
			case 2: callsub L_GiveQuest,.@i,12262,12272,"消滅遠古異變魚";
			case 3: callsub L_GiveQuest,.@i,12263,12272,"消滅遠古海神";
			case 4: callsub L_GiveQuest,.@i,12264,12272,"消滅變種虎蜥人";
			case 5: callsub L_GiveQuest,.@i,12265,12272,"消滅深海人魚";
			case 6: callsub L_GiveQuest,.@i,12266,12272,"消滅變形釣魚河童";
		}
	case 3: // General Culvert Weekly Service
		switch(rand(1,2)) {
			case 1: callsub L_GiveQuest,.@i,12267,12273,"消滅古怪腔棘魚";
			case 2: callsub L_GiveQuest,.@i,12268,12273,"消滅黑暗腔棘魚";
		}
	case 4: // Hard Culvert Weekly Service
		switch(rand(1,2)) {
			case 1: callsub L_GiveQuest,.@i,12269,12274,"消滅暴力腔棘魚";
			case 2: callsub L_GiveQuest,.@i,12270,12274,"消滅變種腔棘魚";
		}
	default:
		mes "[阿寶]";
		mes "Uh!? Where is the task? Let me find that quickly. You must speak with Madeca!";
		close;
	}
	end;

// callsub L_GiveQuest,.@i,<quest 1>,<quest 2>,<monster>;
L_GiveQuest:
	setquest getarg(1);
	setquest getarg(2);
	mes "[Albo]";
	mes "今天，排水溝的英雄!";
	mes "我們提高了我們的聲音並且設置了期限來滅絕 ^0000ff"+getarg(3)+"^000000!";
	next;
	mes "[Albo]";
	switch(getarg(0)) {
	case 1:
	case 2:
		mes "來吧，英雄! 不要怕他們! 只要繼續，它是為期一天的任務!";
		break;
	case 3:
		mes "我給你一個星期的時間來完成這個工作! 終結他們全部在副本內的怪，一般模式!";
		break;
	case 4:
		mes "我給你一個星期的時間來完成這個工作! 終結他們全部在副本內的怪，困難模式!";
		break;
	}
	next;
	mes "你收到的請求被消滅 ^005500"+getarg(3)+"^000000，如果你需要詳細資料，請確認任務視窗。";
	close;
}

mal_in01,172,26,2	script	馬黛卡#mal	544,{
	if (checkweight(1201,1) == 0) {
		mes "你身上有太多物品，請減少之後再來。";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你身上太重了，請減少重量之後再來。";
		close;
	}
	if (BaseLevel < 90) {
		mes "[馬黛卡]";
		mes "Karrrrrrr!!";
		mes "我們有一些壞的海鮮產品可以煮或烤!";
		next;
		mes "[馬黛卡]";
		mes "你是誰? 你看起來很踉蹌，甚至是海草都可以殺了你!";
		close;
	}
	mes "[馬黛卡]";
	mes "恩?";
	mes "處理愛寶哥委託的人類就是你嗎?辛苦囉!";
	next;
	set .@i, select("你來這裡幹麼?:一般-排水溝每日任務獎勵:困難-排水溝每日任務獎勵:一般-排水溝週期任務獎勵:困難-排水溝週期任務獎勵")-1;
	if (.@i == 0) {
		mes "[馬黛卡]";
		mes "我要來幫助我的大哥 愛寶!";
		next;
		mes "[馬黛卡]";
		mes "所以是我們提供真貴的材料給那些用壞掉的海鮮去做水溝理爛泥的人!";
		next;
		mes "[馬黛卡]";
		mes "我們提供A~B級代幣作為每日服務的補償, 每週服務的補償是海神的憤怒.";
		next;
		mes "[馬黛卡]";
		mes "如果你照顧大哥 愛寶 要求, 我將時常看見你. 所以, 讓我們保持聯繫!";
		close;
	}
	mes "[馬黛卡]";
	mes "我不能給你超過期限的獎勵，所以請讓我檢查。";
	next;
	specialeffect2 EF_SPHERE;
	progressbar "0xFFFF00",3;
	specialeffect2 EF_STEAL;
	switch(.@i) {
	case 1: // General Culvert Daily Service
		if (checkquest(12271,PLAYTIME) == 2)
			callsub L_EraseQuest,12255,12256,12257,12258,12259,12260,12271;
		else {
			// Reward: 2x B Grade Coin
			callsub L_CheckQuest,12255,"消滅深海大蟹",6419,2;
			callsub L_CheckQuest,12256,"消滅深海魷魚",6419,2;
			callsub L_CheckQuest,12257,"消滅遠古甲殼類",6419,2;
			callsub L_CheckQuest,12258,"消滅深海貝殼",6419,2;
			callsub L_CheckQuest,12259,"消滅遠古庫克雷",6419,2;
			callsub L_CheckQuest,12260,"消滅深海海螺",6419,2;
		}
		break;
	case 2: // Hard Culvert Daily Service
		if (checkquest(12272,PLAYTIME) == 2)
			callsub L_EraseQuest,12261,12262,12263,12264,12265,12266,12272;
		else {
			// Reward: 1x A Grade Coin
			callsub L_CheckQuest,12261,"消滅遠古海馬",6418,1;
			callsub L_CheckQuest,12262,"消滅遠古異變魚",6418,1;
			callsub L_CheckQuest,12263,"消滅遠古海神",6418,1;
			callsub L_CheckQuest,12264,"消滅變種虎蜥人",6418,1;
			callsub L_CheckQuest,12265,"消滅深海人魚",6418,1;
			callsub L_CheckQuest,12266,"消滅變形釣魚河童",6418,1;
		}
		break;
	case 3: // General Culvert Weekly Service
		if (checkquest(12273,PLAYTIME) == 2)
			callsub L_EraseQuest,12267,12268,12273;
		else {
			// Reward: 1x Sea God's Wrath
			callsub L_CheckQuest,12267,"消滅古怪腔棘魚",6423,1;
			callsub L_CheckQuest,12268,"消滅黑暗腔棘魚",6423,1;
		}
		break;
	case 4: // Hard Culvert Weekly Service
		if (checkquest(12274,PLAYTIME) == 2)
			callsub L_EraseQuest,12269,12270,12274;
		else {
			// Reward: 5x Sea God's Wrath
			callsub L_CheckQuest,12269,"消滅暴力腔棘魚",6423,5;
			callsub L_CheckQuest,12270,"消滅變種腔棘魚",6423,5;
		}
		break;
	default:
		mes "[馬黛卡]";
		mes "發生了一些錯誤，請在確認一次!";
		close;
	}
	mes "[馬黛卡]";
	mes "我沒有看見任何已經完成的任務!";
	close;

L_EraseQuest:
	for(set .@j,0; .@j<getargcount(); set .@j,.@j+1) {
		if (checkquest(getarg(.@j)) > -1)
			erasequest getarg(.@j);
	}
	mes "[馬黛卡]";
	mes "Um, 我很抱歉，你超過了時間限制，所以我不能給你任何東西。";
	close;

// callsub L_CheckQuest,<quest ID>,<monster>,<reward item ID>,<reward item amount>;
L_CheckQuest:
	if (checkquest(getarg(0),HUNTING) == 2) {
		mes "[馬黛卡]";
		mes "你將收到獎勵 ^0000ff"+getarg(1)+"^000000!";
		mes "這些就是你的工資!";
		erasequest getarg(0);
		specialeffect2 EF_STEAL;
		getitem getarg(2),getarg(3);
		close;
	}
	return;
}

// Instance Creation :: pump
//============================================================
mal_in01,160,34,4	script	卡琳那	545,{
	if (BaseLevel < 90) {
		mes "[卡琳那]";
		mes "你在跟我說話嗎?!";
		next;
		mes "[卡琳那]";
		mes "我不知道為什麼你會來到這裡，但是我不能讓你進去裡面!";
		next;
		mes "^770099你的等級需達到 Base level 90 以上才可以進入。^000000";
		close;
	}
	if (in_canal_n == 0) {
		mes "[卡琳那]";
		mes "(顫抖)";
		next;
		mes "^660066這隻貓看起來狀況並不是很好，牠微微的顫抖。^000000";
		next;
		if(select("寵物貓:你還好嗎?") == 1) {
			mes "[Missing, the Cleaner]";
			mes "你是在跟我說話嗎?!";
			next;
			mes "^660066全身顫抖的小貓正抬頭看著你。^000000";
			close;
		}
		mes "[卡琳那]";
		mes "打噴嚏!";
		next;
		mes "[卡琳那]";
		mes "我們遇到了許多的問題，";
		mes "在排水溝清理時，但是，貓總是得做的，我們無從選擇...";
		next;
		select("為什麼你需要去清理排水溝?");
		mes "[卡琳那]";
		mes "因為這個地方值得紀念。";
		next;
		mes "[卡琳那]";
		mes "在這座島上有一個很大的陰謀，";
		mes "事情將開始在此浮出水面，";
		mes "所以這就是為什麼我成了這裡的清潔工，";
		mes "雖然我曾經是一名經理。";
		next;
		mes "^660066這隻貓看起來一直在哭...^000000";
		next;
		mes "[卡琳那]";
		mes "沿著排水溝一直走，";
		mes "會到一個地下室，那裡有很多壞人，我們天天與他們戰鬥著。";
		next;
		mes "[卡琳那]";
		mes "喔! 你摸到了我的毛!";
		mes "我的毛掉下來了... 這一切都是那些傢伙的錯~";
		next;
		mes "^660066你剛才才注意到.貓的毛有缺少。^000000";
		next;
		select("你為什麼不退出!");
		mes "[卡琳那]";
		mes "我需要為我自己謀生。";
		next;
		mes "[卡琳那]";
		mes "有時候，我可以得到一個幸運箱子，";
		mes "甚至更好時，我可以得到罐頭..";
		next;
		if(select("好的, 鼓舞牠!:我可能可以有所幫助...") == 1) {
			mes "[卡琳那]";
			mes "謝謝你，我要回去把水倒出來!";
			close;
		}
		mes "[卡琳那]";
		mes "(傳來它的聲音)";
		mes "如果你真的想幫忙，請走近!";
		next;
		select("(接近)");
		mes "[卡琳那]";
		mes "不是每個人都可以當助理";
		mes "但是我無法選擇幫忙，此刻...";
		next;
		mes "[卡琳那]";
		mes "如果你想去這裡，讓我得到這個密碼。";
		next;
		mes "^660066貓在塗鴉密碼的備忘錄。^000000";
		next;
		select("開啟傳點");
		mes "^660000Aragam insulted me.^000000";
		next;
		mes "[卡琳那]";
		mes "你必須跟朋友一起來這裡，";
		mes "因為你不能單獨進去!";
		mes "密碼的代碼會給隊伍的隊長!";
		next;
		mes "[卡琳那]";
		mes "對你來說最重要的是你必須要有海神護身符。";
		next;
		mes "[卡琳那]";
		mes "別忘記! 否則我不會開門。";
		set in_canal_n,1;
		close;
	}
	if (countitem(6436) == 0) {
		mes "[Missing, the Cleaner]";
		mes "你看起來不像是有海神護身符，我現在不能為你把門打開!";
		close;
	}
	set .@party_id,getcharid(1);
	set .@md_name$,"Culvert";
	if (!.@party_id) {
		mes "^0000ff你必須組隊，並且隊員要超過1名，";
		mes "或是入其他隊伍，之後再回來找我。^000000";
		close;
	}
	set .@playtime, checkquest(12254,PLAYTIME);
	if (.@playtime == -1) {
		if (getcharid(0) == getpartyleader(.@party_id,2)) {
			mes "[卡琳那]";
			mes "恩? 發生什麼事情? 請告訴我密碼如果你是隊長!";
			next;
			switch(select("不... 沒什麼:告訴牠密碼:進入排水溝")) {
			case 1:
				mes "[Missing, the Cleaner]";
				mes "居然是個菜鳥!";
				close;
			case 2:
				if (instance_create(.@md_name$) < 0) {
					mes "隊伍名稱: "+getpartyname(.@party_id);
					mes "隊伍隊長: "+strcharinfo(0);
					mes "^0000ff"+.@md_name$+"^000000 - 創建失敗!";
					close;
				}
				mes "^3333FF"+.@md_name$+"^000000 - 創建中";
				mes "當創建完成之後，你就可以選擇進入排水溝。";
				close;
			case 3:
				callsub L_Enter,0;
			}
		}
		if(select("進入排水溝:取消") == 2)
			end;
		callsub L_Enter,1;
	} else if (.@playtime == 0 || .@playtime == 1) {
		mes "如果門打開了，你就可以進入排水溝。";
		next;
		if(select("進入排水溝:取消") == 2)
			close;
		callsub L_Enter,0;
	} else if (.@playtime == 2) {
		mes "^0000ff排水溝的大門再次打開。^000000";
		erasequest 12254;
		close;
	}
	end;
L_Enter:
	switch(instance_enter("Culvert")) {
	case 3:
		mes "發生預期之外的錯誤。";
		close;
	case 2:
		mes "排水溝的門依然關閉，";
		mes "你必須等待冷卻時間到，又或者找到可以創建副本的隊長。";
		close;
	case 1:
		mes "只有隊伍成員可以參與。";
		close;
	case 0:
		if (checkquest(12254) == -1) setquest 12254;
		//warp "1@pump",63,98;
		if (getarg(0) == 0) close;
		else end;
	}
}

// Instance: Common Scripts
//============================================================
1@pump,63,100,4	script	Missing, the Cleaner#0	545,{
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[Missing, the Cleaner]";
		mes "我只想跟隊伍的隊長談話，不是隊長的別找我談!";
		close;
	}
	mes "[Missing, the Cleaner]";
	mes "這是個地下排水溝，在那裡你要與你的隊友一起清潔!";
	next;
	mes "[Missing, the Cleaner]";
	mes "我將會開門，順便說一下，你知道怎麼打架吧?";
	next;
	switch(select("我去外送麵包:我知道怎麼打架:^ffffff給我鑽石!^000000")) {
	case 1:
		mes "[Missing, the Cleaner]";
		mes "Ok 然後，我會讓你如往常的清理排水溝! 我會準備在右上角，請跟隨我!";
		next;
		if(select("等等! 我改變了主意!:如你所願~") == 1) {
			mes "[Missing, the Cleaner]";
			mes "呵呵? 你還沒準備好? 當你準備好的時候再來找我。";
			close;
		}
		set 'party_id,getcharid(1);
		mapannounce instance_mapname("1@pump"),"小草: 走向3點鐘方向，等待我的下一個指令!",bc_map,"0xff88ff",FW_NORMAL,15;
		disablenpc instance_npcname("Missing, the Cleaner#0");
		enablenpc instance_npcname("Missing, the Cleaner#n");
		close;
	case 2:
		if (BaseLevel < 140) {
			mes "[Missing, the Cleaner]";
			mes "恩... 其實小草認為你很脆弱!!";
			close;
		}
		mes "[Missing, the Cleaner]";
		mes "呵呵? 你有一些戰鬥技巧? 好，然後，有一個地方我沒有辦法去清理... 你就去哪裡吧!";
		next;
		mes "[Missing, the Cleaner]";
		mes "我必須告訴你，那地方是這麼長時間沒有清理，自從我們已經清理過那個地方，所以低於140等的人根本不能進入! 你確定你仍然要進去裡面?";
		next;
		if(select("等等!我改變主意了!:當然，我已經準備好了~") == 1) {
			mes "[Missing, the Cleaner]";
			mes "呵呵? 你還沒準備好? 當你準備好的時候再來找我.";
			close;
		}
		set 'party_id,getcharid(1);
		mapannounce instance_mapname("1@pump"),"小草: 我會先出發，所以請跟著我! 我會打開在3點鐘方向的門!",bc_map,"0xff88ff",FW_NORMAL,15;
		disablenpc instance_npcname("Missing, the Cleaner#0");
		enablenpc instance_npcname("Culvert Entrance#i");
		close;
	case 3:
		mes "[Missing, the Cleaner]";
		mes "我警告你不要玩我~!";
		close;
	}
	end;
}

1@pump,84,105,0	script	Culvert Entrance#i	45,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("Culvert Entrance#i");
	end;
OnTouch:
	if (BaseLevel >= 140)
		warp instance_mapname("2@pump"),38,88;
	else
		warp instance_mapname("1@pump"),74,105;
	end;
}

function	script	F_mal_missing	{
	mes "[Missing, the Cleaner]";
	mes "我會告訴你如何快速清理，你能看到四周的排水溝嗎?";
	next;
	if(select("什麼排水溝??:是的，我能看見它們") == 1) {
		mes "[Missing, the Cleaner]";
		mes "疑? 這是你第一次看到排水溝? 你會看到四周被埋在地上的許多機器，你看一看再回來!";
		close;
	}
	mes "[Missing, the Cleaner]";
	mes "這就對了! 這些排水溝是非常重要的! 我們必須清理排水溝，以防止海藻阻攔它們，這就是我們的清潔工作!";
	next;
	mes "[Missing, the Cleaner]";
	mes "注意! 有些怪物可能會在你清理排水溝時出現! 但是請不要屈服於它們底下!";
	next;
	mes "[Missing, the Cleaner]";
	mes "And... 別讓6個海藻堆疊! 5個是最高上限! 如果有6個，我會請你們離開這裡!!";
	next;
	mes "[Missing, the Cleaner]";
	mes "我不在乎你需不需要倚賴隊友，我希望你可以清理所有的排水溝，不要忘記! 我會持續看著你，並且給你指示，我說什麼你就做什麼!!";
	next;
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[Missing, the Cleaner]";
		mes "一旦隊長已經準備完畢，就將開始，請做好心理準備!!";
		close;
	}
	mes "[Missing, the Cleaner]";
	mes "所以，你準備好要開始清理了嗎?";
	next;
	switch(select("等等!還沒!:讓我們開始吧!")) {
	case 1:
		mes "[Missing, the Cleaner]";
		mes "疑? 還沒有準備好? 當你準備好時再來找我。";
		close;
	case 2:
		mes "[Missing, the Cleaner]";
		mes "OK! 現在就開始!";
		return;
	}
}

// Instance: Normal Mode :: in_pump1
//============================================================
1@pump,84,105,4	script	Missing, the Cleaner#nf	545,{
	mes "[Missing, the Cleaner]";
	mes "我在這裡清潔做了40年，我從來沒有見過這麼可怕的一個團隊!!";
	next;
	mes "[Missing, the Cleaner]";
	mes "我看到隨處都是海藻，因為你晃動! 所有的排水溝又將被海藻給堵塞!";
	next;
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[Missing, the Cleaner]";
		mes "一旦隊長準備完畢，定居地就會被確定，所以做好心理準備!!";
		close;
	}
	mes "[Missing, the Cleaner]";
	mes "如果你有膽量再來挑戰，我會再給你一次機會! 你將選擇什麼呢!?";
	next;
	switch(select("等等!還沒好!:Okay，讓我們在一次的開始!!")) {
	case 1:
		mes "[Missing, the Cleaner]";
		mes "你仍然很慢! 當你準備好時再來找我.";
		close;
	case 2:
		mes "[Missing, the Cleaner]";
		mes "Ok! 讓我們現在開始!";
		set .@i$, charat(strnpcinfo(2),0);
		enablenpc instance_npcname("Missing, the Cleaner#"+.@i$);
		donpcevent instance_npcname("Missing, the Cleaner#"+.@i$)+"::OnStart";
		disablenpc instance_npcname("Culvert Entrance#"+.@i$);
		disablenpc instance_npcname("Missing, the Cleaner#"+.@i$+"o");
		donpcevent instance_npcname("Monster Hole#"+.@i$)+"::OnClear";
		disablenpc instance_npcname(strnpcinfo(0));
		close;
	}
	close;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@pump,84,105,4	script	Missing, the Cleaner#n	545,{
	callfunc "F_mal_missing";
	donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnStart";
	close;
OnInstanceInit:
	disablenpc instance_npcname("Missing, the Cleaner#n");
	end;
OnStart:
	killmonster instance_mapname("1@pump"),instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead";
	disablenpc instance_npcname("Missing, the Cleaner#n");
	initnpctimer;
	end;
OnAddSeaweed:
	set .@map$, instance_mapname("1@pump");
	areamonster .@map$,55,99,61,105,"受汙染的海藻",2191,1,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead";
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnFail";
	else
		mapannounce .@map$,"受汙染的海藻: "+.@mob_dead_num+"個!滾開",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnMyMobDead:
	end;
OnFail:
	stopnpctimer;
	donpcevent instance_npcname("Monster Hole#n")+"::OnClear";
	set .@map$, instance_mapname("1@pump");
	killmonster .@map$,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead";
	enablenpc instance_npcname("Missing, the Cleaner#nf");
	mapannounce .@map$,"這是什麼!! 海藻居然遍布整個排水溝! 你做完了清潔 ?! 滾蛋!!",bc_map,"0xff88ff",FW_NORMAL,15;
	disablenpc instance_npcname("Missing, the Cleaner#n");
	end;
OnTimer100:
	mapannounce instance_mapname("1@pump"),"5秒後第一個排水溝將被打開。",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer5500:
	mapannounce instance_mapname("1@pump"),"The one who's in charge of cleaning the culvert cannot move or be attacked until the casting is over.",bc_map,"0x00ffcc",FW_NORMAL,15;
	donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer45000:
OnTimer95000:
OnTimer145000:
OnTimer195000:
OnTimer245000:
OnTimer295000:
OnTimer345000:
OnTimer395000:
OnTimer445000:
	mapannounce instance_mapname("1@pump"),"下一個排水溝將在5秒後開啟，請趕快找到下一個排水溝的地點。",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer50000:
OnTimer100000:
OnTimer150000:
	donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer200000:
OnTimer250000:
OnTimer300000:
OnTimer350000:
OnTimer400000:
OnTimer450000:
	set .@mob_dead_num, mobcount(instance_mapname("1@pump"),instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnFail";
	else
		donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer515000:
	set .@map$, instance_mapname("1@pump");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	mapannounce .@map$,"受汙染的海藻: "+.@mob_dead_num+"個!滾開，貓咪將前來檢查清掃結果",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnTimer520000:
	stopnpctimer;
	set .@mob_dead_num, mobcount(instance_mapname("1@pump"),instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnFail";
	else
		donpcevent instance_npcname("Boss Creation#n")+"::OnEnable";
	end;
}

1@pump,1,1,4	script	Monster Hole#n	-1,{
	end;
OnSpawn:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		set .@n,6;
	else if (.@i$ == "h")
		set .@n,10;
	donpcevent instance_npcname("#Culvert_"+.@i$+rand(1,.@n))+"::OnEnable";
	end;
OnClear:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		set .@n,6;
	else if (.@i$ == "h")
		set .@n,10;
	for(set .@i,1; .@i<=.@n; set .@i,.@i+1)
		donpcevent instance_npcname("#Culvert_"+.@i$+.@n)+"::OnClear";
	end;
}

1@pump,36,111,4	script	#Culvert_n1	844,14,14,{ //temporary workaround for ALL_SAMEMAP
	progressbar "0xFFFF00",10;
	stopnpctimer;
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@map$, instance_mapname("1@pump");
	set .@index, atoi(charat(strnpcinfo(2),9));
	switch(.@index) {
		case 1: setarray .@c[0],32,107,40,115; break;
		case 2: setarray .@c[0],64,120,72,128; break;
		case 3: setarray .@c[0],76,110,84,118; break;
		case 4: setarray .@c[0],36,76,44,84; break;
		case 5: setarray .@c[0],71,76,79,84; break;
		case 6: setarray .@c[0],54,97,62,105; break;
	}
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"深海大蟹",2176,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"深海魷魚",2175,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"遠古甲殼類",2174,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"深海貝殼",2178,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"遠古庫克雷",2179,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"深海海螺",2177,rand(1,3),.@label$;
	specialeffect EF_MAPPILLAR2,ALL_SAMEMAP; //currently broken
	getmapxy(.@map$,.@x,.@y,1);
	getpartymember 'party_id,2;
	copyarray .@partymemberaid[0],$@partymemberaid[0],$@partymembercount;
	for(set .@i,0; .@i<$@partymembercount; set .@i,.@i+1) {
		if (attachrid(.@partymemberaid[.@i])) {
			if (strcharinfo(3) == .@map$)
				viewpoint 0,.@x,.@y,.@index,0xFFFF00;
			detachrid;
		}
	}
	initnpctimer;
	end;
OnMyMobDead:
	end;
OnClear:
	stopnpctimer;
	killmonster instance_mapname("1@pump"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;
OnTimer49500:	//OnTimer50000 clashes with the timer in "Missing, the Cleaner#h".
	donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnAddSeaweed";
	donpcevent instance_npcname(strnpcinfo(0))+"::OnClear";
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	specialeffect EF_MAPPILLAR2;
	end;
}
1@pump,68,124,4	duplicate(#Culvert_n1)	#Culvert_n2	844,14,14
1@pump,80,114,4	duplicate(#Culvert_n1)	#Culvert_n3	844,14,14
1@pump,40,80,4	duplicate(#Culvert_n1)	#Culvert_n4	844,14,14
1@pump,75,80,4	duplicate(#Culvert_n1)	#Culvert_n5	844,14,14
1@pump,58,101,4	duplicate(#Culvert_n1)	#Culvert_n6	844,14,14

1@pump,1,1,4	script	Boss Creation#n	-1,{
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	initnpctimer;
	end;
OnTimer100:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		mapannounce instance_mapname("1@pump"),"恩! 這是不是太糟糕了!",bc_map,"0xff88ff",FW_NORMAL,15;
	else if (.@i$ == "h")
		mapannounce instance_mapname("2@pump"),"恩! 你做的很好!!",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer5000:
	mapannounce strnpcinfo(4),"讓我們收拾東西走人... 什麼!!?",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer10000:
	mapannounce strnpcinfo(4),"我感覺到一種奇怪的氣氛!! 不要鬆懈-- 準備好戰鬥!!",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer20000:
	stopnpctimer;
	set .@i$, charat(strnpcinfo(2),0);
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	if (rand(1,100) > 50) {
		if (.@i$ == "n")
			monster instance_mapname("1@pump"),0,0,"怪異腔棘魚",2188,1,.@label$;
		else if (.@i$ == "h")
			monster instance_mapname("2@pump"),0,0,"暴力腔棘魚",2189,1,.@label$;
		mapannounce strnpcinfo(4),"在排水溝的深處傳來巨大噪音。",bc_map,"0x00ffcc",FW_NORMAL,15;
	} else {
		if (.@i$ == "n")
			monster instance_mapname("1@pump"),0,0,"黑暗腔棘魚",2187,1,.@label$;
		else if (.@i$ == "h")
			monster instance_mapname("2@pump"),0,0,"變種腔棘魚",2190,1,.@label$;
		mapannounce strnpcinfo(4),"在排水溝的深處傳來巨大噪音。",bc_map,"0x00ffcc",FW_NORMAL,15;
	}
	end;
OnMyMobDead:
	if (mobcount(strnpcinfo(4),instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		mapannounce strnpcinfo(4),"你已經擊敗了排水溝內的所有怪物，獎勵掉在地上，請找一找。",bc_map,"0xffff00",FW_NORMAL,15;
		set .@i$, charat(strnpcinfo(2),0);
		set .@map$, strnpcinfo(4);
		enablenpc instance_npcname("Culvert Entrance#"+.@i$);
		enablenpc instance_npcname("Missing, the Cleaner#"+.@i$+"o");
		if (.@i$ == "n") {
			for(set .@i,0; .@i<10; set .@i,.@i+1) {
				set .@j, rand(1,6401);
				     if (.@j < 5001) set .@item,12636; //Malang_Sp_Can
				else if (.@j < 5501) set .@item,12615; //Low_Coin_Pocket
				else if (.@j < 6001) set .@item,12621; //Egrade_Pocket
				else if (.@j < 6201) set .@item,12620; //Dgrade_Pocket
				else if (.@j < 6401) set .@item,12623; //High_Weapon_Box
				else continue;
				makeitem .@item,1,.@map$,rand(40,77),rand(87,120);
			}
		} else if (.@i$ == "h") {
			for(set .@i,0; .@i<10; set .@i,.@i+1) {
				set .@j, rand(1,5001);
				     if (.@j < 2001) set .@item,12615; //Low_Coin_Pocket
				else if (.@j < 3001) set .@item,12621; //Egrade_Pocket
				else if (.@j < 4001) set .@item,12620; //Dgrade_Pocket
				else if (.@j < 4501) set .@item,12619; //Cgrade_Pocket
				else if (.@j < 5001) set .@item,12623; //High_Weapon_Box
				else continue;
				makeitem .@item,1,.@map$,rand(40,77),rand(87,120);
			}
		}
	} else
		mapannounce strnpcinfo(4),"目前還是有怪物存活著。",bc_map,"0x00ff99",FW_NORMAL,20;
	end;
}

1@pump,84,105,4	script	Missing, the Cleaner#no	545,{
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n") {
		mes "[Missing, the Cleaner]";
		mes "我非常的驚訝!!";
		next;
		mes "[Missing, the Cleaner]";
		mes "那個古怪的魚似乎是一個腔棘魚，居然會生存在這裡，這是我第一次看見一個人獨自來這裡!";
		next;
		mes "[Missing, the Cleaner]";
		mes "我會負責清理屍體，請離開這裡! 入口位於另一側，請尋找它!";
		next;
	} else if (.@i$ == "h") {
		mes "[Missing, the Cleaner]";
		mes "你擊倒了一個巨大怪物!";
		next;
		mes "[Missing, the Cleaner]";
		mes "你擊倒的是腔棘魚的品種，之前也有人試圖擊倒它，但是都失敗。";
		next;
		mes "[Missing, the Cleaner]";
		mes "我會負責清理屍體，你可以先走一步去領獎勵了!";
		next;
	}
	mes "[Missing, the Cleaner]";
	mes "還有一件事情!別跟任何人說今天所發生的事情!!";
	next;
	mes "[Missing, the Cleaner]";
	mes "如果老闆關閉這個排水溝，我們將會失去工作!";
	close;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@pump,32,100,0	script	Culvert Entrance#n	45,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	set .@map$,strnpcinfo(4);
//	callfunc "instance_reward",.@map$,3,getcharid(0);
	warp "mal_in01",161,32;
	end;
}

//MD_Putmob "1@pump" 0 0 0 0 20 HYDRA 0 0 2
1@pump,1,1,4	script	Hydra#n	-1,{
OnInstanceInit:
	monster strnpcinfo(4),0,0,"海葵",1068,20;
	end;
}

// Instance: Hard Mode :: in_pump2
//============================================================
2@pump,39,88,4	duplicate(Missing, the Cleaner#nf)	Missing, the Cleaner#hf	545

2@pump,39,88,4	script	Missing, the Cleaner#h	545,{
	callfunc "F_mal_missing";
	donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnStart";
	close;
OnStart:
	killmonster instance_mapname("2@pump"),instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead";
	disablenpc instance_npcname("Missing, the Cleaner#h");
	initnpctimer;
	end;
OnAddSeaweed:
	set .@map$, instance_mapname("2@pump");
	areamonster .@map$,75,78,85,88,"受汙染的海藻",2191,1,instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead";
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnFail";
	else
		mapannounce .@map$,"增值的污染海藻: "+.@mob_dead_num+"個!滾開",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnMyMobDead:
	end;
OnFail:
	stopnpctimer;
	donpcevent instance_npcname("Monster Hole#h")+"::OnClear";
	set .@map$, instance_mapname("2@pump");
	killmonster .@map$, instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead";
	enablenpc instance_npcname("Missing, the Cleaner#hf");
	mapannounce .@map$,"這是什麼!!海藻遍布著排水溝!你沒有做好清潔! 滾出去!!",bc_map,"0xff88ff",FW_NORMAL,15;
	disablenpc instance_npcname("Missing, the Cleaner#h");
	end;
OnTimer100:
	mapannounce instance_mapname("2@pump"),"第一個排水溝將在5秒後打開，請找到可以清潔的排水溝，並且開始清理。",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer5500:
	mapannounce instance_mapname("2@pump"),"負責清理排水溝的人將無法攻擊與移動。",bc_map,"0x00ffcc",FW_NORMAL,15;
	donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer35000:
OnTimer75000:
OnTimer115000:
OnTimer155000:
OnTimer195000:
OnTimer235000:
OnTimer275000:
OnTimer315000:
OnTimer355000:
	mapannounce instance_mapname("2@pump"),"下一個排水溝將在5秒後打開，請趕快找到下一個排水溝的位置。",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer40000:
OnTimer80000:
OnTimer120000:
	donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer160000:
OnTimer200000:
OnTimer240000:
OnTimer280000:
OnTimer320000:
OnTimer360000:
	set .@mob_dead_num, mobcount(instance_mapname("2@pump"),instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnFail";
	else
		donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer420000:
	mapannounce instance_mapname("2@pump"),"牠要來檢查了，我們要快點清理這裡。",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnTimer425000:
	stopnpctimer;
	set .@mob_dead_num, mobcount(instance_mapname("2@pump"),instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnFail";
	else
		donpcevent instance_npcname("Boss Creation#h")+"::OnEnable";
	end;
}

2@pump,53,114,4	script	#Culvert_h1	844,14,14,{ //temporary workaround for ALL_SAMEMAP
	progressbar "0xFFFF00",15;
	stopnpctimer;
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@map$, instance_mapname("2@pump");
	set .@index, atoi(substr(strnpcinfo(2),9,getstrlen(strnpcinfo(2))-1));
	switch(.@index) {
		case 1: setarray .@c[0],49,110,57,118; break;
		case 2: setarray .@c[0],75,105,83,113; break;
		case 3: setarray .@c[0],110,110,118,118; break;
		case 4: setarray .@c[0],94,94,102,102; break;
		case 5: setarray .@c[0],58,92,66,100; break;
		case 6: setarray .@c[0],53,66,61,74; break;
		case 7: setarray .@c[0],43,45,51,53; break;
		case 8: setarray .@c[0],77,59,85,67; break;
		case 9: setarray .@c[0],96,70,104,78; break;
		case 10: setarray .@c[0],111,46,119,54; break;
	}
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"遠古海馬",2182,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"遠古異變魚",2181,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"遠古海神",2180,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"突變虎蜥人",2183,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"深海人魚",2184,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"變形釣魚河童",2185,rand(2,3),.@label$;
	specialeffect EF_MAPPILLAR2,ALL_SAMEMAP; //currently broken
	initnpctimer;
	end;
OnMyMobDead:
	end;
OnClear:
	stopnpctimer;
	killmonster instance_mapname("2@pump"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;
OnTimer39500:	//OnTimer40000 clashes with the timer in "Missing, the Cleaner#h".
	donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnAddSeaweed";
	donpcevent instance_npcname(strnpcinfo(0))+"::OnClear";
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	specialeffect EF_MAPPILLAR2;
	end;
}
2@pump,79,109,4	duplicate(#Culvert_h1)	#Culvert_h2	844,14,14
2@pump,114,114,4	duplicate(#Culvert_h1)	#Culvert_h3	844,14,14
2@pump,98,98,4	duplicate(#Culvert_h1)	#Culvert_h4	844,14,14
2@pump,62,96,4	duplicate(#Culvert_h1)	#Culvert_h5	844,14,14
2@pump,57,70,4	duplicate(#Culvert_h1)	#Culvert_h6	844,14,14
2@pump,47,49,4	duplicate(#Culvert_h1)	#Culvert_h7	844,14,14
2@pump,81,63,4	duplicate(#Culvert_h1)	#Culvert_h8	844,14,14
2@pump,100,74,4	duplicate(#Culvert_h1)	#Culvert_h9	844,14,14
2@pump,115,50,4	duplicate(#Culvert_h1)	#Culvert_h10	844,14,14

2@pump,1,1,4	duplicate(Monster Hole#n)	Monster Hole#h	-1
2@pump,1,1,4	duplicate(Boss Creation#n)	Boss Creation#h	-1
2@pump,39,88,4	duplicate(Missing, the Cleaner#no)	Missing, the Cleaner#ho	545
2@pump,38,100,0	duplicate(Culvert Entrance#n)	Culvert Entrance#h	45,3,3

//MD_Putmob "2@pump" 0 0 0 0 20 HYDRA 0 0 2
2@pump,1,1,4	duplicate(Hydra#n)	Hydra#h	-1
