//===== rAthena Script ======================================= 
//= Malangdo Culvert
//===== By: ================================================== 
//= Euphy & 天波工作組
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Clean the culverts and defeat the Coelacanth.
//= Daily and weekly quests are available.
//= Contains a normal mode and hard mode.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//=     Letters "n" and "h" in NPC names indicate difficulty.
//= 1.0a There is no minimum party size on official servers.
//= 1.0b Fixed incorrect use of 'close'. [Joseph]
//= 1.1 Instance system rewrite. [Euphy]
//= 1.2 翻譯成繁體中文及修正部分問題 [天波工作組]
//============================================================ 
1@pump	mapflag	nosave	SavePoint
1@pump	mapflag	nomemo
1@pump	mapflag	noteleport
1@pump	mapflag	partylock
2@pump	mapflag	nosave	SavePoint
2@pump	mapflag	nomemo
2@pump	mapflag	noteleport
2@pump	mapflag	partylock

mal_in01,175,29,2	script	綿綿島排水溝#02	837,{

	mes "----------------------------";	
	mes ">> 副本獎勵清單";
	mes "----------------------------";
	mes "<ITEM>海神之憤怒<INFO>6423</INFO></ITEM>";
	mes "<ITEM>A級錢袋<INFO>12617</INFO></ITEM>";
	mes "<ITEM>B級錢袋<INFO>12618</INFO></ITEM>";
	mes "<ITEM>C級錢袋<INFO>12619</INFO></ITEM>";
	mes "<ITEM>D級錢袋<INFO>12620</INFO></ITEM>";
	mes "<ITEM>E級錢袋<INFO>12621</INFO></ITEM>";
	mes "----------------------------";
	close2;
	cutin "",255;
	end;
}

mal_in01,172,28,2	script	愛寶#mal	561,{
	if (checkweight(1201,1) == 0) {
		mes "你擁有太多物品，請先減少物品後繼續。";	
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你擁有的物品太重，請先減輕重量後繼續。";		
		close;
	}
	if (BaseLevel < 90) {
		mes "[愛寶]";
		mes "你是誰?";
		mes "我們不需要等級過低的人!!";
		close;
	}
	setarray .@quests[0],12271,12272,12273,12274;
	setarray .@names$[0],
		"一般的每日任務","^990000困難的每日任務^000000",
		"一般的每週任務","^990000困難的每週任務^000000";
	set @menu$,"你為什麼在做這個工作?:";
			
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		if (checkquest(.@quests[.@i],PLAYTIME) > -1) {
			set .@status[.@i],2;
			set @menu$, @menu$+"^aaaaaa- 目前沒有新任務 -^000000:";
							
		} else if (.@i%2 && BaseLevel < 140) {
			set .@status[.@i],0;
			set @menu$, @menu$+"^aaaaaa因為等級太低而無法接取此任務^000000:";
							
		} else {
			set .@status[.@i],1;
			set @menu$, @menu$+.@names$[.@i]+":";
		}
	}
	mes "[愛寶]";
	mes "我們橫越了海洋!";	
	mes "我們是排水溝的英雄!";
	mes "我們不原諒以排水溝為攻擊目標的海怪!";
	next;
	set .@i, select(@menu$)-1;
	if (.@i == 0) {
		mes "[愛寶]";
		mes "我們過去是小草的心腹!而我們是他的守護者!";
			
		next;
		mes "[愛寶]";
		mes "儘管如此，當海水沖毀了所有珍貴的資源，我所有的夢想皆被摧毀了...";
		next;
		mes "[愛寶]";
		mes "我們不會原諒入侵我們領土的海怪，我會將他們搭配辣醬一起吃掉!!!";
		next;
		mes "^0000ff愛寶的聲音因生氣而顫抖著，當他提到辣醬的時候他的嘴唇顫抖著。^000000";
			
		close;
	}
	switch(.@status[.@i-1]) {
	case 0:
		mes "[愛寶]";
		mes "這項任務對你來說真的十分艱難，因此我無法告訴你，為什麼你不找另一個呢?";	
		close;
	case 1:
		break;
	case 2:
		mes "[愛寶]";
		mes "我很抱歉，目前這裡還沒有新任務，如果我找到了，我會告訴你!";
		close;
	}
	switch(.@i) {
	case 1: // General Culvert Daily Service
		switch(rand(1,6)) {
			case 1: callsub L_GiveQuest,.@i,12255,12271,"深海大蟹";
			case 2: callsub L_GiveQuest,.@i,12256,12271,"深海魷魚";
			case 3: callsub L_GiveQuest,.@i,12257,12271,"遠古甲殼類";
			case 4: callsub L_GiveQuest,.@i,12258,12271,"深海貝殼";
			case 5: callsub L_GiveQuest,.@i,12259,12271,"遠古庫克雷";
			case 6: callsub L_GiveQuest,.@i,12260,12271,"深海海螺";
		}
	case 2: // Hard Culvert Daily Service
		switch(rand(1,6)) {
			case 1: callsub L_GiveQuest,.@i,12261,12272,"遠古海馬";
			case 2: callsub L_GiveQuest,.@i,12262,12272,"遠古異變魚";
			case 3: callsub L_GiveQuest,.@i,12263,12272,"遠古海神";
			case 4: callsub L_GiveQuest,.@i,12264,12272,"變種虎蜥人";
			case 5: callsub L_GiveQuest,.@i,12265,12272,"深海人魚";
			case 6: callsub L_GiveQuest,.@i,12266,12272,"變形釣魚河童";
		}
	case 3: // General Culvert Weekly Service
		switch(rand(1,2)) {
			case 1: callsub L_GiveQuest,.@i,12267,12273,"古怪腔棘魚";
			case 2: callsub L_GiveQuest,.@i,12268,12273,"黑暗腔棘魚";
		}
	case 4: // Hard Culvert Weekly Service
		switch(rand(1,2)) {
			case 1: callsub L_GiveQuest,.@i,12269,12274,"暴力腔棘魚";
			case 2: callsub L_GiveQuest,.@i,12270,12274,"變種腔棘魚";
		}
	default:
		mes "[愛寶]";
		mes "任務在哪裡?";
		mes "讓我快速的找一下...";
		mes "你必須跟馬黛卡講話。";			
		close;
	}
	end;

// callsub L_GiveQuest,.@i,<quest 1>,<quest 2>,<monster>;
L_GiveQuest:
	setquest getarg(1);
	setquest getarg(2);
	mes "[愛寶]";
	mes "今天，排水溝的英雄們!";
	mes "我們喊出我們的聲音並且訂下消滅 ^0000ff"+getarg(3)+"^000000 的日期!";	
	next;
	mes "[愛寶]";
	switch(getarg(0)) {
	case 1:
	case 2:
		mes "來吧!英雄們，不要畏懼他們!";
		mes "去吧! 因為這是每日限定任務!";
		break;
	case 3:
		mes "我會給你一個禮拜的時間完成這項任務!";
		mes "在普通難度下的排水溝副本完成這些任務!";
		break;
	case 4:
		mes "我會給你一個禮拜的時間完成這項任務!";
		mes "在困難難度下的排水溝副本完成這些任務!";	
		break;
	}
	next;
	mes "你收到一項消滅 ^005500"+getarg(3)+"^000000 的請求。";
	mes "如果你需要更多訊息，請點擊訊息框。";
	close;
}

mal_in01,172,26,2	script	馬黛卡#mal	544,{
	if (checkweight(1201,1) == 0) {
		mes "你擁有太多物品，請先減少物品後繼續。";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你擁有的物品太重，請先減輕重量後繼續。";
		close;
	}
	if (BaseLevel < 90) {
		mes "[馬黛卡]";
		mes "讓我們吃點可以烤或水煮的海怪!";
		next;
		mes "[馬黛卡]";
		mes "你是誰?";
		mes "你如此的慢，連海草都能殺了你!";	
		close;
	}
	mes "[馬黛卡]";
	mes "你是那位接受我大哥愛寶請求的人，這是個艱難的任務!";
		
	next;
	set .@i, select("你在這裡幹嘛 ?:一般的每日任務:困難的每日任務:一般的每週任務:困難的每週任務")-1;
	if (.@i == 0) {
		mes "[馬黛卡]";
		mes "我是來幫助我大哥愛寶的!";
		next;
		mes "[馬黛卡]";
		mes "我們提供珍貴的物品給那些在排水溝清除邪惡海怪的勇者!";	
		next;
		mes "[馬黛卡]";
		mes "我們提供A~B等級的錢幣當做每日任務的獎賞，並且提供稀有的海神之憤怒當做每週任務的獎賞。";
		next;
		mes "[馬黛卡]";
		mes "如果你接大哥愛寶的請求，我會常常看到你。";
		mes "所以讓我們保持密切的關係吧!";
		close;
	}
	mes "[馬黛卡]";
	mes "讓我檢查一下你是否有任何過期的任務..";
	next;
	specialeffect2 EF_SPHERE;
	progressbar "0xFFFF00",3;
	specialeffect2 EF_STEAL;
	switch(.@i) {
	case 1: // General Culvert Daily Service
		if (checkquest(12271,PLAYTIME) == 2)
			callsub L_EraseQuest,12255,12256,12257,12258,12259,12260,12271;
		else {
			// Reward: 2x B Grade Coin
			callsub L_CheckQuest,12255,"深海大蟹",6419,2;
			callsub L_CheckQuest,12256,"深海魷魚",6419,2;
			callsub L_CheckQuest,12257,"遠古甲殼類",6419,2;
			callsub L_CheckQuest,12258,"深海貝殼",6419,2;
			callsub L_CheckQuest,12259,"遠古庫克雷",6419,2;
			callsub L_CheckQuest,12260,"深海海螺",6419,2;
		}
		break;
	case 2: // Hard Culvert Daily Service
		if (checkquest(12272,PLAYTIME) == 2)
			callsub L_EraseQuest,12261,12262,12263,12264,12265,12266,12272;
		else {
			// Reward: 1x A Grade Coin
			callsub L_CheckQuest,12261,"遠古海馬",6418,1;
			callsub L_CheckQuest,12262,"遠古異變魚",6418,1;
			callsub L_CheckQuest,12263,"遠古海神",6418,1;
			callsub L_CheckQuest,12264,"變種虎蜥人",6418,1;
			callsub L_CheckQuest,12265,"深海人魚",6418,1;
			callsub L_CheckQuest,12266,"變形釣魚河童",6418,1;
		}
		break;
	case 3: // General Culvert Weekly Service
		if (checkquest(12273,PLAYTIME) == 2)
			callsub L_EraseQuest,12267,12268,12273;
		else {
			// Reward: 1x Sea God's Wrath
			callsub L_CheckQuest,12267,"古怪腔棘魚",6423,1;
			callsub L_CheckQuest,12268,"黑暗腔棘魚",6423,1;
		}
		break;
	case 4: // Hard Culvert Weekly Service
		if (checkquest(12274,PLAYTIME) == 2)
			callsub L_EraseQuest,12269,12270,12274;
		else {
			// Reward: 5x Sea God's Wrath
			callsub L_CheckQuest,12269,"暴力腔棘魚",6423,5;
			callsub L_CheckQuest,12270,"變種腔棘魚",6423,5;
		}
		break;
	default:
		mes "[馬黛卡]";
		mes "發生了錯誤，請再次檢查!";
		close;
	}
	mes "[馬黛卡]";
	mes "我沒看到任何完成的任務!";
	close;

L_EraseQuest:
	for(set .@j,0; .@j<getargcount(); set .@j,.@j+1) {
		if (checkquest(getarg(.@j)) > -1)
			erasequest getarg(.@j);
	}
	mes "[馬黛卡]";
	mes "摁...我很抱歉，你超過了時間限制，所以我無法給你任何報酬。";
	close;

// callsub L_CheckQuest,<quest ID>,<monster>,<reward item ID>,<reward item amount>;
L_CheckQuest:
	if (checkquest(getarg(0),HUNTING) == 2) {
		mes "[馬黛卡]";
		mes "你必須懲罰 ^0000ff"+getarg(1)+"^000000 !";
		mes "這是你的報酬!";
		erasequest getarg(0);
		specialeffect2 EF_STEAL;
		getitem getarg(2),getarg(3);
		close;
	}
	return;
}

// Instance Creation
//============================================================
mal_in01.gat,168,34,4	script	清潔員小草	545,{
	if (BaseLevel < 140) {
		mes "[清潔員小草]";
		mes "^770099你最少需達140級才可進入。^000000";
		close;
	}

	if (in_canal_n == 0) {
		mes "[清潔員小草]";
		mes "(顫抖著)";
			
		next;
		mes "^660066這隻貓看起來狀況不太好，他輕微的顫抖著。^000000";
				
			
		next;
		if(select("寵物貓咪:你還好嗎?") == 1) {
				
			mes "[清潔員小草]";
			mes "你是在跟我講話嗎?!";
			next;
			mes "^660066貓咪顫抖著抬頭看你。^000000";	
			close;
		}
		mes "[清潔員小草]";
		mes "哈啾!";	
		next;
		mes "[清潔員小草]";
		mes "我們在清理排水溝的過程中";
		mes "發生了許多事情";
		mes "但是貓咪必須去做...";
		next;
		select("你在清理什麼排水溝?");		
		mes "[清潔員小草]";
		mes "這或許對一個外人來說十分不熟悉，";
		mes "但事實上這個地方被建造來保存紀念品與奢侈品。";
		next;
		mes "[清潔員小草]";
		mes "在一陣巨大的震動後，";
		mes "在這座島上物品四散，";
		mes "這就是為何我成為了一名清潔工，";
		mes "儘管我習慣當一名管理員。";
		next;
		mes "^660066這隻貓咪看起來正在哭泣...^000000";	
		next;
		mes "[清潔員小草]";
		mes "這邊有個通往地下室的排水溝";	
		mes "這裡有許多傷害我的壞人";
		mes "我曾一天試著反抗他們許多次!";
		next;
		mes "[清潔員小草]";
		mes "你碰到了我的毛!";
		mes "我的毛掉的到處都是...";
		mes "都是那些傢伙的錯~!";
		next;
		mes "^660066你剛發現貓咪的毛掉在某個地方.^000000";
		next;
		select("為什麼你不乾脆放棄!");
		mes "[清潔員小草]";
		mes "我仍必須維持生計，不是嗎?";
		next;
		mes "[清潔員小草]";
		mes "有時候我能夠拿到幸運盒，甚至是更好的罐頭...";			
		next;
		if(select("好吧!:如果可以的話我會幫忙的...") == 1) {
			mes "[清潔員小草]";
			mes "謝謝你。 我現在要回去倒水了!";
			close;
		}
		mes "[清潔員小草]";
		mes "(降低他的音量)";
		mes "如果你真的想要幫忙，就靠近點。";
		next;
		select("(靠近)");
				
		mes "[清潔員小草]";
		mes "不是每個人在這都能成為助手";
		mes "但是在選擇的時候我能夠幫忙...";
		next;
		mes "[清潔員小草]";
		mes "如果你想要下去那裡，讓我拿到這個密碼。";	
		next;
		mes "^660066貓咪在備忘錄上寫下了密碼給你。^000000";
		next;
		select("打開備忘錄.");
		mes "^660000亞拉岡羞辱我.^000000";
		next;
		mes "[清潔員小草]";
		mes "你必須與朋友一起到這裡，";	
		mes "因為你不能夠一個人進去。";	
		mes "此外，密碼必須來自隊伍隊長。";
		set in_canal_n,1;
		close;
	}
	
	if (!rentalcountitem(6436)) {
		mes "[清潔員小草]";
		mes "你看起來沒有海神護身符…";
		mes "我沒辦法讓你進入。";
		close;
	}
				
	set .@party_id,getcharid(1);
	set .@md_name$,"排水溝";
	if (!.@party_id) {
		mes "[清潔員小草]";
		mes "^0000ff你必須先擁有一個隊伍或加入大於一人的隊伍再回來。^000000";
		close;
	}
	set .@playtime, checkquest(12254,PLAYTIME);
	if (.@playtime == -1) {
		if (getcharid(0) == getpartyleader(.@party_id,2)) {
			mes "[清潔員小草]";
			mes "怎麼了？";
			mes "如果你是隊長的話請告訴我密碼。";
			next;
			switch(select("創建排水溝:進入排水溝")) {
			//callfunc "PartyIPCheck";
			case 1:
				if (instance_create(.@md_name$) < 0) {
					mes "[清潔員小草]";
					mes "隊伍名稱： "+getpartyname(.@party_id);
					mes "隊伍隊長： "+strcharinfo(0);
					mes "^0000ff"+.@md_name$+"^000000 - 創建失敗!";
					close;
				}
				announce "★ 副本公告 ★ 隊伍 [ "+getpartyname(getcharid(1)) +" ] 準備挑戰【 排水溝 】副本！",15,0x33ea91;
				mes "[清潔員小草]";
				mes "^3333FF"+.@md_name$+"^000000 - 創建中";
				mes "在創建之後，你必須選擇進入排水溝。";
				close;
			case 2:
				callsub L_Enter,0;
			}
		}
		if(select("進入排水溝:取消") == 2)
			end;
		callsub L_Enter,1;
	} else if (.@playtime == 0 || .@playtime == 1) {
		mes "[清潔員小草]";
		mes "如果大門已經開啟，你可以進入排水溝。";	
		next;
		if(select("進入排水溝:取消") == 2)
			close;
		callsub L_Enter,0;
	} else if (.@playtime == 2) {
		mes "[清潔員小草]";
		mes "^0000ff通往排水溝的大門又再度開啟^000000";		
		erasequest 12254;
		close;
	}
	end;
L_Enter:
	set .@playtime, checkquest(12254,PLAYTIME);
	if (.@playtime == 0 || .@playtime == 1) {
		mes "[清潔員小草]";
		mes "現在還不需要清潔，";
		mes "請你晚點再來吧！";
		close;
	}
	//callfunc "PartyIPCheck";
	set .@md_name$,"排水溝";
	switch(instance_enter(.@md_name$)) {
	case 3:
		mes "[清潔員小草]";
		mes "發生了未知的錯誤。";	
		close;
	case 2:
		mes "[清潔員小草]";
		mes "通往排水溝的大門仍是關閉的。";	
		mes "你必須等待，直到你能夠進入，或找到能夠遠組的隊伍隊長。";
		close;
	case 1:
		mes "[清潔員小草]";
		mes "只有隊伍成員能夠參加。";
		close;
	case 0:
		mapannounce strnpcinfo(4),"隊伍 " + getpartyname(getcharid(1)) + " 裡的勇者 " + strcharinfo(0) + " 開始進行【"+.@md_name$+"】副本了",bc_map,"0x00ff99";
		if (checkquest(12254) == -1) setquest 12254;
		//set Culvert_Lasttime,gettimetick(2);
		if (getarg(0) == 0) close;
		else end;
	}
	
}

// Instance: Common Scripts
//============================================================
1@pump,63,100,4	script	清潔員小草#0	545,{
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[清潔員小草]";
		mes "我正與你的隊長談話中，請稍等。";			
		close;
	}
	mes "[清潔員小草]";
	mes "這將是你與你的朋友們必須清理的排水溝!";	
	next;
	mes "[清潔員小草]";
	mes "我會先開門，此外，你知道如何戰鬥嗎?";
		
	next;
	switch(select("我很擅長輕鬆的工作:我知道如何戰鬥:^ffffff給我鑽石^000000")) {				
	case 1:
		mes "[清潔員小草]";
		mes "好，我會讓你一如往常般清理排水溝！";
		mes "我會在右手邊的轉角準備，請跟我來！";	
		next;
		if(select("等一下!:好的~") == 1) {
			mes "[清潔員小草]";
			mes "還沒準備好嗎？當你準備好的時候再跟我講。";
			close;
		}
		set 'party_id,getcharid(1);
		mapannounce instance_mapname("1@pump"),"小草 : 往三點鐘方向移動，並等待我的指示。",bc_map,"0xff88ff",FW_NORMAL,15;
		disablenpc instance_npcname("清潔員小草#0");
		enablenpc instance_npcname("清潔員小草#n");
		close;
	case 2:
		if (BaseLevel < 140) {
			mes "[清潔員小草]";
			mes "小草，感謝你！告訴我真相吧！";
			close;
		}
		mes "[清潔員小草]";
		mes "你有些戰鬥技能？那麼這裡有個地方是我無法清理的...你怎麼不去那邊呢?!";	
		next;
		mes "[清潔員小草]";
		mes "我必須告訴你，距離上一次清理那個地方有很長一段時間了，所以任何低於140等的玩家禁止進入！你確定你仍想前往嗎？";
		next;
		if(select("等一下!:我準備好了") == 1) {
			mes "[清潔員小草]";
			mes "嗯？當你準備好的時候再跟我講。";
			close;
		}
		set 'party_id,getcharid(1);
		mapannounce instance_mapname("1@pump"),"小草 : 跟著我走，我會在三點鐘方向開一個門。",bc_map,"0xff88ff",FW_NORMAL,15;
		disablenpc instance_npcname("清潔員小草#0");
		enablenpc instance_npcname("Culvert Entrance#i");
		close;
	case 3:
		mes "[清潔員小草]";
		mes "我告訴你不要跟我玩了~!";
		close;
	}
	end;
}

1@pump,84,105,0	script	Culvert Entrance#i	45,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("Culvert Entrance#i");
	end;
OnTouch:
	if (BaseLevel >= 140)
		warp instance_mapname("2@pump"),38,88;
	else
		warp instance_mapname("1@pump"),74,105;
	end;
}

function	script	F_mal_missing	{
	mes "[清潔員小草]";
	mes "我會告訴你如何在短時間內清理，你能夠看見你周圍的排水溝嗎？";
		
	next;
	if(select("什麼排水溝？:我能看見！") == 1) {
		mes "[清潔員小草]";
		mes "這是你第一次看到排水溝嗎？你將會在這個區域看見許多被掩埋的機器，你可以四處晃晃再回來！";
 		close;
	}
	mes "[清潔員小草]";
	mes "沒錯！這些排水溝是十分重要的！為了避免讓海草堵住這些排水溝，我們必須清理，這就是我們清潔工的工作！";
	next;
	mes "[清潔員小草]";
	mes "小心！有的怪物會在你清理排水溝的時候跟著你！但是不要驚動他們！";
	next;
	mes "[清潔員小草]";
	mes "還有...絕對不要讓水草疊到6層！最多只能5層！如果到了6層，我會把你拖出那裡！！";
	next;
	mes "[清潔員小草]";
	mes "我不在乎你有沒有朋友的幫忙，我只希望你能夠清理所有的排水溝！別忘了，我會看著你並且給你指示，照著我說的做就對了！！";
	next;
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[清潔員小草]";
		mes "當你的隊長準備完成，就會開始，所以隨時準備開始吧！！";
			
		close;
	}
	mes "[清潔員小草]";
	mes "你現在準備好去清理了嗎？";
		
	next;
	switch(select("等一下:我們出發吧")) {
	case 1:
		mes "[清潔員小草]";
		mes "還沒準備好嗎？當你準備好的時候再跟我講！";
		close;
	case 2:
		mes "[清潔員小草]";
		mes "好了！現在開始！";
		return;
	}
}

// Instance: Normal Mode
//============================================================
1@pump,84,105,4	script	清潔員小草#nf	545,{
	mes "[清潔員小草]";
	mes "我做了40年的清理工作，但我也沒有看過如此糟糕的隊伍！！";
		
	next;
	mes "[清潔員小草]";
	mes "因為你在閒晃，到處都是水草，所有的排水溝都被堵住了！！";
		
	next;
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[清潔員小草]";
		mes "當你的隊長準備完成，準備好進入排水溝吧！！";
			
		close;
	}
	mes "[清潔員小草]";
	mes "如果你有膽再挑戰一次，我會再給你一次機會！你是否要再進行一次？";
		
	next;
	switch(select("等一下:讓我們再一次")) {
	case 1:
		mes "[清潔員小草]";
		mes "你還是很慢！當你準備好的時候再跟我講。";
		close;
	case 2:
		mes "[清潔員小草]";
		mes "好！讓我們開始吧！";
		set .@i$, charat(strnpcinfo(2),0);
		enablenpc instance_npcname("清潔員小草#"+.@i$);
		donpcevent instance_npcname("清潔員小草#"+.@i$)+"::OnStart";
		disablenpc instance_npcname("Culvert Entrance#"+.@i$);
		disablenpc instance_npcname("清潔員小草#"+.@i$+"o");
		donpcevent instance_npcname("Monster Hole#"+.@i$)+"::OnClear";
		disablenpc instance_npcname(strnpcinfo(0));
		close;
	}
	close;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@pump,84,105,4	script	清潔員小草#n	545,{
	callfunc "F_mal_missing";
	donpcevent instance_npcname("清潔員小草#n")+"::OnStart";
	close;
OnInstanceInit:
	disablenpc instance_npcname("清潔員小草#n");
	end;
OnStart:
	killmonster instance_mapname("1@pump"),instance_npcname("清潔員小草#n")+"::OnMyMobDead";
	disablenpc instance_npcname("清潔員小草#n");
	initnpctimer;
	end;
OnAddSeaweed:
	set .@map$, instance_mapname("1@pump");
	areamonster .@map$,55,99,61,105,"被汙染的海草",2191,1,instance_npcname("清潔員小草#n")+"::OnMyMobDead";
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("清潔員小草#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("清潔員小草#n")+"::OnFail";
	else
		mapannounce .@map$,"被汙染的海草 : "+.@mob_dead_num+" 個水草",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnMyMobDead:
	end;
OnFail:
	stopnpctimer;
	donpcevent instance_npcname("Monster Hole#n")+"::OnClear";
	set .@map$, instance_mapname("1@pump");
	killmonster .@map$,instance_npcname("清潔員小草#n")+"::OnMyMobDead";
	enablenpc instance_npcname("清潔員小草#nf");
	mapannounce .@map$,"這是什麼！！海藻遍布排水溝！你清除完成後就快點出去！",bc_map,"0xff88ff",FW_NORMAL,15;
	disablenpc instance_npcname("清潔員小草#n");
	end;
OnTimer100:
	mapannounce instance_mapname("1@pump"),"第一個排水溝將會在5秒後開啟，清潔工能夠找到且開啟排水孔並且開始清理",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer5500:
	mapannounce instance_mapname("1@pump"),"負責清理排水溝的負責人直到工作完成前不能被攻擊及移動",bc_map,"0x00ffcc",FW_NORMAL,15;
	donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer45000:
OnTimer95000:
OnTimer145000:
OnTimer195000:
OnTimer245000:
OnTimer295000:
OnTimer345000:
OnTimer395000:
OnTimer445000:
	mapannounce instance_mapname("1@pump"),"下一個排水溝將會在5秒後開啟，請趕快尋找排水溝的位置",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer50000:
OnTimer100000:
OnTimer150000:
	donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer200000:
OnTimer250000:
OnTimer300000:
OnTimer350000:
OnTimer400000:
OnTimer450000:
	set .@mob_dead_num, mobcount(instance_mapname("1@pump"),instance_npcname("清潔員小草#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("清潔員小草#n")+"::OnFail";
	else
		donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer515000:
	set .@map$, instance_mapname("1@pump");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("清潔員小草#n")+"::OnMyMobDead");
	mapannounce .@map$,"被汙染的海草 : "+.@mob_dead_num+" 株，小草將會過來檢查清理的成果",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnTimer520000:
	stopnpctimer;
	set .@mob_dead_num, mobcount(instance_mapname("1@pump"),instance_npcname("清潔員小草#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("清潔員小草#n")+"::OnFail";
	else
		donpcevent instance_npcname("Boss Creation#n")+"::OnEnable";
	end;
}

1@pump,1,1,4	script	Monster Hole#n	-1,{
	end;
OnSpawn:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		set .@n,6;
	else if (.@i$ == "h")
		set .@n,10;
	donpcevent instance_npcname("#Culvert_"+.@i$+rand(1,.@n))+"::OnEnable";
	end;
OnClear:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		set .@n,6;
	else if (.@i$ == "h")
		set .@n,10;
	for(set .@i,1; .@i<=.@n; set .@i,.@i+1)
		donpcevent instance_npcname("#Culvert_"+.@i$+.@n)+"::OnClear";
	end;
}

1@pump,36,111,4	script	#Culvert_n1	844,14,14,{ //temporary workaround for ALL_SAMEMAP
	progressbar "0xFFFF00",10;
	stopnpctimer;
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@map$, instance_mapname("1@pump");
	set .@index, atoi(charat(strnpcinfo(2),9));
	switch(.@index) {
		case 1: setarray .@c[0],32,107,40,115; break;
		case 2: setarray .@c[0],64,120,72,128; break;
		case 3: setarray .@c[0],76,110,84,118; break;
		case 4: setarray .@c[0],36,76,44,84; break;
		case 5: setarray .@c[0],71,76,79,84; break;
		case 6: setarray .@c[0],54,97,62,105; break;
	}
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2176,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2175,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2174,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2178,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2179,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2177,rand(1,3),.@label$;
	specialeffect EF_MAPPILLAR2,ALL_SAMEMAP; //currently broken
	getmapxy(.@map$,.@x,.@y,1);
	getpartymember 'party_id,2;
	copyarray .@partymemberaid[0],$@partymemberaid[0],$@partymembercount;
	for(set .@i,0; .@i<$@partymembercount; set .@i,.@i+1) {
		if (attachrid(.@partymemberaid[.@i])) {
			if (strcharinfo(3) == .@map$)
				viewpoint 0,.@x,.@y,.@index,0xFFFF00;
			detachrid;
		}
	}
	initnpctimer;
	end;
OnMyMobDead:
	end;
OnClear:
	stopnpctimer;
	killmonster instance_mapname("1@pump"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;
OnTimer49500:
	donpcevent instance_npcname("清潔員小草#n")+"::OnAddSeaweed";
	donpcevent instance_npcname(strnpcinfo(0))+"::OnClear";
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	specialeffect EF_MAPPILLAR2;
	end;
}
1@pump,68,124,4	duplicate(#Culvert_n1)	#Culvert_n2	844,14,14
1@pump,80,114,4	duplicate(#Culvert_n1)	#Culvert_n3	844,14,14
1@pump,40,80,4	duplicate(#Culvert_n1)	#Culvert_n4	844,14,14
1@pump,75,80,4	duplicate(#Culvert_n1)	#Culvert_n5	844,14,14
1@pump,58,101,4	duplicate(#Culvert_n1)	#Culvert_n6	844,14,14

1@pump,1,1,4	script	Boss Creation#n	-1,{
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	initnpctimer;
	end;
OnTimer100:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		mapannounce instance_mapname("1@pump"),"摁...這真是可惜!",bc_map,"0xff88ff",FW_NORMAL,15;
	else if (.@i$ == "h")
		mapannounce instance_mapname("2@pump"),"摁...你們真是厲害!!",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer5000:
	mapannounce strnpcinfo(4),"讓我們打包離開吧...什麼!!?",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer10000:
	mapannounce strnpcinfo(4),"我感覺到某種陌生的怪物!! 別輕敵~~準備戰鬥!!",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer20000:
	stopnpctimer;
	set .@i$, charat(strnpcinfo(2),0);
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	if (rand(1,100) > 50) {
		if (.@i$ == "n")
			monster instance_mapname("1@pump"),0,0,"怪異腔棘魚",2188,1,.@label$;
		else if (.@i$ == "h")
			monster instance_mapname("2@pump"),0,0,"變種腔棘魚",2189,1,.@label$;
		mapannounce strnpcinfo(4),"有隻龐大的生物在這個深淵內發出巨大的聲響",bc_map,"0x00ffcc",FW_NORMAL,15;
	} else {
		if (.@i$ == "n")
			monster instance_mapname("1@pump"),0,0,"黑暗腔棘魚",2187,1,.@label$;
		else if (.@i$ == "h")
			monster instance_mapname("2@pump"),0,0,"暴力腔棘魚",2190,1,.@label$;
		mapannounce strnpcinfo(4),"有個野蠻的怪物隨著巨大的聲響從深淵中出現",bc_map,"0x00ffcc",FW_NORMAL,15;
	}
	end;
OnMyMobDead:
	if (mobcount(strnpcinfo(4),instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		mapannounce strnpcinfo(4),"你已經成功消滅了排水溝中的所有怪物。",bc_map,"0xffff00",FW_NORMAL,15;
		set .@i$, charat(strnpcinfo(2),0);
		set .@map$, strnpcinfo(4);
		enablenpc instance_npcname("Culvert Entrance#"+.@i$);
		if (.@i$ == "n") {
			for(set .@i,0; .@i<10; set .@i,.@i+1) {
				set .@j, rand(1,6401);
				     if (.@j < 5001) set .@item,12636; //Malang_Sp_Can
				else if (.@j < 5501) set .@item,12615; //Low_Coin_Pocket
				else if (.@j < 6001) set .@item,12621; //Egrade_Pocket
				else if (.@j < 6201) set .@item,12620; //Dgrade_Pocket
				else if (.@j < 6401) set .@item,12623; //High_Weapon_Box
				else continue;
				makeitem .@item,1,.@map$,rand(40,77),rand(87,120);
			}
		} else if (.@i$ == "h") {
			for(set .@i,0; .@i<10; set .@i,.@i+1) {
				set .@j, rand(1,5001);
				     if (.@j < 2001) set .@item,12615; //Low_Coin_Pocket
				else if (.@j < 3001) set .@item,12621; //Egrade_Pocket
				else if (.@j < 4001) set .@item,12620; //Dgrade_Pocket
				else if (.@j < 4501) set .@item,12619; //Cgrade_Pocket
				else if (.@j < 5001) set .@item,12623; //High_Weapon_Box
				else continue;
				makeitem .@item,1,.@map$,rand(40,77),rand(87,120);
			}
		}
		//callfunc "F_GetInstancePrize", .@map$, 'party_id;
	} else
		mapannounce strnpcinfo(4),"這裡仍有怪物存活著",bc_map,"0x00ff99",FW_NORMAL,20;
	end;
}

1@pump,84,105,4	script	清潔員小草#no	545,{
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n") {
		mes "[清潔員小草]";
		mes "我十分驚訝！！";
		next;
		mes "[清潔員小草]";
		mes "這可笑的魚看起來是在這附近生活的腔棘魚，這是我第一次看到牠獨自來到這裡！";
		next;
		mes "[清潔員小草]";
		mes "我會清理剩下的部分，快打包離開！";
		mes "出口在另一邊，找一下吧！";
			
		next;
	} else if (.@i$ == "h") {
		mes "[清潔員小草]";
		mes "你消滅了一隻強大的怪物！";
		next;
		mes "[清潔員小草]";
		mes "這隻怪物是腔棘魚的變種，其他人想消滅時都失敗了。";	
		next;
		mes "[清潔員小草]";
		mes "我會清理剩下的部分，你去領取物品並且離開吧！";
		next;
	}
	mes "[清潔員小草]";
	mes "還有一件事！千萬不要跟任何人講你今天在這所看到東西！！";
		
	next;
	mes "[清潔員小草]";
	mes "如果湯馬士將這關閉了，我們都會丟了工作！";	
	close;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@pump,32,100,0	script	Culvert Entrance#n	45,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	warp "egg1",214,167;
	end;
}

//MD_Putmob "1@pump" 0 0 0 0 20 HYDRA 0 0 2
1@pump,1,1,4	script	Hydra#n	-1,{
OnInstanceInit:
	monster strnpcinfo(4),0,0,"海葵",1068,20;
	end;
}

// Instance: Hard Mode
//============================================================
2@pump,39,88,4	duplicate(清潔員小草#nf)	清潔員小草#hf	545

2@pump,39,88,4	script	清潔員小草#h	545,{
	callfunc "F_mal_missing";
	donpcevent instance_npcname("清潔員小草#h")+"::OnStart";
	close;
OnStart:
	killmonster instance_mapname("2@pump"),instance_npcname("清潔員小草#h")+"::OnMyMobDead";
	disablenpc instance_npcname("清潔員小草#h");
	initnpctimer;
	end;
OnAddSeaweed:
	set .@map$, instance_mapname("2@pump");
	areamonster .@map$,75,78,85,88,"被汙染的海草",2191,1,instance_npcname("清潔員小草#h")+"::OnMyMobDead";
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("清潔員小草#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("清潔員小草#h")+"::OnFail";
	else
		mapannounce .@map$,"被汙染的海草： "+.@mob_dead_num+" 株",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnMyMobDead:
	end;
OnFail:
	stopnpctimer;
	donpcevent instance_npcname("Monster Hole#h")+"::OnClear";
	set .@map$, instance_mapname("2@pump");
	killmonster .@map$, instance_npcname("清潔員小草#h")+"::OnMyMobDead";
	enablenpc instance_npcname("清潔員小草#hf");
	mapannounce .@map$,"這是什麼!! 海草蔓延了整個排水溝! 快點離開!!",bc_map,"0xff88ff",FW_NORMAL,15;
	disablenpc instance_npcname("清潔員小草#h");
	end;
OnTimer100:
	mapannounce instance_mapname("2@pump"),"第一個排水溝將會在5秒後開啟，清潔工能夠找到排水孔並點擊開始清理。",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer5500:
	mapannounce instance_mapname("2@pump"),"清理排水溝的負責人在過程中不能移動或者被攻擊",bc_map,"0x00ffcc",FW_NORMAL,15;
	donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer35000:
OnTimer75000:
OnTimer115000:
OnTimer155000:
OnTimer195000:
OnTimer235000:
OnTimer275000:
OnTimer315000:
OnTimer355000:
	mapannounce instance_mapname("2@pump"),"下個排水溝將會在5秒後開啟，請趕快尋找下個排水溝的位置。",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer40000:
OnTimer80000:
OnTimer120000:
	donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer160000:
OnTimer200000:
OnTimer240000:
OnTimer280000:
OnTimer320000:
OnTimer360000:
	set .@mob_dead_num, mobcount(instance_mapname("2@pump"),instance_npcname("清潔員小草#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("清潔員小草#h")+"::OnFail";
	else
		donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer420000:
	mapannounce instance_mapname("2@pump"),"小草將會來檢查清理的結果，我們能夠清理這附近嗎？",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnTimer425000:
	stopnpctimer;
	set .@mob_dead_num, mobcount(instance_mapname("2@pump"),instance_npcname("清潔員小草#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("清潔員小草#h")+"::OnFail";
	else
		donpcevent instance_npcname("Boss Creation#h")+"::OnEnable";
	end;
}

2@pump,53,114,4	script	#Culvert_h1	844,14,14,{ //temporary workaround for ALL_SAMEMAP
	progressbar "0xFFFF00",10;
	stopnpctimer;
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@map$, instance_mapname("2@pump");
	set .@index, atoi(substr(strnpcinfo(2),9,getstrlen(strnpcinfo(2))-1));
	switch(.@index) {
		case 1: setarray .@c[0],49,110,57,118; break;
		case 2: setarray .@c[0],75,105,83,113; break;
		case 3: setarray .@c[0],110,110,118,118; break;
		case 4: setarray .@c[0],94,94,102,102; break;
		case 5: setarray .@c[0],58,92,66,100; break;
		case 6: setarray .@c[0],53,66,61,74; break;
		case 7: setarray .@c[0],43,45,51,53; break;
		case 8: setarray .@c[0],77,59,85,67; break;
		case 9: setarray .@c[0],96,70,104,78; break;
		case 10: setarray .@c[0],111,46,119,54; break;
	}
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2182,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2181,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2180,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2183,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2184,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"--ja--",2185,rand(2,3),.@label$;
	specialeffect EF_MAPPILLAR2,ALL_SAMEMAP; //currently broken
	initnpctimer;
	end;
OnMyMobDead:
	end;
OnClear:
	stopnpctimer;
	killmonster instance_mapname("2@pump"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;
OnTimer39500:	
	donpcevent instance_npcname("清潔員小草#h")+"::OnAddSeaweed";
	donpcevent instance_npcname(strnpcinfo(0))+"::OnClear";
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	specialeffect EF_MAPPILLAR2;
	end;
}
2@pump,79,109,4	duplicate(#Culvert_h1)	#Culvert_h2	844,14,14
2@pump,114,114,4	duplicate(#Culvert_h1)	#Culvert_h3	844,14,14
2@pump,98,98,4	duplicate(#Culvert_h1)	#Culvert_h4	844,14,14
2@pump,62,96,4	duplicate(#Culvert_h1)	#Culvert_h5	844,14,14
2@pump,57,70,4	duplicate(#Culvert_h1)	#Culvert_h6	844,14,14
2@pump,47,49,4	duplicate(#Culvert_h1)	#Culvert_h7	844,14,14
2@pump,81,63,4	duplicate(#Culvert_h1)	#Culvert_h8	844,14,14
2@pump,100,74,4	duplicate(#Culvert_h1)	#Culvert_h9	844,14,14
2@pump,115,50,4	duplicate(#Culvert_h1)	#Culvert_h10	844,14,14

2@pump,1,1,4	duplicate(Monster Hole#n)	Monster Hole#h	-1
2@pump,1,1,4	duplicate(Boss Creation#n)	Boss Creation#h	-1
2@pump,39,88,4	duplicate(清潔員小草#no)	清潔員小草#ho	545
2@pump,38,100,0	duplicate(Culvert Entrance#n)	Culvert Entrance#h	52,3,3

//MD_Putmob "2@pump" 0 0 0 0 20 HYDRA 0 0 2
2@pump,1,1,4	duplicate(Hydra#n)	Hydra#h	-1
