1@os_a	mapflag	noteleport
1@os_a	mapflag	monster_noteleport
1@os_a	mapflag	nosave	SavePoint
1@os_a	mapflag	nomemo
1@os_a	mapflag	nobranch
1@os_a	mapflag	noicewall
1@os_a	mapflag	restricted	6

sp_cor,165,55,5	script	再探奧斯#02	837,{

	mes "----------------------------";	
	mes ">> 副本獎勵清單";
	mes "----------------------------";
	mes "<ITEM>核心元件<INFO>25723</INFO></ITEM>";
	mes "<ITEM>神秘零件<INFO>25669</INFO></ITEM>";
	mes "<ITEM>加農刺劍-OS<INFO>13493</INFO></ITEM>";
	mes "<ITEM>綠光巨劍-OS<INFO>21047</INFO></ITEM>";
	mes "<ITEM>紅魔鬼手杖-OS<INFO>26151</INFO></ITEM>";
	mes "<ITEM>殺人電路板-OS<INFO>28629</INFO></ITEM>";
	mes "<ITEM>光明戰斧-OS<INFO>28136</INFO></ITEM>";
	mes "<ITEM>藍寶石棒鎚-OS<INFO>16088</INFO></ITEM>";
	mes "<ITEM>虛幻天弓-OS<INFO>18178</INFO></ITEM>";
	mes "<ITEM>MH-P89-OS<INFO>18179</INFO></ITEM>";
	mes "<ITEM>綠光拳刃-OS<INFO>28038</INFO></ITEM>";
	mes "<ITEM>燃燒指虎-OS<INFO>1862</INFO></ITEM>";
	mes "<ITEM>HR-S55-OS<INFO>28253</INFO></ITEM>";
	mes "<ITEM>暗黑白珠短劍-OS<INFO>28755</INFO></ITEM>";
	mes "<ITEM>AC-B44-OS<INFO>18180</INFO></ITEM>";
	mes "<ITEM>推進器長槍-OS<INFO>32019</INFO></ITEM>";
	mes "<ITEM>烏爾蒂奧流星錘-OS<INFO>16089</INFO></ITEM>";
	mes "<ITEM>電流狐尾草-OS<INFO>26164</INFO></ITEM>";
	mes "<ITEM>幻象機甲A型<INFO>15376</INFO></ITEM>";
	mes "<ITEM>幻象機甲B型<INFO>15377</INFO></ITEM>";
	mes "<ITEM>幻象推進翼A型<INFO>20933</INFO></ITEM>";
	mes "<ITEM>幻象推進翼B型<INFO>20934</INFO></ITEM>";
	mes "<ITEM>幻象戰靴A型<INFO>22196</INFO></ITEM>";
	mes "<ITEM>幻象戰靴B型<INFO>22197</INFO></ITEM>";
	mes "<ITEM>幻象助推器R<INFO>32207</INFO></ITEM>";
	mes "<ITEM>幻象助推器L<INFO>32208</INFO></ITEM>";
	mes "<ITEM>幻象戰鬥晶片R<INFO>32209</INFO></ITEM>";
	mes "<ITEM>幻象戰鬥晶片L<INFO>32210</INFO></ITEM>";
	mes "----------------------------";
	close2;
	cutin "",255;
	end;
}

sp_cor,160,55,5	script	作戰指揮官#os	10203,{	
	if(BaseLevel < 110) {
		mes "[作戰指揮官]";
		mes "這個作戰行動對你來說太危險了。";
		mes "至少到110級再來吧。";
		close;
	}
	if(checkquest(12455, PLAYTIME) == 2) {
		erasequest 12455;
	}
	mes "^ff0000如果馴服副本中的任意魔物，";
	mes "可能會導致副本不能正常進行。";
	mes "敬請留意。^000000";
	next;
	if(checkquest(12455, PLAYTIME) == 0 || checkquest(12455, PLAYTIME) == 1) {
		mes "[作戰指揮官]";
		mes "現在還沒到作戰時間，所以請先休息吧。";
		close;
	}
	if(checkquest(12454) == -1) {	
		mes "[作戰指揮官]";
		mes "雖然之前進行了第一次的壓制，";
		mes "但是現在突變體還是盤踞在奧斯。";
		next;
		mes "[作戰指揮官]";
		mes "所以為了清理乾淨突變體，";
		mes "上級要求對奧斯進行第二次的壓制。";
		setquest 12454;
	} else {
		mes "[作戰指揮官]";
		mes "希望能順利地結束探查。";
	}
	next;
	mes "[作戰指揮官]";
	mes "我想你已經準備好了吧？";
	next;
	switch(select("準備再探奧斯","進入","取消")) {
		case 1:
			.@party_id = getcharid(1);
			if (!.@party_id) {	
				mes "[作戰指揮官]";
				mes "前方很危險，你需要組成一支隊伍前往。";
				close;
			}
			if (getcharid(0) != getpartyleader(getcharid(1),2)) {
				mes "[作戰指揮官]";
				mes "請讓你們隊長來找我。";
				close;
			}
			if (instance_mapname("1@os_a") != "" && instance_mapname("1@os_a") != "1@os_a") {
				mes "[作戰指揮官]";
				mes "行動已經成功申請，請迅速做好準備進入奧斯。";
				close;
			}
			if(instance_create("再探奧斯") < 0) {
				mes "[作戰指揮官]";
				mes "再探奧斯行動申請失敗。";
				close;
			}
			announce "★ 副本公告 ★ 隊伍 [ "+getpartyname(getcharid(1)) +" ] 準備挑戰【 再探奧斯 】副本！",15,0x33ea91;
			mes "[作戰指揮官]";
			mes "行動申請成功，做好準備就來找我吧。";
			close;	
		case 2:
			.@party_id = getcharid(1);
			if (!.@party_id) {	
				mes "[作戰指揮官]";
				mes "前方很危險，你需要組成一支隊伍前往。";
				close;
			}
			//if (instance_mapname("1@os_a") == "" || instance_mapname("1@os_a") == "1@os_a") {
			//	mes "[作戰指揮官]";
			//	mes "讓你們隊長來申請行動。";
			//	close;	
			//}
//			announce "★ 副本公告 ★ 隊伍 [ "+getpartyname(getcharid(1)) +" ] 準備挑戰【 再探奧斯 】副本！",15,0x33ea91;
			switch(instance_enter("再探奧斯")) {
				case 3:
					mes "發生一個未知錯誤。";
					close;
				case 2:
					mes "副本進度 "+"再探奧斯"+" 不存在。";
					mes "隊長還沒有建立副本進度。";
					close;
				case 1:
					mes "只有註冊過的成員可以進入副本 "+"再探奧斯"+"。";
					close;
				case 0:
					mapannounce "sp_cor",getpartyname(getcharid(1))+"小隊成員 "+strcharinfo(0)+" 進入副本再探奧斯。",bc_map,"0x00ff99";
					setquest 12455;
					//warp "1@os_a",335,34;
					end;
			}
		case 3:
			mes "[作戰指揮官]";
			mes "做好準備再來吧。";
			close;	
	}
}

1@os_a,335,34,0	script	#osastart	139,1,1,{	
	end;
OnTouch_:
	if(!'begin) {
		set 'begin,1;
		hideonnpc instance_npcname("#osastart");
		donpcevent instance_npcname("#osamobctrl")+"::OnAddmob1";
		sleep 4000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：一旦探測到附近有突變體反應，請阿爾法部隊立刻做好戰鬥準備。",bc_map,"0x00ffff";
		sleep 5000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：此搜索區域內不存在平民所以只要是會移動的生物就可直接擊殺。",bc_map,"0x00ffff";
		sleep 4000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：查理、德爾塔部隊如果有戰況也無需報告，直接開戰。",bc_map,"0x00ffff";
		end;
	}
	end;
OnInstanceInit:
	'begin = 0;
	areamonster instance_mapname("1@os_a"),193,194,300,33,"--ja--",20348,5;
	end;
}

1@os_a,0,0,0	script	#osamobctrl	139,{	
	end;
OnAddmob1:
	for(set .@i,0;.@i < 8;set .@i,.@i+1) {
		set .@rand,rand(20348,20350);
		areamonster instance_mapname("1@os_a"),240,87,300,105,replacestr(getmonsterinfo(.@rand,MOB_NAME),"A013-","CP1 "),.@rand,1,instance_npcname("#osamobctrl")+"::OnMobDead1";
	}
	for(set .@i,0;.@i < 8;set .@i,.@i+1) {
		set .@rand,rand(20348,20350);
		areamonster instance_mapname("1@os_a"),240,33,268,86,replacestr(getmonsterinfo(.@rand,MOB_NAME),"A013-","CP2 "),.@rand,1,instance_npcname("#osamobctrl")+"::OnMobDead1";
	}
	end;
OnAddmob2:
	for(set .@i,0;.@i < 14;set .@i,.@i+1) {
		set .@rand,rand(20348,20350);
		areamonster instance_mapname("1@os_a"),192,94,217,66,replacestr(getmonsterinfo(.@rand,MOB_NAME),"A013-","CP3 "),.@rand,1,instance_npcname("#osamobctrl")+"::OnMobDead2";
	}
	end;
OnAddmob3:
	for(set .@i,0;.@i < 14;set .@i,.@i+1) {
		set .@rand,rand(20348,20350);
		areamonster instance_mapname("1@os_a"),198,141,223,121,replacestr(getmonsterinfo(.@rand,MOB_NAME),"A013-","CP4 "),.@rand,1,instance_npcname("#osamobctrl")+"::OnMobDead3";
	}
	end;
OnAddmob4:
	for(set .@i,0;.@i < 14;set .@i,.@i+1) {
		set .@rand,rand(20348,20350);
		areamonster instance_mapname("1@os_a"),234,165,270,143,replacestr(getmonsterinfo(.@rand,MOB_NAME),"A013-","CP5 "),.@rand,1,instance_npcname("#osamobctrl")+"::OnMobDead4";
	}
	end;
OnAddmob5:
	for(set .@i,0;.@i < 20;set .@i,.@i+1) {
		set .@rand,rand(20348,20350);
		areamonster instance_mapname("1@os_a"),193,195,243,181,replacestr(getmonsterinfo(.@rand,MOB_NAME),"A013-","CP6 "),.@rand,1,instance_npcname("#osamobctrl")+"::OnMobDead5";
	}
	end;
OnAddboss:
	monster instance_mapname("1@os_a"),205,188,"CP"+'miguellv+" 米古爾",20346,1,instance_npcname("#osamobctrl")+"::OnBossDead"; 
	'BossID = $@mobid[0];
	donpcevent instance_npcname("#osacheck")+"::OnCheck";
	end;
OnMobDead1:	
	if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead1") == 5) {
		mapannounce instance_mapname("1@os_a"), "殘餘突變體存活反應 - 5",bc_map,"0xFFFF00";
	} else if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead1") == 2) {
		mapannounce instance_mapname("1@os_a"), "殘餘突變體存活反應 - 2",bc_map,"0xFFFF00";
	} else if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead1") < 1) {
		sleep 4000;
		mapannounce instance_mapname("1@os_a"),"第1據點已安全",bc_map,"0x00ff00";
		donpcevent instance_npcname("#osamobctrl")+"::OnAddmob2";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：現在告知本搜索區域的注意事項。",bc_map,"0x00ffff";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：具有A013標誌的突變體是一種與這個據點無關的獨立突變體。",bc_map,"0x00ffff";
		sleep 6000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：因其作戰能力比一般突變體更強所以如非必要請盡量避免戰鬥。",bc_map,"0x00ffff";
	} 
	end;
OnMobDead2:	
	if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead2") == 5) {
		mapannounce instance_mapname("1@os_a"), "殘餘突變體存活反應 - 5",bc_map,"0xFFFF00";
	} else if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead2") == 2) {
		mapannounce instance_mapname("1@os_a"), "殘餘突變體存活反應 - 2",bc_map,"0xFFFF00";
	} else if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead2") < 1) {
		sleep 4000;
		mapannounce instance_mapname("1@os_a"),"第2據點已安全",bc_map,"0x00ff00";
		donpcevent instance_npcname("#osamobctrl")+"::OnAddmob3";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：與CP4以上等級的突變體作戰時要特別注意。",bc_map,"0x00ffff";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：雖然和CP1有著相同的樣貌但是等級越高攻擊力越強，注意不要被圍攻了。",bc_map,"0x00ffff";
	} 
	end;
OnMobDead3:	
	if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead3") == 2) {
		mapannounce instance_mapname("1@os_a"), "殘餘突變體存活反應 - 2",bc_map,"0xFFFF00";
	} else if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead3") < 1) {
		sleep 4000;
		mapannounce instance_mapname("1@os_a"),"第3據點已安全",bc_map,"0x00ff00";
		donpcevent instance_npcname("#osamobctrl")+"::OnAddmob4";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：4號區域是機密文件保管室。",bc_map,"0x00ffff";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：雖然先遣隊已調查過周圍並無特別但還是要集中精神不要被圍攻了。",bc_map,"0x00ffff";
	} 
	end;
OnMobDead4:	
	if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead4") == 2) {
		mapannounce instance_mapname("1@os_a"), "殘餘突變體存活反應 - 2",bc_map,"0xFFFF00";
	} else if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead4") < 1) {
		sleep 4000;
		mapannounce instance_mapname("1@os_a"),"第4據點已安全",bc_map,"0x00ff00";
		donpcevent instance_npcname("#osamobctrl")+"::OnAddmob5";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：亡命徒部隊，正與CP6等級交戰中，攻擊力很強。",bc_map,"0x00ffff";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：請阿爾法、查理、德爾塔部隊也特別注意，千萬不要被CP6等級的圍攻了。",bc_map,"0x00ffff";
	} 
	end;
OnMobDead5:		
	if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead5") == 5) {
		mapannounce instance_mapname("1@os_a"), "殘餘突變體存活反應 - 5",bc_map,"0xFFFF00";
	} else if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead5") == 2) {
		mapannounce instance_mapname("1@os_a"), "殘餘突變體存活反應 - 2",bc_map,"0xFFFF00";
	} else if(mobcount(instance_mapname("1@os_a"),instance_npcname("#osamobctrl")+"::OnMobDead5") < 1) {
		sleep 4000;
		mapannounce instance_mapname("1@os_a"),"第5據點已安全",bc_map,"0x00ff00";
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：各部隊，如果發現S級突變體請立刻與之開戰。",bc_map,"0x00ffff";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"？？？？：呼嚕嚕嚕...",bc_map,"0xff0000";
		sleep 3000;
		mapannounce instance_mapname("1@os_a"),"作戰指揮官：高CP等級的突變體相對更棘手所以務必要小心！",bc_map,"0x00ffff";
		set 'miguellv,rand(7,22);
		donpcevent instance_npcname("#osamobctrl")+"::OnAddboss";
	} 
	end;
OnBossDead:
	'BossID = 0;
	for(set .@i,1;.@i <= 8;set .@i,.@i+1) {
		if(getd("'FireID"+.@i)) {
			mobremove getd("'FireID"+.@i);
		}
	}
	mapannounce instance_mapname("1@os_a"),"作戰指揮官：我好像來晚了！",bc_map,"0x00ffff";
	hideoffnpc instance_npcname("作戰指揮官#osa");
	end;
OnInstanceInit:
	set 'miguellv,0;
	end;
}

1@os_a,0,0,0	script	#osacheck	139,{
	end;
OnCheck:
	initnpctimer;
	end;
OnTimer5000:
	stopnpctimer;
	if(!'BossID) {
		end;
	}
	getunitdata 'BossID,'osabossdata;
	if('osabossdata[2] < 'osabossdata[3]*7/10) {
		donpcevent instance_npcname("#osabossskill")+"::OnStart";		
		end;
	}
	initnpctimer;
	end;
}

1@os_a,0,0,0	script	#osabossskill	139,{
	end;
OnStart:
	initnpctimer;
OnTimer200:
	stopnpctimer;
	if(!'BossID) {
		end;
	}
	for(set .@i,1;.@i <= 8;set .@i,.@i+1) {
		if(getd("'FireID"+.@i)) {
			mobremove getd("'FireID"+.@i);
		}
	}
	getunitdata 'BossID,'osabossdata;	
	if('osabossdata[2] < 'osabossdata[3]*7/20) {
		set 'fire,1;
	}
	monster instance_mapname("1@os_a"),'osabossdata[6],'osabossdata[7]+8,"--ja--",20351,1,instance_npcname("#osabossskill")+"::OnFire";
	'FireID1 = $@mobid[0];
	monster instance_mapname("1@os_a"),'osabossdata[6],'osabossdata[7]-8,"--ja--",20351,1,instance_npcname("#osabossskill")+"::OnFire";
	'FireID2 = $@mobid[0];
	monster instance_mapname("1@os_a"),'osabossdata[6]+8,'osabossdata[7],"--ja--",20351,1,instance_npcname("#osabossskill")+"::OnFire";
	'FireID3 = $@mobid[0];
	monster instance_mapname("1@os_a"),'osabossdata[6]-8,'osabossdata[7],"--ja--",20351,1,instance_npcname("#osabossskill")+"::OnFire";
	'FireID4 = $@mobid[0];
	if('fire) {
		monster instance_mapname("1@os_a"),'osabossdata[6]+6,'osabossdata[7]+6,"--ja--",20351,1,instance_npcname("#osabossskill")+"::OnFire";
		'FireID5 = $@mobid[0];
		monster instance_mapname("1@os_a"),'osabossdata[6]-6,'osabossdata[7]-6,"--ja--",20351,1,instance_npcname("#osabossskill")+"::OnFire";
		'FireID6 = $@mobid[0];
		monster instance_mapname("1@os_a"),'osabossdata[6]+6,'osabossdata[7]-6,"--ja--",20351,1,instance_npcname("#osabossskill")+"::OnFire";
		'FireID7 = $@mobid[0];
		monster instance_mapname("1@os_a"),'osabossdata[6]-6,'osabossdata[7]+6,"--ja--",20351,1,instance_npcname("#osabossskill")+"::OnFire";
		'FireID8 = $@mobid[0];
	}
	initnpctimer;
	end;
OnInstanceInit:
	set 'fire,0;
	end;
}

1@os_a,254,108,0	script	#osapoison	139,{
	end;
OnTimer250:
	stopnpctimer;
	initnpctimer;
	getmapxy (.@map$,.@x,.@y,BL_NPC);
	areapercentheal instance_mapname("1@os_a"),.@x-4,.@y+2,.@x+3,.@y-3,-10,0;
	for(set .@i,0; .@i<20; set .@i,.@i+1){
		specialeffect 1020;
		movenpc instance_npcname("#osapoison"),rand(.@x-4,.@x+3),rand(.@y-3,.@y+2);
	}
	movenpc instance_npcname("#osapoison"),.@x,.@y;
	end;
OnInstanceInit:
	initnpctimer;
	end;
}

1@os_a,187,195,5	script	作戰指揮官#osa	10203,{
	if(checkquest(12454) > -1) {	
		mes "[作戰指揮官]";
		if('miguellv >= 21) {
			mes "你出色地解決了CP"+'miguellv+"等級的突變體！辛苦你了！";
		} else if('miguellv >= 11) {
			mes "解決了CP"+'miguellv+"等級的突變體，辛苦了。";
		} else {
			mes "解決了CP"+'miguellv+"等級的突變體...戰鬥沒問題吧？";
		}
		next;
		mes "[作戰指揮官]";
		mes "這是給參與作戰的隊員的補給品和經驗值。請收下吧。";
		erasequest 12454;
//base經驗未完善		getexp 216000+66000+'miguellv*12000,0;
//job經驗未完善	getexp 0,0;
		getitem 25723,1;
		getitem 25669,5;
		next;
		mes "[作戰指揮官]";
		if('miguellv == 22) {
			mes "因為是CP22等級，所以最大限度地追加經驗值。";
		} else if('miguellv >= 18) {
			mes "因為是CP"+'miguellv+"等級，所以追加了大量的經驗值。";
		} else if('miguellv >= 11) {
			mes "因為是CP"+'miguellv+"等級，所以沒能追加很多經驗值。";
		} else {
			mes "因為是CP"+'miguellv+"等級，很遺憾幾乎沒有追加經驗值。";
		}
		close;
	}
	mes "[作戰指揮官]";
	mes "那麼我們出去吧。";
	mes "我們必須加緊腳步。";
	next;
	if(select("再等一下","走吧") == 1) {
		mes "[作戰指揮官]";
		mes "我明白了。";
		mes "請盡快。";
		close;		
	}
	warp "egg1",214,167;
	end;
OnInstanceInit:
	hideonnpc instance_npcname("作戰指揮官#osa");
	end;
}

//7、180     735000 基礎4500000(mvpbexp 4500000)
//9、172     1215000 基礎4710000(mvpbexp 4500000)   j1192725(基礎jexp 7290000)
//11、163    1815000 基礎4710000(mvpbexp 4500000)   j1781725(基礎jexp 7289985)
//17、176    4335000 基礎4920000(mvpbexp 4500000)
//22、157    7260000 基礎4878000(mvpbexp 4500000)
//13、171    2535000 基礎5166000(mvpbexp 4725000)   j2488525(基礎jexp 7985250)
//18、175    4860000 基礎4710000(mvpbexp 4500000)
//19、176    5415000 基礎4710000(mvpbexp 4500000)
//8、165     960000 基礎4668000(mvpbexp 4500000)   j942400(基礎jexp 7227000)

//12、167    2160000 基礎4920000(mvpbexp 4500000)
//16、166    3840000 基礎4920000(mvpbexp 4500000)   j3769600(基礎jexp 7605000)
//21、175    5953500 基礎4549267(mvpbexp 4252500)
//14、157    2940000 基礎4710000(mvpbexp 4500000)
