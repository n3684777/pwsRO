//===== rAthena Script ======================================= 
//= 克雷斯特漢姆古城-挑戰模式
//===== By: ================================================== 
//= 天波工作組
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===================請保留以上資訊=========================

1@gl_he	mapflag	nomemo
1@gl_he	mapflag	pvp	off
1@gl_he	mapflag	nosave
1@gl_he	mapflag	noteleport
1@gl_he	mapflag	monster_noteleport
1@gl_he	mapflag	nowarpto
1@gl_he	mapflag	partylock

glast_01,141,291,3	script	克雷斯特漢姆古城-挑戰模式#02	837,{
	mes "----------------------------";	
	mes ">> 副本獎勵清單";
	mes "----------------------------";
	mes "<ITEM>時間頭冠（盧恩騎士）<INFO>19474</INFO></ITEM>";
	mes "<ITEM>時間頭冠（皇家禁衛軍）<INFO>19475</INFO></ITEM>";
	mes "<ITEM>時間頭冠（機械工匠）<INFO>19476</INFO></ITEM>";
	mes "<ITEM>時間頭冠（基因學者）<INFO>19477</INFO></ITEM>";
	mes "<ITEM>時間頭冠（十字斬首者）<INFO>19478</INFO></ITEM>";
	mes "<ITEM>時間頭冠（魅影追蹤者）<INFO>19479</INFO></ITEM>";
	mes "<ITEM>時間頭冠（大主教）<INFO>19480</INFO></ITEM>";
	mes "<ITEM>時間頭冠（修羅）<INFO>19481</INFO></ITEM>";
	mes "<ITEM>時間頭冠（咒術士）<INFO>19482</INFO></ITEM>";
	mes "<ITEM>時間頭冠（妖術師）<INFO>19483</INFO></ITEM>";
	mes "<ITEM>時間頭冠（遊俠）<INFO>19484</INFO></ITEM>";
	mes "<ITEM>時間頭冠（宮廷樂師與浪跡舞者）<INFO>19485</INFO></ITEM>";
	mes "<ITEM>時間頭冠（拳皇）<INFO>19486</INFO></ITEM>";
	mes "<ITEM>時間頭冠（獵靈士）<INFO>19487</INFO></ITEM>";
	mes "<ITEM>時間頭冠（叛亂者）<INFO>19488</INFO></ITEM>";
	mes "<ITEM>時間頭冠（朧）<INFO>19489</INFO></ITEM>";
	mes "<ITEM>時間頭冠（影狼）<INFO>19490</INFO></ITEM>";
	mes "<ITEM>時間頭冠（超級初學者）<INFO>19491</INFO></ITEM>";
	mes "<ITEM>時間頭冠（召喚師）<INFO>19492</INFO></ITEM>";
	mes "----------------------------";
	mes "<ITEM>守護騎士寶石之劍<INFO>32353</INFO></ITEM>";
	mes "<ITEM>守護騎士重劍<INFO>21055</INFO></ITEM>";
	mes "<ITEM>守護騎士單手斧<INFO>1336</INFO></ITEM>";
	mes "<ITEM>守護騎士戰鬥雙手斧<INFO>28141</INFO></ITEM>";
	mes "<ITEM>守護騎士單手矛<INFO>32027</INFO></ITEM>";
	mes "<ITEM>守護騎士弓箭手之弓<INFO>18198</INFO></ITEM>";
	mes "<ITEM>守護騎士之弓<INFO>18191</INFO></ITEM>";
	mes "<ITEM>皇家拳刃<INFO>28046</INFO></ITEM>";
	mes "<ITEM>皇家格拉迪烏斯(R)<INFO>28774</INFO></ITEM>";
	mes "<ITEM>皇家格拉迪烏斯(L)<INFO>28775</INFO></ITEM>";
	mes "<ITEM>皇家巫師匕首<INFO>28776</INFO></ITEM>";
	mes "<ITEM>皇家巫師單手杖<INFO>26166</INFO></ITEM>";
	mes "<ITEM>皇家巫師雙手杖<INFO>2060</INFO></ITEM>";
	mes "<ITEM>皇家聖職者單手杖<INFO>26165</INFO></ITEM>";
	mes "<ITEM>守護者煉金術之杖<INFO>32403</INFO></ITEM>";
	mes "<ITEM>皇家拳套<INFO>1870</INFO></ITEM>";
	mes "<ITEM>皇家賢者之書<INFO>28636</INFO></ITEM>";
	mes "<ITEM>皇家大提琴<INFO>32111</INFO></ITEM>";
	mes "<ITEM>皇家鞭子<INFO>26216</INFO></ITEM>";
	mes "<ITEM>皇家柱子<INFO>32401</INFO></ITEM>";
	mes "<ITEM>皇家注射器<INFO>32402</INFO></ITEM>";
	mes "<ITEM>皇家左輪手槍<INFO>32304</INFO></ITEM>";
	mes "<ITEM>皇家手裡劍<INFO>13347</INFO></ITEM>";
	mes "<ITEM>皇家狐尾草<INFO>26172</INFO></ITEM>";
	mes "----------------------------";
	close2;
	cutin "",255;
	end;
}
glast_01,143,288,3	script	奧斯卡#oscar2	4_ED_OSCAR,{
if (#CGHTYPE == 0){
 #CGHTYPE=1;
}
	if (BaseLevel < 170) {
		mes "您的等級不足，請先去提升等級吧！";
		close;
	}
	if (isbegin_quest(22340) == 0) {
		mes "[奧斯卡]";
		mes "哦~這不是^0000ff"+strcharinfo(0)+"^000000嗎？你怎麼會在這裡！";
		cutin "oscar01",2;
		next;
		select("奧斯卡是妳啊！妳又在這裡幹嘛？");
		mes "[奧斯卡]";
		mes "哈哈，我就像以往一樣，正在確認古城周邊有沒有汙染的事件。";
		next;
		mes "[奧斯卡]";
		mes "但目前我發現了一個問題";
		mes "可是依靠我自己我恐怕無法一個人完成...";
		next;
		select("是很危險的事情嗎？");
		mes "[奧斯卡]";
		mes "嗯阿。如果交給平凡的冒險家處理，無論如何都是很危險的。";
		mes "所以我打算暫時把案件正式提交到古城諮詢小組";
		next;
		select("古城諮詢小組？");
		mes "[奧斯卡]";
		mes "古城諮詢小組嗎？他們是一群會定期處理這種事件的大型戰鬥團體";
		next;
		select("原來如此..");
		select("那妳可以跟我說說裡面目前的情況嗎？");
		mes "[奧斯卡]";
		mes "啊,我正想跟妳解釋個清楚。";
		next;
		mes "[奧斯卡]";
		mes "這次發現的問題是希梅爾茲襲擊的時間段中的怪異變種。";
		mes "他們出現的情況真的變得很詭異";
		next;
		mes "[奧斯卡]";
		mes "其中霸勒門德和哈希埵]都不見了。";
		next;
		select("哦...這樣啊...");
		mes "[奧斯卡]";
		mes "這與過去的記錄有天壤之別。假如只是簡單程度地錯誤，系統則會自動的線上校正。";
		next;
		mes "[奧斯卡]";
		mes "但是存在的人物本身消失的事件很少見。";
		mes "再加上其他的影響非常強烈，目前事件錯誤正在逐漸擴大。";
		next;
		select("如果一直都不理會他會怎樣？");
		mes "[奧斯卡]";
		mes "那麼現在的時間會被汙染的時間所蠶食，會增加很多蝴蝶效應。";
		mes "我們應該封閉那個陣地或阻止嚴重偏移的地方。";
		next;
		if (select("這樣啊...到時候再說吧...","聽起來很好玩，我想去看看。") == 1) {
			mes "[奧斯卡]";
			mes "我應該要如何做才能馬上處理呢....";
			cutin "oscar01",255;
			close;
		}
		mes "[奧斯卡]";
		mes "^0000ff"+strcharinfo(0)+"^000000？這很危險的喔！";
		next;
		mes "[奧斯卡]";
		mes "好吧，畢竟要找到像妳一樣具有豐富旅行經驗的冒險家也是很難的";
		mes "而且，在上頭做出正式決定之前，我覺得必須要有人先阻止這堛漲冀V度。";
		next;
		mes "[奧斯卡]";
		mes "我們就一起進去看看吧！，那我先確認一下時空的裂縫，妳等等再來找我吧！";
		cutin "oscar01",255;
		setquest 22340;
		completequest 22340;
		close;
	}
	else {
		mes "[奧斯卡]";
		mes "真剛好，你能幫我處理汙染事件嗎？";
	}
	next;
	set .@ghins_time, checkquest(22341,PLAYTIME);
	if (.@ghins_time == -1) {
		set .@party_id,getcharid(1);
		set .@p_name$,getpartyname(.@party_id);

		if (!.@party_id) {
			mes "[奧斯卡]";
			mes "沒有隊伍是無法前往的哦！";
			close;
		}else if(BaseLevel < 170) {
			mes "[奧斯卡]";
			mes "裡面是很危險的…"; 
			mes "還是等到Lv.170後再來吧…"; 
			close;
		}
		.@md_name$ = "克雷斯特漢姆-挑戰模式";
		
		if (getcharid(0) == getpartyleader(.@party_id,2))
			set @menu$, "創建時間的縫隙:進入克雷斯特漢姆-挑戰模式";
		else
			set @menu$, ":進入挑戰古城";
		switch(select(@menu$)) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "隊伍名稱: "+.@p_name$;
				mes "隊伍隊長: "+strcharinfo(0);
				mes "^0000ff"+.@md_name$+" ^000000- 創建失敗!";
				close;
			}
			mes "[奧斯卡]";
			mes "時間的縫隙已經創立了∼";
			mes "請選擇^0066CC進入克雷斯特漢姆-挑戰模式^000000。";
			announce "★ 副本公告 ★ 隊伍 [ "+getpartyname(getcharid(1)) +" ] 準備挑戰【 克雷斯特漢姆-挑戰模式 】副本！",15,0x33ea91;
			close;
		case 2:								
			switch(instance_enter(.@md_name$)) {
			case IE_OTHER:
				mes "不明錯誤。";
				close;
			case IE_NOINSTANCE:
				mes .@md_name$+" 副本不存在。";
				mes "隊長尚未申請記憶迷宮。";
				close;
			case IE_NOMEMBER:
				mes "只有申請 "+.@md_name$+" 副本的隊員才可以進入。";
				close;
			case IE_OK:
				setquest 22341;	
				close;
			}
		}
	} else if (.@ghins_time == 0 || .@ghins_time == 1) {
		mes "[奧斯卡]";
		mes "噢，哎呀。";
		mes "你的身體還持續著時空旅行的副作用。";
		mes "在這狀態下，你無法再進行時空旅行。";
		next;
		mes "[奧斯卡]";
		mes "等你休息過後再回來找我吧。";
		close;
	} else {
		mes "^0000ff克雷斯特漢姆古城-挑戰模式^000000";
		mes "^0000ff副本冷卻已消除，^000000";
		mes "^0000ff請再次與'奧斯卡'對話。^000000";
		erasequest 22341;
		close;
	}
	end;
OnInit:
			//難道系數設定:增加的公式$@ATKMIN_Challenge*'selectdiff(基礎難度*挑戰等級)參考以下公式
			
			/*setunitdata 'BossID,UMOB_ATKMIN,.@ATKMIN+($@ATKMIN_Challenge*'selectdiff);
			setunitdata 'BossID,UMOB_ATKMAX,.@ATKMAX+($@ATKMAX_Challenge*'selectdiff);
			setunitdata 'BossID,UMOB_MATKMIN,.@MATKMIN+($@MATKMIN_Challenge*'selectdiff);
			setunitdata 'BossID,UMOB_MATKMAX,.@MATKMAX+($@MATKMAX_Challenge*'selectdiff);			
			setunitdata 'BossID,UMOB_DEF,.@DEF+($@DEF_Challenge+*'selectdiff*10);
			setunitdata 'BossID,UMOB_MDEF,.@MDEF+($@MDEF_Challenge+*'selectdiff*10);
			setunitdata 'BossID,UMOB_HIT,.@HIT+($@HIT_Challenge+*'selectdiff*10);	*/
			
			
			$@ATKMIN_Challenge=5200;
			$@ATKMAX_Challenge=10200;
			$@MATKMIN_Challenge=3200;
			$@MATKMAX_Challenge=8200;
			$@DEF_Challenge=520;
			$@MDEF_Challenge=520;
			$@HIT_Challenge=520;
			


OnTimer1000:
	showscript " [★EDDA挑戰古副] ";
	initnpctimer;
	end;
}

1@gl_he,149,28,0	script	#wall001	-1,2,2,{
	end;
OnTouch:
	if(.@wall != 1){
	set .@map$, instance_mapname("1@gl_he");
	setcell .@map$,144,43,155,43,cell_walkable,0;
	set .@wall,1;
	}
}

//============================================================
1@gl_he,149,41,5	script	奧斯卡#oscar_1	4_ED_OSCAR,{
	if(getpartyleader(getcharid(1),1) == getcharid(3)) {

		cutin "oscar01",2;
		mes "[奧斯卡]";
		mes "請仔細的選擇您要挑戰的難度";
		mes "根據選擇的階段出現的魔物強度也會有所不同";
		mes "獎勵也是不盡相阿";
		mes "清除的時間也會影響副本的流程";
		next;
		set .@typeChoice, select("我想再等一下", "選擇汙染魔力階段等級");
		switch(.@typeChoice) {
		case 1:
			mes "[奧斯卡]";
			mes "等你好再跟我說吧！。";
			cutin "gl_heinrich1",255;
			close;
		case 2:
			set .@typeset ,#CGHTYPE;
			
			
			for ( .@i = 1; .@i <= .@typeset; .@i++ ) {
				  .@menu$ += "汙染魔力第[" + .@i + "]階段";
				  .@menu$ += ":";
			}
			'selectdiff = select(.@menu$);
			break;
		}
		mes "[奧斯卡]";
		mes "您選擇為第["+'selectdiff +"]階段";
		mes "請問是否正確？";
		next;
		if(select("不對", "是的，沒有錯。") == 1) {
			mes "[奧斯卡]";
			mes "請重新再選擇一次吧！";
			cutin "gl_heinrich1",255;
			close;
		}
		donpcevent instance_npcname("#CGH_ghmemorialmob05")+"::OnEnable";
		donpcevent instance_npcname("#CGH_ghmemorialmob08")+"::OnEnable";
		donpcevent instance_npcname("#checkkill")+"::OnEnable";
		set .@partpeople,getmapusers(instance_mapname("1@gl_he"));
		set 'warppeople,.@partpeople/2;
		set 'warppeopleto1,0;
		set 'warppeopleto2,0;
		donpcevent instance_npcname("奧斯卡#oscar_1")+"::OnPrize1";
		disablenpc instance_npcname("奧斯卡#oscar_1");
		cutin "gl_heinrich1",255;
		end;
	}else{
		mes "[奧斯卡]";
		mes "麻煩請叫隊長來找我。";
	}
	close;
	
	OnPrize1://組隊平均分散傳送
		addrid(4,0,166,12,133,43);
		if( rand(2) ){
			warp instance_mapname("1@gl_he"),126,123;
			set 'warppeopleto1,'warppeopleto1+1;
				if ('warppeopleto1 > 'warppeople){
					warp instance_mapname("1@gl_he"),167,140;
					set 'warppeopleto2,'warppeopleto2+1;
				}
		}else{
			warp instance_mapname("1@gl_he"),167,141;
			set 'warppeopleto2,'warppeopleto2+1;
				if ('warppeopleto2 > 'warppeople){
					warp instance_mapname("1@gl_he"),126,123;
					set 'warppeopleto1,'warppeopleto1+1;
				}
		}
	end;
}

//----傳點設置------
1@gl_he,135,124,0	warp	LeftWarps#face	1,1,1@gl_he,150,69
1@gl_he,146,122,0	warp	midLeftWarps#face	1,1,1@gl_he,126,123
1@gl_he,164,140,0	warp	RightWarps#face	1,1,1@gl_he,150,69
1@gl_he,154,100,0	warp	midRightWarps#face	1,1,1@gl_he,167,141
1@gl_he,150,163,0	warp	Toboss#face	1,1,1@gl_he,150,168
//------------------

1@gl_he,0,0,0	script	#CGH_ghmemorialmob05	-1,{//左方區域
OnInstanceInit:
	hideonnpc instance_npcname("奧斯卡#oscar_2");
	hideonnpc instance_npcname("奧斯卡#oscar_3");
	hideonnpc instance_npcname("寶箱#chatnmtop");
	hideonnpc instance_npcname("奧斯卡#oscar_5");
	hideonnpc instance_npcname("奧斯卡#oscar_4");
	disablenpc instance_npcname("LeftWarps#face");
	disablenpc instance_npcname("RightWarps#face");
	disablenpc instance_npcname("midLeftWarps#face");
	disablenpc instance_npcname("midRightWarps#face");
	disablenpc instance_npcname("Toboss#face");
OnDisable:
	disablenpc instance_npcname("#CGH_ghmemorialmob05");
	end;
OnEnable:
	enablenpc instance_npcname("#CGH_ghmemorialmob05");
	set .@map$, instance_mapname("1@gl_he");
	set .@label$, instance_npcname("#CGH_ghmemorialmob05")+"::OnMyMobDead";


	mapannounce .@map$, "奧斯卡：為了消除這個空間的汙染魔力，首先我們得先淨化兩側空間受汙染的幻影魔物。",bc_map,"0xFFFF00";
	mapannounce .@map$, "奧斯卡：我會告訴你們還有多少數量的受汙染的幻影需要淨化。",bc_map,"0xFFFF00";
	
	areamonster .@map$,124,20,31,162,"--ja--",20574,10,.@label$;
	areamonster .@map$,124,20,31,162,"--ja--",20576,10,.@label$;
	areamonster .@map$,124,20,31,162,"--ja--",20577,10,.@label$;
	areamonster .@map$,124,20,31,162,"--ja--",20578,10,.@label$;
	areamonster .@map$,124,20,31,162,"--ja--",20579,10,.@label$;
	set 'MyMobs2,50;
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@gl_he");
	set 'MyMobs2, 'MyMobs2-1;
	areaannounce .@map$,124,20,31,162,""+'MyMobs2+" <- 左側未淨化的幻影 -> 50",bc_map,"0xFF8800";
	if ('MyMobs2 == 0) {
		areaannounce .@map$,124,20,31,162, "奧斯卡：右側的幻影尚未淨化完成，通往右邊區域的傳送點已經開啟了。",bc_map,"0xFFFF00";
		enablenpc instance_npcname("LeftWarps#face");
		enablenpc instance_npcname("midRightWarps#face");
		donpcevent instance_npcname("#checkkill")+"::OnEnd";
		donpcevent instance_npcname("奧斯卡#oscar_1")+"::OnPrize1";
		
	}
	end;
}


1@gl_he,0,0,0	script	#CGH_ghmemorialmob08	-1,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#CGH_ghmemorialmob08");
	end;
OnEnable:
	enablenpc instance_npcname("#CGH_ghmemorialmob08");
	set .@map$, instance_mapname("1@gl_he");
	set .@label$, instance_npcname("#CGH_ghmemorialmob08")+"::OnMyMobDead";
	areamonster .@map$,175,163,265,18,"--ja--",20574,10,.@label$;
	areamonster .@map$,175,163,265,18,"--ja--",20576,10,.@label$;
	areamonster .@map$,175,163,265,18,"--ja--",20577,10,.@label$;
	areamonster .@map$,175,163,265,18,"--ja--",20578,10,.@label$;
	areamonster .@map$,175,163,265,18,"--ja--",20579,10,.@label$;
	set 'MyMobs,50;
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@gl_he");
	set 'MyMobs, 'MyMobs-1;
	areaannounce .@map$,157,165,269,18,""+'MyMobs+" <- 右側未淨化的幻影 -> 50",bc_map,"0xFF8800";
	if ('MyMobs == 0) {
		areaannounce .@map$,175,163,265,18, "奧斯卡：左側的幻影尚未淨化完成，通往左邊區域的傳送點已經開啟了。",bc_map,"0xFFFF00";
		enablenpc instance_npcname("RightWarps#face");
		enablenpc instance_npcname("midLeftWarps#face");
		donpcevent instance_npcname("#checkkill")+"::OnEnd";
	}
	end;
}

//-----------判定兩邊清除結束--------------
1@gl_he,172,206,4	script	#checkkill	-1,{
OnEnable:
	enablenpc instance_npcname("#checkkill");
	set 'checkkillcheck,2;
	end;
OnEnd:
	set 'checkkillcheck,'checkkillcheck-1;
	if('checkkillcheck){
	}else{
		set .@map$, instance_mapname("1@gl_he");
		set .@label$, instance_npcname("#checkkill")+"::OnMobDead";
		disablenpc instance_npcname("midLeftWarps#face");
		disablenpc instance_npcname("midRightWarps#face");
		monster .@map$,150,150,"--ja--",20575,1,.@label$;
		set 'Mob,1;
		mapannounce .@map$, "奧斯卡：眼前這模糊不清的東西，就是扭曲時間裡魔力的汙染源。",bc_map,"0xFFFF00";
	}	
	end;
OnMobDead:
		set .@map$, instance_mapname("1@gl_he");
		mapannounce .@map$, "奧斯卡：淨化完成，通往中間區域的傳送點已經開啟了。",bc_map,"0xFFFF00";
		mapannounce .@map$, "奧斯卡：到達後請前往12點鐘方向移動並找到我。",bc_map,"0xFFFF00";
		hideoffnpc instance_npcname("奧斯卡#oscar_2");
		end;
}


1@gl_he,150,163,1	script	#bossdoor	139,{
end;
OnEnable:
	initnpctimer;
	specialeffect 831;
	end;
OnTimer3000:
	specialeffect 135;
	enablenpc instance_npcname("#bossdoor");
	hideonnpc instance_npcname("#bossdoor");
	stopnpctimer;
	end;
}

1@gl_he,150,158,1	script	奧斯卡#oscar_2	4_ED_OSCAR,{
	if(getpartyleader(getcharid(1),1) == getcharid(3)) {
		cutin "oscar01",2;
		mes "[奧斯卡]";
		mes "如果國王問起的話.";
		mes "這也是沒有辦法的事情，我只能去做了";
		next;
		mes "[奧斯卡]";
		mes "我們剩沒有多少時間了";
		mes "別忘記我們來的目的.";
		next;
		if(select("等等！", "變形") == 1) {
			mes "[奧斯卡]";
			mes "好了再跟我說一下。";
			cutin "gl_heinrich1",255;
			close;
		}
		cutin "gl_heinrich1",255;
		close2;
		
		npctalk "(變形中)";
		donpcevent instance_npcname("#bossdoor")+"::OnEnable";
		progressbar_npc "ffff00",3;
		specialeffect 521;
		specialeffect 215;
		setnpcdisplay strnpcinfo(3), "奧斯卡", 1713;
		npctalk "這就是我的真面目";
		initnpctimer;


		end;
	}else{
		mes "[奧斯卡]";
		mes "麻煩請叫隊長來找我。";
	}
	close;
	
	OnTimer1000:
	npcspeed 200;
	npcwalkto 150,162;
	end;
	OnTimer2000:
	enablenpc instance_npcname("Toboss#face");
	hideonnpc instance_npcname("奧斯卡#oscar_2");
	specialeffect 215;
	stopnpctimer;
	donpcevent instance_npcname("奧斯卡#oscar_3")+"::OnEnable";
	end;
}

1@gl_he,150,169,1	script	奧斯卡#oscar_3	4_ED_OSCAR,{
end;
OnEnable:
	hideoffnpc instance_npcname("奧斯卡#oscar_3");
	initnpctimer;
	setnpcdisplay strnpcinfo(3), "奧斯卡", 1713;
	end;
OnTimer3000:
	npcspeed 200;
	npcwalkto 150,199;
	end;
OnTimer9000:
	npcspeed 200;
	npcwalkto 157,229;
	end;
OnTimer15500:
	specialeffect 215;
	stopnpctimer;
	hideonnpc instance_npcname("奧斯卡#oscar_3");
	hideoffnpc instance_npcname("奧斯卡#oscar_4");
	donpcevent instance_npcname("#bosseffect")+"::OnEnable";
	end;
}

1@gl_he,157,240,1	script	#bosseffect	139,{
end;
OnEnable:
	specialeffect 838;
	specialeffect 1085;
	end;
OnStrat:
	initnpctimer;
	specialeffect 1066;
	end;
OnTimer3000:
	specialeffect 840;
	specialeffect 1068;
	stopnpctimer;
	end;
}
//==================BOSS召喚==============
1@gl_he,157,229,1	script	奧斯卡#oscar_4	4_ED_OSCAR,{
	if(getpartyleader(getcharid(1),1) == getcharid(3)) {
		cutin "oscar01",2;
		mes "[奧斯卡]";
		mes "眼前這模糊不清的東西，";
		mes "就是扭曲時間裡魔力的汙染源";
		next;
		mes "[奧斯卡]";
		mes "現在我會將它重新淨化.";
		mes "如果進行的順利的話，他就會出現在這裡";
		next;
		mes "[奧斯卡]";
		mes "請特別註意.";
		mes "這個汙染源是非常強大的，";
		mes "務必隨時保持警惕。";
		next;
		mes "[奧斯卡]";
		mes "如果準備好了，請讓我知道.";
		next;
		if(select("等等！", "我們準備好了") == 1) {
			mes "[奧斯卡]";
			mes "好了再跟我說一下。";
			cutin "gl_heinrich1",255;
			close;
		}
		cutin "gl_heinrich1",255;
		close2;
		npctalk "汗染等級"+'selectdiff+"的幻影即將出現了，我需要牠在中間的魔法陣裡才能將他淨化，請想辦法將他困在中間區域。";
		donpcevent instance_npcname("#bosseffect")+"::OnStrat";
		initnpctimer;
		progressbar_npc "ffff00",5;
		donpcevent instance_npcname("#CGH_BOSS")+"::OnEnable";
		donpcevent instance_npcname("#bossarea")+"::OnEnable";
		

		end;
	}else{
		mes "[奧斯卡]";
		mes "麻煩請叫隊長來找我。";
	}
	close;
	OnTimer4000:
	specialeffect 236;
	end;
	OnTimer5000:
	hideonnpc instance_npcname("奧斯卡#oscar_4");
	end;
	OnTimer6000:
	mapwarp instance_mapname("1@gl_he"),instance_mapname("1@gl_he"),157,227;
	stopnpctimer;
	set .@map$,instance_mapname("1@gl_he");
	mapannounce .@map$, "奧斯卡：闇答萊屍的幻影的魔力會使我們在這空間中述失自我，請務必要小心對付！",bc_map,"0xFFFF00";
	unittalk 'BossID, "你是我的新主人嗎？";
	end;
}

1@gl_he,2,3,4	script	#CGH_BOSS	-1,{

OnEnable:
	enablenpc instance_npcname("#CGH_BOSS");
	donpcevent instance_npcname("#CGH_BOSS")+"::OnStart";
	end;

OnStart:
	set .@map$,instance_mapname("1@gl_he");
	set .@killboss$, instance_npcname("#CGH_BOSS")+"::OnKillMob";
	

	if('select_boss==1) monster .@map$,157,239,"闇答萊屍的幻影",20573,1,.@killboss$;
	if('select_boss==2) monster .@map$,157,239,"希梅爾茲的幻影",20572,1,.@killboss$;	
	if(!'select_boss){
		if ('selectdiff <= 10 ){
			.@q = rand(100);
			if (.@q >20){
				'select_boss=1;
				monster .@map$,157,239,"闇答萊屍的幻影",20573,1,.@killboss$;
				
			}
			else{
				'select_boss=2;
				monster .@map$,157,239,"希梅爾茲的幻影",20572,1,.@killboss$;
				
			}
		}
	}

	
	'BossID = $@mobid[0];
	initnpctimer;
	'bossf2 = 0;
	

	getunitdata 'BossID,'amdaraisdata;
	//設定汙染階級血量
	setarray .@bosshp[0],200000000,220000000,280000000,300000000,320000000,350000000,370000000,390000000,400000000,450000000;
			.@ATKMIN='amdaraisdata[33];
			.@ATKMAX='amdaraisdata[34];
			.@MATKMIN='amdaraisdata[35];
			.@MATKMAX='amdaraisdata[36];
			.@DEF='amdaraisdata[37];
			.@MDEF='amdaraisdata[38];
			.@HIT='amdaraisdata[39];	
			setunitdata 'BossID,UMOB_MAXHP,.@bosshp['selectdiff-1];
			setunitdata 'BossID,UMOB_ATKMIN,.@ATKMIN+($@ATKMIN_Challenge*'selectdiff);
			setunitdata 'BossID,UMOB_ATKMAX,.@ATKMAX+($@ATKMAX_Challenge*'selectdiff);
			setunitdata 'BossID,UMOB_MATKMIN,.@MATKMIN+($@MATKMIN_Challenge*'selectdiff);
			setunitdata 'BossID,UMOB_MATKMAX,.@MATKMAX+($@MATKMAX_Challenge*'selectdiff);			
			setunitdata 'BossID,UMOB_DEF,.@DEF+($@DEF_Challenge+'selectdiff*10);
			setunitdata 'BossID,UMOB_MDEF,.@MDEF+($@MDEF_Challenge+'selectdiff*10);
			setunitdata 'BossID,UMOB_HIT,.@HIT+($@HIT_Challenge+'selectdiff*10);		
	
	
	
	
	
	
	end;
	
OnKillMob:
	if ('fail == 0){
	set .@map$,instance_mapname("1@gl_he");
	mapannounce .@map$, "奧斯卡:：闇答萊屍的幻影已經淨化完成",bc_map,"0xFFFF00";
	donpcevent instance_npcname("#CGH_BOSS")+"::Onstoptime";
	donpcevent instance_npcname("#bossarea")+"::Onstoptime";
	hideoffnpc instance_npcname("寶箱#chatnmtop");
	hideoffnpc instance_npcname("奧斯卡#oscar_5");
	'bossf2 = 1;
	}
	end;
	
Onrandwarp:
	addrid(4,0,30,267,265,18);
		if(rand(100) > 65){
		warp instance_mapname("1@gl_he"),0,0;
		}
	end;


Onstoptime:
	stopnpctimer;
	end;
	
Onstarttime:
	initnpctimer;
	end;


OnTimer1000:
	if ('bossf2 != 1){
	getunitdata 'BossID,'amdaraisdata;
	.@label$ = instance_npcname("#CGH_BOSS") + "::OnSkillunit";
	.@x = 'amdaraisdata[6];
	.@y = 'amdaraisdata[7];
	.@skill = rand(1,5);

	set .@map$,instance_mapname("1@gl_he");
	switch(5) {
	case 1:  //廣範圍
		unittalk 'BossID, "廣範圍詛咒";
		sleep 1000;
		monster .@map$,.@x+4,.@y-4,"--ja--",3095,1,.@label$;		
		.@id[0] = $@mobid[0];
		monster .@map$,.@x+8,.@y-4,"--ja--",3095,1,.@label$;
		.@id[1] = $@mobid[0];
		monster .@map$,.@x+4,.@y+4,"--ja--",3095,1,.@label$;
		.@id[2] = $@mobid[0];
		monster .@map$,.@x+8,.@y+4,"--ja--",3095,1,.@label$;
		.@id[3] = $@mobid[0];
		for(.@i = 0; .@i < 4; .@i++) {
			unitskilluseid .@id[.@i], 353, 1;
		}
		sleep 500;
		for(.@i = 0; .@i <= 18; .@i=.@i+2){
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],83,5,.@x+.@i,.@y+.@i,-10;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],83,5,.@x+.@i,.@y-.@i,-10;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],83,5,.@x-.@i,.@y-.@i,-10;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],83,5,.@x-.@i,.@y+.@i,-10;
			sleep 500;
		}
		sleep 1000;
			break;
	case 2: //十字地震術
		unittalk 'BossID, "!！";
		sleep 1000;
		monster .@map$,.@x+3,.@y+3,"--ja--",3095,1,.@label$;
		.@id[0] = $@mobid[0];
		monster .@map$,.@x-3,.@y-3,"--ja--",3095,1,.@label$;
		.@id[1] = $@mobid[0];
		monster .@map$,.@x-3,.@y+3,"--ja--",3095,1,.@label$;
		.@id[2] = $@mobid[0];
		monster .@map$,.@x+3,.@y-3,"--ja--",3095,1,.@label$;
		.@id[3] = $@mobid[0];
		for(.@i = 0; .@i < 4; .@i++) {
			setunitdata .@id[.@i], 29, 1;
			unitskilluseid .@id[.@i], 353, 1;
		}
		sleep 500;
		for(.@i=0;.@i<10;.@i++){
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+(.@i+1)*2,.@y,-5;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],91,5,.@x-(.@i+1)*2,.@y,-5;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],91,5,.@x,.@y+(.@i+1)*2,-5;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],91,5,.@x,.@y-(.@i+1)*2,-5;
		sleep 10;
		}
		sleep 1000;
		break;
	case 3: //擴散地震
		unittalk 'BossID, "怒爆！！";
		sleep 500;
		monster .@map$,.@x+3,.@y+3,"--ja--",3095,1,.@label$;
		.@id[0] = $@mobid[0];
		monster .@map$,.@x-3,.@y-3,"--ja--",3095,1,.@label$;
		.@id[1] = $@mobid[0];
		monster .@map$,.@x-3,.@y+3,"--ja--",3095,1,.@label$;
		.@id[2] = $@mobid[0];
		monster .@map$,.@x+3,.@y-3,"--ja--",3095,1,.@label$;
		.@id[3] = $@mobid[0];
		for(.@i = 0; .@i < 4; .@i++) {
			setunitdata .@id[.@i], 29, 1;
			unitskilluseid .@id[.@i], 353, 1;
		}
		sleep 500;
		for(.@i=0;.@i<5;.@i++){
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+(.@i+1)*2,.@y,-5;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],91,5,.@x-(.@i+1)*2,.@y,-5;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],91,5,.@x,.@y+(.@i+1)*2,-5;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],91,5,.@x,.@y-(.@i+1)*2,-5;
		sleep 10;
		}
		sleep 500;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+4,.@y+4,-5;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],91,5,.@x-4,.@y+4,-5;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],91,5,.@x+4,.@y-4,-5;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],91,5,.@x-4,.@y-4,-5;
		sleep 1000;
		break;
	case 4: //交叉火牆
		unittalk 'BossID, "!！";
		sleep 1000;
		monster .@map$,.@x,.@y,"--ja--",3095,1,.@label$;
		.@id[0] = $@mobid[0];
		monster .@map$,.@x,.@y,"--ja--",3095,1,.@label$;
		.@id[1] = $@mobid[0];
		monster .@map$,.@x,.@y,"--ja--",3095,1,.@label$;
		.@id[2] = $@mobid[0];
		monster .@map$,.@x,.@y,"--ja--",3095,1,.@label$;
		.@id[3] = $@mobid[0];
		for(.@i = 0; .@i < 4; .@i++) {
			setunitdata .@id[.@i], 29, 1;
			unitskilluseid .@id[.@i], 353, 1;
		}
		sleep 500;
		for(.@i = 0; .@i < 8; .@i++){
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],18,5,.@x+(.@i+1)*2,.@y+(.@i+1)*2,-5;
			if(unitexists(.@id[1]) == true)                    
				unitskillusepos .@id[1],18,5,.@x+(.@i+1)*2,.@y-(.@i+1)*2,-5;
			if(unitexists(.@id[2]) == true)                    
				unitskillusepos .@id[2],18,5,.@x-(.@i+1)*2,.@y-(.@i+1)*2,-5;
			if(unitexists(.@id[3]) == true)                    
				unitskillusepos .@id[3],18,5,.@x-(.@i+1)*2,.@y+(.@i+1)*2,-5;
			sleep 10;
		}
		sleep 5000;
		break;
/*	case 5: //隨機傳送
		if(rand(100) > 60){
			donpcevent instance_npcname("#CGH_BOSS")+"::Onrandwarp";//呼叫隨機傳送事件
		}
		break;
*/	
	case 5: //闇審判事件
		unittalk 'BossID, "！！";
		//	set .@label$, strnpcinfo(0)+"::OnMyMobDead";
		sleep 1000;
		for(.@i = 0; .@i < 4; .@i++){
		
			for(.@j = 0; .@j < 4; .@j++){
				monster .@map$,.@x-13+.@j*9,.@y+13-.@i*9,"--ja--",3095,1,.@label$;					
				.@id[.@k++] = $@mobid[0];	
			}
		}

		
		
		for(.@i = 0; .@i < getarraysize(.@id); .@i++) {
			if(unitexists(.@id[.@i]) == true)
			unitskilluseid .@id[.@i], 353, 1;
		}
		
		sleep 500;
		for(.@i = 0; .@i < getarraysize(.@id); .@i++) {
			if(unitexists(.@id[.@i]) == true)
			unitskilluseid .@id[.@i], 339, 5;
		}
		sleep 3000;

		//donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnEnable";//呼叫審判事件
		//set 'bossf2 , 1;
		break;
	}
	sleep 1000;
	for(set .@i,0;.@i < getarraysize(.@id); set .@i,.@i+1) {
		mobremove .@id[.@i];
	}
	if(!'bossf2)
		initnpctimer;
	end;
	}
	
OnSkillunit:
end;	
}

1@gl_he,2,2,4	script	#bossarea	-1,{

OnEnable:
	initnpctimer;
	enablenpc instance_npcname("#bossarea");
	set .outside,0;
	set 'fail,0;
	sleep 2000;
	end;

Onstoptime:
	stopnpctimer;
	end;

OnTimer10000://10秒判定一次 離開超過30秒重新打
	getunitdata 'BossID,'amdaraisdata;
	.@x = 'amdaraisdata[6];
	.@y = 'amdaraisdata[7];
	if (.@x < 150 || .@x > 165 || .@y > 245 || .@y <232){
		set .outside,.outside + 1;
		set .@map$,instance_mapname("1@gl_he");
		mapannounce .@map$, "奧斯卡：間答萊屍的幻影已經離開中間一定時間了，要是讓她離開太久的話這個時空的臨界點將會被突破，我就無法順利完成淨化了.",bc_map,"0xA42D00";
	}
	else{
		set .outside,0;
	}
	initnpctimer;
	if (.outside >= 3){
		set 'fail,1;
		donpcevent instance_npcname("#CGH_BOSS")+"::Onstoptime";
		set 'bossf2 , 1;
		donpcevent instance_npcname("#CGH_BOSS")+"::Onstoptime";
		donpcevent instance_npcname("#bossarea")+"::Onstoptime";
		hideoffnpc instance_npcname("奧斯卡#oscar_4");
		unitkill 'BossID;
		stopnpctimer;
		detachnpctimer;
		set .@map$,instance_mapname("1@gl_he");
		mapannounce .@map$, "間答萊屍的幻影：看來你沒有資格當我的新主人",bc_map,"0xFF3333";
		
	}
	end;

}




1@gl_he,172,206,4	script	#Dc_GRANDCROSS	-1,{

OnEnable:
	enablenpc instance_npcname("#Dc_GRANDCROSS");
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnStart";
	end;
OnStart:
	set .@map$,instance_mapname("1@gl_he");
	set .@labelG$, instance_npcname("#Dc_GRANDCROSS")+"::OnKillGRANDCROSS";
	setarray 'gid[0], 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0;
	set .@k,0;
	for(.@i = 224; .@i <= 251; .@i=.@i+9){
		set  'gid[.@k], bg_monster(0,.@map$,144,.@i,"受汙染的骨頭荊棘",20581,.@labelG$);
		set  'gid[.@k+1], bg_monster(0,.@map$,153,.@i,"受汙染的骨頭荊棘",20581,.@labelG$);
		set  'gid[.@k+2], bg_monster(0,.@map$,162,.@i,"受汙染的骨頭荊棘",20581,.@labelG$);
		set  'gid[.@k+3], bg_monster(0,.@map$,171,.@i,"受汙染的骨頭荊棘",20581,.@labelG$);
		set .@k,.@k+4;
	}

	initnpctimer;
	end;

OnKillGRANDCROSS:
	end;

OnGRANDCROSS:
	for(.@i = 0; .@i <= 15; .@i++){
	
		if(unitexists('gid[.@i]) == true){
			setunitdata 'gid[.@i], 2, 50;
			getunitdata 'gid[.@i],'mobGRANDCROSS;
			.@x = 'mobGRANDCROSS[6];
			.@y = 'mobGRANDCROSS[7];
			unitskillusepos 'gid[.@i],339,5,.@x,.@y,1;
		}
	}
	end;

OnTimer15000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer17000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer18000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer20000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer22000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer24000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer26500:
	for(.@i = 0; .@i <= 15; .@i++){
		if(unitexists('gid[.@i]) == true){
			unitkill 'gid[.@i];
		}
	
	}
	donpcevent instance_npcname("#CGH_BOSS")+"::Onstarttime";
	set 'bossf2 , 0;
	end;
	
}


//===================================
//--------------END--------------------

//物品掉落設定
1@gl_he,158,240,3	script	寶箱#chatnmtop	10005,{
	setarray .@gain[0],25864,25865,25866,25867;//獎勵物品設定
	setarray .@item25864min[1],1,1,2,2,3,3,4,4,5,5;
	setarray .@item25864max[1],5,6,7,8,9,10,10,10,10,10;
	setarray .@item25865min[1],1,1,1,1,2,2,2,2;
	setarray .@item25865max[1],3,4,4,5,5,6,6,7;
	setarray .@item25866min[1],1,3,5,8,11,14;
	setarray .@item25866max[1],3,7,11,16,22,28;
	setarray .@item25867min[1],1,1,1,1,2,2,3,3,4,4;
	setarray .@item25867max[1],15,20,25,30,30,30,30,30,30,30;
	set .@dropNum,'selectdiff+5;
	getmapxy(.@map$,.@x,.@y,BL_NPC,instance_npcname(strnpcinfo(0)));
	specialeffect 10;
	hideonnpc instance_npcname(strnpcinfo(0));
	
	for(.@i = 0; .@i <= rand(.@item25864min['selectdiff],.@item25864max['selectdiff]); .@i++ ){
		makeitem .@gain[0],1,.@map$,.@x+rand(5)-1,.@y+rand(5)-1;
	}
	for(.@i = 0; .@i <= rand(.@item25867min['selectdiff],.@item25867max['selectdiff]); .@i++ ){
		makeitem .@gain[3],1,.@map$,.@x+rand(5)-1,.@y+rand(5)-1;
	}
	if ('selectdiff >= 3){
		for(.@i = 0; .@i <= rand(.@item25865min['selectdiff-2],.@item25865max['selectdiff-2]); .@i++ ){
			makeitem .@gain[1],1,.@map$,.@x+rand(5)-1,.@y+rand(5)-1;
		}
	}
	if ('selectdiff >= 5){
		for(.@i = 0; .@i <= rand(.@item25866min['selectdiff-5],.@item25866max['selectdiff-5]); .@i++ ){
			makeitem .@gain[2],1,.@map$,.@x+rand(5)-1,.@y+rand(5)-1;
		}
	}
	end;
}

1@gl_he,158,247,1	script	奧斯卡#oscar_5	4_ED_OSCAR,{
	cutin "oscar01",2;
		mes "[奧斯卡]";
		mes "恭喜你完成淨化";
		mes "這是我提供的獎勵你就收下吧！";
		next;
		switch(select("領取獎勵並離開:我想再看看..")) {
		case 1:
			//---設定難度(目前為一階段打5次才會升一階)
			set #CGHTYPENUM,#CGHTYPENUM+1;
			if (#CGHTYPENUM >= 5){//打5次
			set #CGHTYPE,#CGHTYPE+1;
			set #CGHTYPENUM,0;
			}
			if (#CGHTYPE >= 10){
			set #CGHTYPE,10;
			set #CGHTYPENUM,0;
			}
			
			//------------------
			mes "[奧斯卡]";
			mes "目前最高可挑戰階級為："+#CGHTYPE+"";
			mes "提升至下一階級還需再通過^ff5555第["+#CGHTYPE+"]階級^000000 ^ff0000《"+#CGHTYPENUM+"》^000000至-->5次";
			next;
			getitem 25864,#CGHTYPE;
			warp "prontera",156,174;
				if(!getmapusers(strnpcinfo(4)))
					instance_destroy instance_id();
			cutin "oscar01",255;
			end;
		case 2:
			cutin "oscar01",255;
			end;
		}
		
	}