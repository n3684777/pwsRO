
////---------------------------------------------------------------
////				RWC附魔			-
////---------------------------------------------------------------
function	script	RWCEnc	{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您身上持有太多物品，請試著減少持有物品再回來嘗試！！";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您負重量過高，無法繼續下一步驟！！";
		close;
	}
	mes "[RWC飾品工匠]";
	mes "嗨！我可以在RWC紀念飾品上賦予強大的魔力。";
	next;
	switch(select("RWC紀念飾品附魔:RWC紀念飾品打洞:離開")){
	case 1: 
	set .@part, EQI_ACC_L;
	if (!getequipisequiped(.@part)) {
		mes "[RWC飾品工匠]";
		mes "對不起，您沒有任何RWC飾品在於右邊裝飾品位置。";
		close;
	}
	set .@equip_id, getequipid(.@part);
	if (.@equip_id < 2966 || .@equip_id > 2969) {
		mes "[RWC飾品工匠]";
		mes "不過我看您裝備在身上的飾品不是我能處理的範圍...";
		mes "請裝備RWC紀念飾品！";
		close;
	}
	set .@select, select("離開:紀念飾品附魔:洗滌附魔")-1;
	if (.@select == 0) {
		mes "[RWC飾品工匠]";
		mes "好吧！下次見...";
		close;
	}
	set .@equip_refine, getequiprefinerycnt(.@part);
	setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
	if (.@select == 1) {
		switch(getequipid(.@part)) {
		case 2966:
			setarray .@option[0],2,2,4,4;
			break;
		case 2967:
			setarray .@option[0],0,2,4,4;
			break;
		case 2968:
			setarray .@option[0],1,1,3,3;
			break;
		case 2969:
			setarray .@option[0],0,1,3,3;
			break;
		default:
			mes "[RWC飾品工匠]";
			mes "對不起，我不能對您正在配戴的飾品做附魔...";
			mes "如果您裝備RWC紀念飾品在左側，請卸下並且嘗試裝備到右側。";
			close;
		}
		for(set .@i,3; .@i>=0; set .@i,.@i-1) {
			if (.@equip_card[.@i] == 0) {
				set .@slot, .@i;
				set .@op_type, .@option[.@i];
				break;
			}
		}
		switch(.@op_type) {
		case 4:
			mes "[RWC飾品工匠]";
			mes "您想選擇哪一種附魔？";
			next;
			setarray .@enchant_select[0],1,2,3,4;
			set .@i, select("取消:鬥志:ATK:MHP:HP")-2;
			break;
		case 3:
			setarray .@enchant_select[0],5,6,7;
			set .@i, select("取消:MATK:魔力:SP")-2;
			break;
		case 2:
			mes "[RWC飾品工匠]";
			mes "^ff0000請注意，有 25% 的機率會失敗，如果發生這種情況，RWC紀念飾品將會消失。^000000 接下來，您想選擇哪一種附魔？";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,14;
			set .@i, select("Cancel:STR:AGI:VIT:INT:DEX:LUK:SP")-2;
			break;
		case 1:
			mes "[RWC飾品工匠]";
			mes "^ff0000請注意，有 25% 的機率會失敗，如果發生這種情況，RWC紀念飾品將會消失。^000000 接下來，您想選擇哪一種附魔？";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,15,16;
			set .@i, select("Cancel:STR:AGI:VIT:INT:DEX:LUK:MHP:HP")-2;
			break;
		case 0:
			mes "[RWC飾品工匠]";
			mes "您的RWC紀念飾品已經無法在附魔了！";
			close;
		}
		if (.@i == -1) {
			mes "[RWC飾品工匠]";
			mes "好吧！下次見...";
			close;
		}
		mes "[RWC飾品工匠]";
		mes "附魔的種類將隨機選擇。^ff0000過程中請物登出，結束遊戲。^000000 我們該繼續嗎？";
		next;
		if(select("取消:繼續附魔") == 1) {
			mes "[RWC飾品工匠]";
			mes "好吧！下次見...";
			close;
		}
		set .@enchant_type, .@enchant_select[.@i];
		if (!getequipisequiped(.@part)) {
			mes "[RWC飾品工匠]";
			mes "不要在我工作的時候，卸下您的裝備好嗎？";
			close;
		}
		switch(.@enchant_type) {
		case 1:
			setarray .@enc[0],4811,4810,4809; //Fighting_Spirit1,Fighting_Spirit2,Fighting_Spirit3
			break;
		case 2:
			setarray .@enc[0],4819,4766,4767; //Atk1,Atk2,Atk3
			break;
		case 3:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 4:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		case 5:
			setarray .@enc[0],4760,4761,4806; //Matk1,Matk2,Matk3
			break;
		case 6:
			setarray .@enc[0],4815,4814,4813; //Spell1,Spell2,Spell3
			break;
		case 7:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 8:
			setarray .@enc[0],4700,4701,4702; //Strength1,Strength2,Strength3
			break;
		case 9:
			setarray .@enc[0],4730,4731,4732; //Agility1,Agility2,Agility3
			break;
		case 10:
			setarray .@enc[0],4740,4741,4742; //Vitality1,Vitality2,Vitality3
			break;
		case 11:
			setarray .@enc[0],4710,4711,4712; //Inteligence1,Inteligence2,Inteligence3
			break;
		case 12:
			setarray .@enc[0],4720,4721,4722; //Dexterity1,Dexterity2,Dexterity3
			break;
		case 13:
			setarray .@enc[0],4750,4751,4752; //Luck1,Luck2,Luck3
			break;
		case 14:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 15:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 16:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		default:
			mes "[RWC飾品工匠]";
			mes "我們有一個問題，讓我檢查一下。";
			close;
		}

		if (.@enchant_type < 8)
			set .@i, rand(1,300); // 0% break chance.
		else
			set .@i, rand(1,400); // 25% break chance.

		     if (.@i < 151) set .@enchant, .@enc[0];
		else if (.@i < 251) set .@enchant, .@enc[1];
		else if (.@i < 301) set .@enchant, .@enc[2];
		else set .@enchant,9;

		set .@equip_card[.@slot], .@enchant;
		if (.@slot == 2 && .@enchant == 0) {
			set .@equip_card[3],0;
		} else if (.@slot == 1 && .@enchant == 0) {
			set .@equip_card[2],0;
			set .@equip_card[3],0;
		} else if (.@slot == 0 && .@enchant == 0) {
			set .@equip_card[1],0;
			set .@equip_card[2],0;
			set .@equip_card[3],0;
		}

		delequip .@part;
		if (.@enchant == 9) {
			mes "[RWC飾品工匠]";
			mes "天阿！";
 			mes "RWC紀念飾品沒辦法承受附魔強大的力量，我很抱歉...";
			specialeffect2 EF_LORD;
			close;
		}
		if (.@enchant == 0) { // Should never happen.
			mes "[RWC飾品工匠]";
			mes "好像什麼事都沒發生，這是一種恥辱，請您重試一次！";
		} else {
			mes "[RWC飾品工匠]";
			mes "非常好！";
			mes "附魔成功了！它將會在第 No.^990000"+(.@slot+1)+"^000000 插槽上運作！";
			specialeffect2 EF_REPAIRWEAPON;
		}

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	} else if (.@select == 2) {
		mes "[RWC飾品工匠]";
		mes "我只會幫您初始化附魔，且不會對卡片照成影響，您要繼續嗎？";
		next;
		if(select("取消:洗滌附魔") == 1) {
			mes "[RWC飾品工匠]";
			mes "如果您改變心意，就回來找我吧...";
			close;
		}
		if (countitem(6665) == 0) {
			mes "[RWC飾品工匠]";
			mes "對不起，您沒有持有任何 RWC重置券 ...";
			close;
		}
		if (.@equip_card[3] == 0) {
			mes "[RWC飾品工匠]";
			mes "這個紀念飾品是乾淨的，我無法做任何事...";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[RWC飾品工匠]";
		mes "您選擇的項目附魔將會被初始化...";
		delitem 6665,1; //RWC_Inicializer
		delequip .@part;
	
//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] >= 4700) // Armor Enchant System
				set .@equip_card[.@i],0;
		}
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
		
		close;
	}
	case 2:
	set .@part, EQI_ACC_L;
	if (!getequipisequiped(.@part)) {
		mes "[RWC飾品工匠]";
		mes "雖然我的工作是在RWC紀念飾品上鑽洞，";
		mes "但對不起，您沒有任何RWC飾品在於右邊裝飾品位置。";
		close;
	}
	mes "[RWC飾品工匠]";
	mes "雖然我的工作是在RWC紀念飾品上鑽洞，但我只能鑽第一卡片位置的插槽！";
	mes "且紀念飾品是不能被附魔過的！";
	next;
	set .@equip_id, getequipid(.@part);
	if (.@equip_id != 2966 && .@equip_id != 2968) {
		mes "[RWC飾品工匠]";
		mes "然而我看到您上上持有的RWC飾品已經鑽過動了！";
		close;
	}
	mes "[RWC飾品工匠]";
	mes "您應該知道鑽洞這舉動是非常危險的！ ^ff0000成功率只有 50%^000000 您還要繼續嗎？";
	next;
	if(select("取消:開始鑽洞") == 1) {
		mes "[RWC飾品工匠]";
		mes "再見！";
		close;
	}
	if (.@equip_id == 2966) {
		set .@slotted, 2967; //RWC_2012_Ring_
		set .@name$,"RWC紀念戒指";
		set .@str$,"ring";
	} else if (.@equip_id == 2968) {
		set .@slotted, 2969; //RWC_2012_Pendant_
		set .@name$,"RWC紀念墜子";
		set .@str$,"pendant";
	} else {
		mes "[RWC飾品工匠]";
		mes "您右邊裝飾品的項目是我不能鑽洞的！";
		close;
	}
	if (getequipcardid(.@part,3) > 0) {
		mes "[RWC飾品工匠]";
		mes "這紀念飾品已經被附魔了，我無法鑽洞！";
		close;
	}
	delequip .@part;
	if (rand(1,10) > 5) {
		getitem .@slotted,1;
		specialeffect2 EF_REPAIRWEAPON;
		mes "[RWC飾品工匠]";
		mes "成功了！您的 "+.@name$+" 現在有一個卡片插槽，拿去看吧！";
		close;
	} else {
		specialeffect2 EF_LORD;
		mes "[RWC飾品工匠]";
		mes "該死！ "+.@str$+"... 抱歉，飾品在過程中損壞了...";
		close;
	}
	case 3:
	close;
	}
}


