//===== rAthena Script =======================================
//= Nightmare Biolabs
//===== Description: =========================================
//= Nightmare Biolabs Monster Spawn Script.
//===== Additional Comments: =================================
//= 1.0 First version. Boss and slaves spawn are assumptions
//		based on lhz_dun.txt monsters spawn. [Capuche]
//= 1.1 Changed MvP spawn based on the amount of
//		monsters killed. [Capuche]
//============================================================

lhz_dun_n,0,0	monster	Eremes Guille	3208,20,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Magaleta Sorin	3209,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Catherine Cheiron	3210,15,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Shecil Damon	3211,20,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Harword Alt-Eisen	3212,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Seyren Windsor	3213,10,5000,0,"lhz_dun_n::OnRegularDead"

lhz_dun_n,0,0	monster	Randel Lawrence	3226,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Flamel Emule	3227,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Celia Alde	3228,15,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Chen Liu	3229,15,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Gertie Wie	3230,15,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Alphoccio Basil	3231,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Trentini	3232,10,5000,0,"lhz_dun_n::OnRegularDead"

lhz_dun_n,0,0,0	script	lhz_dun_n	-1,{
	end;
OnRegularDead:
	.@mob_id = killedrid;
	if (.lhz_dun_n[.@mob_id] < 110 && .lhz_dun_n_boss == 0) {
		.lhz_dun_n[.@mob_id]++;
		if (.lhz_dun_n[.@mob_id] == 110) {
			set .lhz_dun_n[.@mob_id],0;
			.@mini_boss = .@mob_id + 6 + (.@mob_id > 3213);	// mini boss version ID: regular ID +6 for non-transcendent, +7 for others
			monster "lhz_dun_n",0,0,"--ja--",.@mini_boss,1, "lhz_dun_n::OnMiniDead";
			mapannounce "lhz_dun_n", "死神的聲音: 感受到非常強烈的氣息。", bc_map,0x00FF00;	// unknown message
			
		}
	}
	end;
OnMiniDead:
	if (.lhz_dun_boss < 4 && .lhz_dun_n_boss == 0) {
		set .lhz_dun_boss,.lhz_dun_boss+1;
		if (.lhz_dun_boss == 4) {	
			set .lhz_dun_boss,0;
			setarray .@mvp[0],3220,3221,3222,3223,3224,3225,3240,3241,3242,3243,3244,3245,3246;
			setarray .@mvp$[0],"艾勒梅斯","瑪嘉蕾特","迪文","哈沃德","凱特莉娜"," 賽依連","蘭達羅倫斯","普拉梅姆","西里亞阿迪","陳理歐","科迪","雅歐帕奇爾","特蘭提尼";
			
			set .@m,rand(getarraysize(.@mvp));
			monster "lhz_dun_n",140,230,"--ja--",.@mvp[.@m],1, "lhz_dun_n::OnMyMVPDead";
			mapannounce "lhz_dun_n", "死神的聲音: 原來你不是正港的勇士。正港的勇士終於出現了!", bc_map,0xff0000;	// unknown message
			mapannounce "lhz_dun_n", "死神的聲音: 你好啊~"+.@mvp$[.@m]+"，好久不見啊!", bc_map,0xff0000;	// unknown message
			.lhz_dun_n_boss = 1;
		}
	}
	end;

OnMyMVPDead:
	initnpctimer;
	.lhz_dun_n_delay = 0;
	killmonster "lhz_dun_n", "lhz_dun_n::OnMyMVPDead";
	mapannounce "lhz_dun_n", "死神的聲音: 後會有期了，曾是戰士的你喔!", bc_map, 0x00FF00;
	end;
OnTimer60000:
	.lhz_dun_n_delay++;
	if (.lhz_dun_n_delay < 120) // unknown delay (currently 100 mins)
		initnpctimer;
	else {
		stopnpctimer;
		mapannounce "lhz_dun_n", "死神的聲音: 來吧!正港的戰士喔!", bc_map, 0x00FF00;
		.lhz_dun_n_delay = 0;
		.lhz_dun_n_boss = 0;
	}
	end;
}
