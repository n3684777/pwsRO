//===== rAthena Script ======================================= 
//= 轉蛋函數
//===== By: ================================================== 
//= Wayne
//===== Current Version: ===================================== 
//= 1.0
//===== Description: ========================================= 
//= 可設計轉蛋物品，並決定保底物品及需開啟次數
//===== Additional Comments: ================================= 
//= 1.0 First version. [Wayne]
//============================================================ 
// === 天波工作組 (https://www.angeling-studio.com/)
//============================================================
//
// 使用方法：callfunc "F_Gashapon", item_id;
// 使用範例：14296,Angel_Scroll,天使轉蛋,2,20,,10,,,,,0xFFFFFFFF,63,2,,,,,,{ callfunc "F_Gashapon", 14296; },{},{}
//
function	script	F_Gashapon	{
	// .@DEBUG_FLAG 	: 如果設為 1，則 map-server 視窗會輸出轉蛋的機率及對應物品列表，
	// 						用於設計完一個新轉蛋後做檢查確認用。
	// .@announce_rate 	: 開到的物品，若低於設定機率，則會進行全伺服器廣播，採用萬分表(10000=100%)
	//						若不要有廣播設定，請將此設為 0 
	.@DEBUG_FLAG = 1;
	.@announce_rate = 100;
	
	.@egg_item = getarg(0);
	switch(.@egg_item) {
	/*
		setarray .@fun_name$[0]	: 轉蛋機率與物品列表，例如：
		
			setarray .@fun_name$[0], "10,501,10","500,502,6","2000,504,4","10000,506,2,507,2";		
			第一個值 "10,501,1" : 有 0.1 %的機率取得 501 道具1個
			第二個值 "500,502,6" : 有 5% 的機率獲得 502 道具6個
			第二個值 "2000,504,4" : 有 20% 的機率獲得 504 道具4個
			第三個值 "10000,506,2,507,2" : 若機率落在 > 20% 的機率以外則獲得 506 道具2個 或 507 道具2個 

			備註 :
			1. 機率填寫必須由小到大
			2. 最後一個機率一定要是 10000 (萬分率)
			
		.@guarantee_item		: 保底物品的物品編號
		.@guarantee_need_times 	: 開中保底物品，需要的開啟次數
			
			備註 :
			1. 若無保底設定，不要設定這兩個變數即可
	
	*/
	case 14296:	// 天使轉蛋
		setarray .@fun_name$[0],"10,501,10","500,502,6","2000,504,4","10000,506,2,507,2";
		.@guarantee_item = 501;
		.@guarantee_need_times = 10;
		break;
	case 14297:	// 惡魔轉蛋
		setarray .@fun_name$[0],"50,607,10","200,608,10","2200,504,50","10000,501,15,502,10";
		.@guarantee_item = 607;
		.@guarantee_need_times = 30;
		break;	
	case 61001:	// 普通道館獎勵
		setarray .@fun_name$[0],"30,7582,100,7606,1200",
	                           	"500,7582,60,7606,800",
		                        "2000,7582,10,7606,50",
		                        "10000,7606,5";
		//.@guarantee_item = 7582;
		//.@guarantee_need_times = 1000;
		break;	
	case 61002:	// 中等道館獎勵
		setarray .@fun_name$[0],"250,6230,1,6234,1,12246,1",
		                        "500,616,1,6226,2,6225,2",
								"600,6241,3,6240,3",
								"10000,6223,3,6224,3,7619,3,7620,3,12214,2,12405,15";
		//.@guarantee_item = 6231;
		//.@guarantee_need_times = 1000;
		break;
	case 61003:	// 高等道館獎勵
		setarray .@fun_name$[0],"50,22829,1",
		                        "250,6229,1,6233,1,12246,1",
		                        "500,616,1,6226,4,6225,4,6241,4,6240,4",
								"10000,6223,6,6224,6,7619,6,7620,6,12214,3,12405,20";
		//.@guarantee_item = 6231;
		//.@guarantee_need_times = 1000;
		break;
	case 61004:	// 夢魘道館獎勵
		setarray .@fun_name$[0],"50,22829,1",
		                        "100,6584,1,6584,1,6238,1,6238,1,12246,1",
								"250,6993,1,6994,1",
		                        "500,616,1,6226,4,6225,4,12246,1,6241,6,6240,6",
								"10000,6223,10,6224,10,7619,10,7620,10,12214,5,12405,30";
		//.@guarantee_item = 6231;
		//.@guarantee_need_times = 1000;
		break;
	case 61005:	// 終極夢魘道館獎勵
		setarray .@fun_name$[0],"20,22654,1",
		                        "100,6870,1,6871,1,6876,1,6877,1,22829,1",
		                        "200,6584,1,6585,1",
								"500,6635,3",
								"10000,6225,6,6226,6,6240,6,6241,6,12214,5,12405,30";
		//.@guarantee_item = 6231;
		//.@guarantee_need_times = 1000;
		break;
	case 61030:	// 萌新轉蛋
		setarray .@fun_name$[0],"10,4494,1",
		                        "60,2566,1,2856,1,2857,1,15023,1",
		                        "1000,20737,1,20746,1,5914,1,5584,1,18599,1,5495,,18858,1",
								"10000,14533,1,14996,1,23043,1,6484,1,616,1,12900,1,13607,5,12913,1,12903,1,12904,1,12905,1,12906,1,12907,1,12908,1,16770,1,6423,3,12914,1,12922,1,12919,1,23630,1,12901,1";
		//.@guarantee_item = 13549;
		//.@guarantee_need_times = 1;
		break;
	case 22654:	// 黃金卡冊
		setarray .@fun_name$[0],"2000,4352,1,4359,1,4361,1,4363,1,4365,1,4367,1,4560,1,4561,1,4562,1,4563,1,4564,1,4565,1,4566,1",
		                        "3000,4128,1,4145,1,4342,1,4403,1,4374,1,4376,1",
		                        "5000,4526,1,4527,1,4528,1,4529,1,4407,1,4408,,4559,1",
								"10000,4121,1,4123,1,4131,1,4132,1,4134,1,4135,1,4142,1,4143,1,4144,1,4146,1,4147,1,4148,1,4168,1,4236,1,4263,1,4276,1,4305,1,4318,1,4324,1,4330,1,4372,1,4386,1,4419,1,4425,1,4430,1,4441,1,4451,1,4456,1,4457,1,4507,1,4525,1,4534,1,4574,1,4576,1,4578,1,4580,1,4590,1,4591,1,4592,1,4601,1,4603,1,4604,1";
		//.@guarantee_item = 13549;
		//.@guarantee_need_times = 1;
		break;
	}
	
	.@check_index = 0;
	for(.@i=0; .@fun_name$[.@i]!=""; .@i++) {
		deletearray .@array$;
		explode(.@array$,.@fun_name$[.@i],",");
				
		.@rate[.@i] = atoi(.@array$[0]);
		for(.@j=1; .@j<getarraysize(.@array$); .@j+=2) {
			setd ".@item_"+.@i+"["+(.@j/2)+"]",atoi(.@array$[.@j]);
			setd ".@num_"+.@i+"["+(.@j/2)+"]",atoi(.@array$[.@j+1]);
			if(.@guarantee_item == atoi(.@array$[.@j]))
				.@check_index++;
		}		
		
		if(.@DEBUG_FLAG) {
			debugmes "機率 : "+.@rate[.@i];
			for(.@j=0; .@j<getarraysize(getd(".@item_"+.@i)); .@j++) {
				debugmes ".@item : " + getitemname(getd(".@item_"+.@i+"["+.@j+"]")) + " x " + getd(".@num_"+.@i+"["+.@j+"]");
			}
		}
	}
	
	if(!.@check_index) {
		debugmes "[轉蛋系統]"+getitemname(.@egg_item)+"(ID:"+.@egg_item+")的保底物品並沒有出現在轉蛋可獲得的列表中。";
		debugmes "[轉蛋系統]請再次檢查設定。";
	}	
	
	set .@rand,rand(1,10000);
	for(.@k=0; .@k<getarraysize(.@rate); .@k++) {
		if(.@rand <= .@rate[.@k]) {
			.@rand_item = rand(getarraysize(getd(".@item_"+.@k)));
			set .@return_item_id, getd(".@item_"+.@k+"["+.@rand_item+"]");
			set .@return_item_amount, getd(".@num_"+.@k+"["+.@rand_item+"]");
			setd "egg_"+.@egg_item+"_times", getd("egg_"+.@egg_item+"_times")+1;
			break;
		}
	}
	
	// check guarantee item
	if(.@guarantee_item == .@return_item_id)
		setd "egg_"+.@egg_item+"_times", 0;
	else if(.@guarantee_item) {
		.@open_times = getd("egg_"+.@egg_item+"_times");
		if(.@open_times >= .@guarantee_need_times) {
			for(.@k=0; .@k<getarraysize(.@rate); .@k++) {
				.@index = inarray(getd(".@item_"+.@k),.@guarantee_item);
				if(.@index >= 0)
					break;
			}			
			.@return_item_id = .@guarantee_item;
			.@return_item_amount = getd(".@num_"+.@k+"["+.@index+"]");
			setd "egg_"+.@egg_item+"_times", 0;
		}
	}
		
	if(.@announce_rate && .@rate[.@k] <= .@announce_rate)
		announce "恭喜玩家「"+strcharinfo(0)+"」使用"+getitemname(.@egg_item)+"開到了"+.@return_item_amount+"個"+getitemname(.@return_item_id)+"。",bc_all; 
	getitem .@return_item_id,.@return_item_amount;
	return .@return_item_id;
}