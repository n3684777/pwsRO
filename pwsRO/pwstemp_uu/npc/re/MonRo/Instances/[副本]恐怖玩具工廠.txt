xmas,238,310,6	script	恐怖玩具工廠#02	837,{

	mes "----------------------------";	
	mes ">> 副本獎勵清單";
	mes "----------------------------";
	mes "<ITEM>染血的硬幣<INFO>7642</INFO></ITEM>";
	mes "<ITEM>溫暖紅茶<INFO>11563</INFO></ITEM>";
	mes "<ITEM>甜蜜餅乾<INFO>11564</INFO></ITEM>";
	mes "<ITEM>席琳的蝴蝶結<INFO>18849</INFO></ITEM>";
	mes "<ITEM>邪靈手套<INFO>2980</INFO></ITEM>";
	mes "<ITEM>榮耀十架<INFO>16029</INFO></ITEM>";
	mes "<ITEM>受傷的心<INFO>2977</INFO></ITEM>";
	mes "<ITEM>宅心仁厚<INFO>2978</INFO></ITEM>";
	mes "<ITEM>新鮮玫瑰<INFO>18848</INFO></ITEM>";
	mes "<ITEM>老舊陽傘<INFO>13442</INFO></ITEM>";
	mes "<ITEM>陰暗步行長靴<INFO>2486</INFO></ITEM>";
	mes "<ITEM>紅色手提燈<INFO>2976</INFO></ITEM>";
	mes "<ITEM>(服飾)新鮮玫瑰<INFO>19687</INFO></ITEM>";
	mes "<ITEM>(服飾)紅色無邊軟帽<INFO>19701</INFO></ITEM>";
	mes "<ITEM>(服飾)聖誕玩偶髮箍<INFO>19686</INFO></ITEM>";
	mes "----------------------------";
	close2;
	cutin "",255;
	end;
}
xmas,237,303,3	script	凱瑟琳．澤塔．瓊斯	10032,{
			set .@xm_time, checkquest(12331,PLAYTIME);
			set .@party_id,getcharid(1);
	switch(select("創建記憶迷宮:進入恐怖玩具工廠:取消")){
		case 1:
		if (BaseLevel < 140) {	mes "^00C400 此任務最低等級限制為 140 ^000000";		close;	}
		if (.@xm_time == 0 || .@xm_time == 1) {	mes "副本冷卻中";close;
		}else{
			if (isbegin_quest(12331))
				erasequest 12331;
			if (getpartyleader(getcharid(1),2) == getcharid(0)) {
			if (instance_create("恐怖玩具工廠") < 0)  {	mes "通道已經打開了!";	close;}
			announce "★ 副本公告 ★ 隊伍 [ "+getpartyname(getcharid(1)) +" ] 準備挑戰【 恐怖玩具工廠 】副本！",15,0x33ea91;

				mes "通道已經打開了!";
				close;
				}
				mes "請隊長與我談話吧!";
				close;
		}
		case 2: 
		if (.@xm_time == 0 || .@xm_time == 1) {mes "副本冷卻中";	close;}
			if (isbegin_quest(12331))
				erasequest 12331;
			if (instance_enter("恐怖玩具工廠") != 0) { 
				mes "請先開啟通道";
				close;
				}
				setquest 12331;
				end;
			
		case 3:
		end;
	
		}
}		

1@xm_d,112,20,6	script	凱瑟琳．澤塔．瓊斯#0	10032,{
'xm_d_map$ = instance_mapname("1@xm_d");
	if (getstatus(SC_MONSTER_TRANSFORM,1) > 0) {
		mes "[凱瑟琳．澤塔．瓊斯]";
		mes "如果你處於變身狀態，你不能來這裡.";
		next;
		mes "[凱瑟琳．澤塔．瓊斯]";
		mes "請等到轉換完成。 在你變得正常之前你不能移動到另一個區域.";
		close;
	}
	if (getpartyleader(getcharid(1),2) != getcharid(0)) {
		mes "[凱瑟琳．澤塔．瓊斯]";
		mes "呃..給我一點時間。我正在和負責人談話。請稍候.";
		close;
	}
	mes "[凱瑟琳．澤塔．瓊斯]";
	mes "沒有同伴變裝成其他魔物後跟你來吧?!";
	npctalk "凱瑟琳．澤塔．瓊斯: 沒有同伴變裝成其他魔物後跟你來吧?!";
	next;
	switch( select( "退出對話","聽她的故事","我知道你的情況。讓我們快點開始吧!" ) ) {
	case 1:
		mes "[凱瑟琳．澤塔．瓊斯]";
		mes "當你準備好時，請告訴我.";
		close;
	case 2:
		donpcevent instance_npcname("凱瑟琳．澤塔．瓊斯#01") + "::OnStart";
		close;
	case 3:
		donpcevent instance_npcname("凱瑟琳．澤塔．瓊斯#01") + "::OnStart2";
		close;
	}
}

1@xm_d,112,20,1	script	凱瑟琳．澤塔．瓊斯#01	10032,{
	end;
OnStart:
	enablenpc instance_npcname("#bgm01");
	enablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#01");
	disablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#0");
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 這個區域是工廠1號。 它曾經是玩具和玩偶在作為禮物贈送之前存放的地方。";
	sleep 5000;
	npctalk "凱瑟琳．澤塔．瓊斯: 啊，我記得有幾件事情。如果有人不穿員工的制服，那些守衛會打他們..........";
	sleep 5000;
	npctalk "凱瑟琳．澤塔．瓊斯: 順便一提....";
	donpcevent instance_npcname("#fac1ct") + "::OnStart";
	sleep 3000;
	mapannounce 'xm_d_map$, "工廠宣布: 各位玩具工廠的夥伴們~ 都起來吧!愉快的工作時間到囉~", bc_map, "0x00ff44";
	sleep 6000;
	npctalk "凱瑟琳．澤塔．瓊斯: 發生了什麼事? 玩具和玩偶正四處徘徊，而不是真正的作業員。看起來就好像他們是僱員...";
	sleep 5000;
	npctalk "凱瑟琳．澤塔．瓊斯: 我認為也許是其他靈魂已經在空工廠居住太久了...";
	sleep 3000;
	mapannounce 'xm_d_map$, "工廠宣布: 記得隨時清理垃圾以及注意安全!", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 使他們就像人類在這裡工作一樣。";
	sleep 3000;
	mapannounce 'xm_d_map$, "工廠宣布: 今天讓我們為每個孩子的夢想製作禮物.", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 但是，現在不是快樂的時候。一但找到工廠的最後區域，我們就要停止所有生產線。";
	sleep 1000;
	mapannounce 'xm_d_map$, "工廠宣布: 請開始產品系列No.1。別忘了戴安全帽!!", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 啊，也許我們應該將這些玩具和禮品盒恢復原狀";
	sleep 5000;
	npctalk "凱瑟琳．澤塔．瓊斯: 呃? 我......我的意思是......按原樣歸位......是的......我們需要和他們戰鬥嗎? 我不確定...";
	sleep 2000;
	mapannounce 'xm_d_map$, "工廠宣布: 所有員工必須穿統一的正確和識別，請與安全檢查", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 過去附近有一箱制服..";
	enablenpc instance_npcname("制服箱子#1");
	enablenpc instance_npcname("制服箱子#2");
	enablenpc instance_npcname("制服箱子#3");
	sleep 5000;
	npctalk "凱瑟琳．澤塔．瓊斯: 啊! 他們在我身後。你最好換上製服。幸運的是，我的員工卡還在...";
	sleep 5000;
	npctalk "凱瑟琳．澤塔．瓊斯: 我會先走一步。讓我們在第二條生產線附近見面";
	sleep 6000;
	disablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#01");
	disablenpc instance_npcname("#bgm01");
	end;

OnStart2:
	enablenpc instance_npcname("#bgm01");
	enablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#01");
	disablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#0");
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 啊..我們來過這裡嗎? 那跟之前一樣，我們在那個地方會合吧!";
	sleep 6000;
	disablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#01");
	enablenpc instance_npcname("制服箱子#1");
	enablenpc instance_npcname("制服箱子#2");
	enablenpc instance_npcname("制服箱子#3");
	mapannounce 'xm_d_map$, "工廠宣布: 各位玩具工廠的夥伴們~ 都起來吧!愉快的工作時間到囉~", bc_map, "0x00ff44";
	sleep 6000;
	mapannounce 'xm_d_map$, "工廠宣布: 記得隨時清理垃圾以及注意安全!", bc_map, "0x00ff44";
	sleep 6000;
	mapannounce 'xm_d_map$, "工廠宣布: 今天讓我們為每個孩子的夢想做禮物.", bc_map, "0x00ff44";
	donpcevent instance_npcname("#fac1ct") + "::OnStart";
	disablenpc instance_npcname("#bgm01");
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	// disable some warps
	disablenpc instance_npcname("#fac3wp");
	disablenpc instance_npcname("#fac3wp2");
	disablenpc instance_npcname("#fac4wp");
	disablenpc instance_npcname("#fac4wp2");
	disablenpc instance_npcname("#fac5wp");
	disablenpc instance_npcname("#fac6wp");
	end;
}

1@xm_d,112,20,0	script	#bgm01	-1,9,9,{
	end;
OnTouch:
	playBGM "99";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,13,105,6	script	制服箱子#1	4_NONMYSTCASE,{
	progressbar "ffff00",1;
	playBGM "52";
	.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
	if (.@mob_id_transform == 1246 || .@mob_id_transform < 1) {
		mes "^0000ff已換上工作服，工作執行中可隨時來此再次變身。^000000";
		transform 1246,180000;// COOKIE_XMAS
		close;
	}
	mes "^ff0000你已經穿上制服了!^000000";
	close;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@xm_d,116,16,6	duplicate(制服箱子#1)	制服箱子#2	4_NONMYSTCASE
1@xm_d,10,20,6	duplicate(制服箱子#1)	制服箱子#3	4_NONMYSTCASE

1@xm_d,1,5,3	script	#fac1ct	CLEAR_NPC,{
	end;
OnStart:
	.@event$ = instance_npcname("#fac1ct") + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	areamonster 'xm_d_map$,16,24,114,112, "禮物包裝負責人",2989,31, .@event$;	// XM_COOKIE
	areamonster 'xm_d_map$,16,24,114,112, "包裝好的箱子",2991,36, .@event$;// XM_MYSTCASE
	end;

OnMyMobDead:
	if (mobcount( 'xm_d_map$, instance_npcname("#fac1ct") + "::OnMyMobDead" ) < 30)
		initnpctimer;
	end;

OnTimer1000:
	killmonster 'xm_d_map$, instance_npcname("#fac1ct") + "::OnMyMobDead";
	enablenpc instance_npcname("作業主任#fac1bs");
	mapannounce 'xm_d_map$, "作業主任的公告: 咦? 人都跑去哪裡! 這些作業員怎麼敢不在生產線上!", bc_map, "0xff8800";
	for ( .@i = 61; .@i <= 89; .@i++ )
		disablenpc instance_npcname( "alert#"+ .@i );
	stopnpctimer;
	end;

OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,71,129,3	script	作業主任#fac1bs	4_M_COOKIE,{
	if (getpartyleader(getcharid(1),2) == getcharid(0)) {
		.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
		mes "[作業主任]";
		if (.@mob_id_transform == 1246) {
			mes "大家趕快工作。孩子們還在等待禮物。";
			next;
			mes "[作業主任]";
			mes "趕快拿著^ff0000那邊的禮盒^000000後前往軌道的東側末端吧! 體積較大要小心搬運!";
			enablenpc instance_npcname("#pck1");
			npctalk "作業主任: 趕快拿著那邊的禮盒後前往軌道的東側末端吧! 體積較大要小心搬運!";
		}
		else if (.@mob_id_transform == 1249)
			mes "你為什麼在偷懶! 把 ^ff0000這個箱子放這^000000 然後往東邊移動.";
		else {
			mes "什麼!?人類!!!";
			donpcevent instance_npcname("作業主任#fac1bs") + "::OnAlert";
		}
		close;
	}
	end;

OnAlert:
	.@npc_name$ = instance_npcname("作業主任#fac1bs");
	.@event$ = .@npc_name$ + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	npctalk "作業主任: 守衛! 守衛在哪裡! 有人類入侵!!";
	sleep 3000;
	disablenpc .@npc_name$;
	for ( .@i = 0; .@i <= 10; .@i++ ) {
		areamonster 'xm_d_map$,61,118,71,128, "玩具工廠警報兵",2990,1, .@event$;// XM_CRUISER
		sleep 300;
	}
	initnpctimer;
	end;
OnMyMobDead:
	end;

OnTimer60000:
	.@npc_name$ = instance_npcname("作業主任#fac1bs");
	killmonster 'xm_d_map$, .@npc_name$ + "::OnMyMobDead";
	enablenpc .@npc_name$;
	npctalk "作業主任: 這些日子裡發生的事情...";
	stopnpctimer;
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,65,127,6	script	#pck1	4_NONMYSTCASE,{
	progressbar "ffff00",1;
	.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
	if (.@mob_id_transform == 1246) {
		mes "^0000ff提起了包裝好的禮物，因體積過大幾乎遮住了全身。^000000";
		transform 1249,180000;// MYSTCASE
	}
	else if (.@mob_id_transform == 1249)
		mes "^009900你已經拿起了禮物。^000000";
	else
		mes "^ff0000你並沒有穿著制服。無法拿取禮物。^000000";
	close;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,76,129,0	script	#fac1wp	WARPNPC,2,2,{
OnTouch:
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 1249) {
		warp 'xm_d_map$,88,129;
		playBGM "54";
	}
	end;
}

1@xm_d,179,129,0	script	#fac2wp	WARPNPC,2,2,{
OnTouch:
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 1249) {
		warp 'xm_d_map$,183,100;
		playBGM "54";
	}
	end;
}

1@xm_d,1,5,3	script	#alert1	CLEAR_NPC,{
	end;
OnStart:
	.@r = rand(1,10);
	if (.@r == 1)
		mapannounce 'xm_d_map$, "工廠宣布: 在產品線上發現入侵者。警衛，請立即請出去。",bc_map,"0x00ffff";
	else if (.@r == 2)
		mapannounce 'xm_d_map$, "工廠宣布: 入侵者已被識別。區別代碼AX0829。類型：人。追!!", bc_map, "0x00ffff";
	else if (.@r == 3)
		mapannounce 'xm_d_map$, "衛兵隊的公告: 請立即離開這裡，入侵者。", bc_map, "0xffff00";
	else if (.@r == 4)
		mapannounce 'xm_d_map$, "工廠宣布: 入侵者，舉起雙手投降。否則，我會射殺你。", bc_map, "0x00ffff";
	else if (.@r == 5)
		mapannounce 'xm_d_map$, "工廠宣布: 派遣守衛。壓制入侵者。", bc_map, "0x00ffff";
	else if (.@r == 6)
		mapannounce 'xm_d_map$, "小心: 工廠經理正在檢查。除掉入侵者。", bc_map, "0xff4444";
	else if (.@r == 7)
		mapannounce 'xm_d_map$, "工廠宣布: 壞消息。發現入侵者。警衛，請動員。", bc_map, "0x00ff88";
	else if (.@r == 8)
		mapannounce 'xm_d_map$, "工廠宣布: 入侵者是破壞工廠的兇手。如果有必要，可以殺死他們。", bc_map, "0xff9999";
	else if (.@r == 9)
		mapannounce 'xm_d_map$, "工廠宣布: 好的就是它，遊戲結束了!離開!!", bc_map, "0x00ffff";
	else
		mapannounce 'xm_d_map$, "衛兵隊的公告: 入侵者被發現了!他們看起來是人類!我們會把他們除掉!", bc_map, "0xffff00";
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,10,24,0	script	alert#61	-1,10,10,{
OnTouch_:
	if (getstatus(SC_MONSTER_TRANSFORM,1) != 1246) {
		switch( atoi(strnpcinfo(2)) ) {
			case 63: case 66:
			case 69: case 72:
			case 79: case 82:
			case 85: case 88:
				.@count = 4;
				break;
			case 61: case 64:
			case 67: case 70:
			case 73: case 75:
			case 77: case 80:
			case 83: case 86:
			case 89:
				.@count = 6;
				break;
			default:
				.@count = 5;
				break;
		}
		getmapxy .@map$,.@x,.@y,BL_PC;
		.@npc_name$ = instance_npcname( strnpcinfo(0));
		.@event$ = .@npc_name$ + "::OnMyMobDead";
		playBGM "125";
		specialeffect EF_VENOMDUST;
		donpcevent instance_npcname("#alert1") + "::Onstart";
		disablenpc .@npc_name$;
		killmonster 'xm_d_map$, .@event$;
		areamonster 'xm_d_map$,(.@x-10),(.@y-10),(.@x+10),(.@y+10), "玩具工廠警報兵",2990,.@count, .@event$;// XM_CRUISER
		initnpctimer;
	}
	end;

OnTimer45000:
	enablenpc instance_npcname( strnpcinfo(0) );
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) +"::OnMyMobDead";
	stopnpctimer;
	end;

OnMyMobDead:
	end;
}
1@xm_d,30,24,0	duplicate(alert#61)	alert#62	-1,10,10
1@xm_d,50,24,0	duplicate(alert#61)	alert#63	-1,10,10
1@xm_d,70,24,0	duplicate(alert#61)	alert#64	-1,10,10
1@xm_d,90,24,0	duplicate(alert#61)	alert#65	-1,10,10
1@xm_d,10,44,0	duplicate(alert#61)	alert#66	-1,10,10
1@xm_d,30,44,0	duplicate(alert#61)	alert#67	-1,10,10
1@xm_d,50,44,0	duplicate(alert#61)	alert#68	-1,10,10
1@xm_d,70,44,0	duplicate(alert#61)	alert#69	-1,10,10
1@xm_d,90,44,0	duplicate(alert#61)	alert#70	-1,10,10
1@xm_d,110,44,0	duplicate(alert#61)	alert#71	-1,10,10
1@xm_d,10,64,0	duplicate(alert#61)	alert#72	-1,10,10
1@xm_d,30,64,0	duplicate(alert#61)	alert#73	-1,10,10
1@xm_d,50,64,0	duplicate(alert#61)	alert#74	-1,10,10
1@xm_d,70,64,0	duplicate(alert#61)	alert#75	-1,10,10
1@xm_d,90,64,0	duplicate(alert#61)	alert#76	-1,10,10
1@xm_d,110,64,0	duplicate(alert#61)	alert#77	-1,10,10
1@xm_d,10,84,0	duplicate(alert#61)	alert#78	-1,10,10
1@xm_d,30,84,0	duplicate(alert#61)	alert#79	-1,10,10
1@xm_d,50,84,0	duplicate(alert#61)	alert#80	-1,10,10
1@xm_d,70,84,0	duplicate(alert#61)	alert#81	-1,10,10
1@xm_d,90,84,0	duplicate(alert#61)	alert#82	-1,10,10
1@xm_d,110,84,0	duplicate(alert#61)	alert#83	-1,10,10
1@xm_d,10,104,0	duplicate(alert#61)	alert#84	-1,10,10
1@xm_d,30,104,0	duplicate(alert#61)	alert#85	-1,10,10
1@xm_d,50,104,0	duplicate(alert#61)	alert#86	-1,10,10
1@xm_d,70,104,0	duplicate(alert#61)	alert#87	-1,10,10
1@xm_d,90,104,0	duplicate(alert#61)	alert#88	-1,10,10
1@xm_d,110,104,0	duplicate(alert#61)	alert#89	-1,10,10
1@xm_d,155,20,0	duplicate(alert#61)	alert#90	-1,10,10
1@xm_d,180,50,0	duplicate(alert#61)	alert#91	-1,10,10
1@xm_d,205,80,0	duplicate(alert#61)	alert#92	-1,10,10
1@xm_d,230,110,0	duplicate(alert#61)	alert#93	-1,10,10
1@xm_d,180,20,0	duplicate(alert#61)	alert#94	-1,10,10
1@xm_d,180,50,0	duplicate(alert#61)	alert#95	-1,10,10
1@xm_d,180,80,0	duplicate(alert#61)	alert#96	-1,10,10
1@xm_d,205,20,0	duplicate(alert#61)	alert#97	-1,10,10
1@xm_d,205,50,0	duplicate(alert#61)	alert#98	-1,10,10
1@xm_d,205,80,0	duplicate(alert#61)	alert#99	-1,10,10
1@xm_d,205,110,0	duplicate(alert#61)	alert#100	-1,10,10
1@xm_d,230,20,0	duplicate(alert#61)	alert#101	-1,10,10
1@xm_d,230,50,0	duplicate(alert#61)	alert#102	-1,10,10
1@xm_d,230,80,0	duplicate(alert#61)	alert#103	-1,10,10
1@xm_d,230,110,0	duplicate(alert#61)	alert#104	-1,10,10


1@xm_d,185,100,6	script	凱瑟琳．澤塔．瓊斯#2	10032,{
	if (getpartyleader(getcharid(1),2) == getcharid(0)) {
		mes "[凱瑟琳．澤塔．瓊斯]";
		mes "還好安全通過了，我要先整理一下思緒。";
		next;
		switch( select( "退出對話", "聽聽她的策略", "我知道要做什麼。快點進行!" ) ) {
		case 1:
			mes "[凱瑟琳．澤塔．瓊斯]";
			mes "當你準備好時，請告訴我.";
			close;
		case 2:
			donpcevent instance_npcname("凱瑟琳．澤塔．瓊斯#21") + "::OnStart";
			close;
		case 3:
			donpcevent instance_npcname("凱瑟琳．澤塔．瓊斯#21") + "::OnStart2";
			close;
		}
	}
	mes "[凱瑟琳．澤塔．瓊斯]";
	mes "等等~我正和你的隊長談話，所以請稍等";
	close;
}

1@xm_d,185,100,6	script	凱瑟琳．澤塔．瓊斯#21	10032,{
	end;
OnStart:
	callsub S_Skip,0;
OnStart2:
	callsub S_Skip,1;
S_Skip:
	enablenpc instance_npcname("#bgm06");
	enablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#21");
	disablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#2");
	sleep 3000;
	if (getarg(0) == 1)
		npctalk "凱瑟琳．澤塔．瓊斯: 請你到處找一下線索，我去一下與玩具匠人最後留下回憶的地方。";
	else {
		npctalk "凱瑟琳．澤塔．瓊斯: 這個地方是2號工廠。";
		sleep 5000;
		npctalk "凱瑟琳．澤塔．瓊斯: 過去常常被人擠滿，但現在卻不是了。";
		sleep 5000;
		npctalk "凱瑟琳．澤塔．瓊斯: 啊，我來到這裡時感覺到了什麼。有些......兒童......";
		sleep 5000;
		npctalk "凱瑟琳．澤塔．瓊斯: 我怎麼能說......有很多靈魂看起來非常可怕，但非常傷心。";
		sleep 5000;
		npctalk "凱瑟琳．澤塔．瓊斯: 他們多麼可憐，但如果他們攻擊你，請讓他們休息。還好他們不會來找我。";
		sleep 5000;
		npctalk "凱瑟琳．澤塔．瓊斯: 如果你在這裡搜索時看到一個作業員，嘗試向他問起這個玩具匠人。";
		sleep 5000;
		npctalk "凱瑟琳．澤塔．瓊斯: 如果他們回想起玩具匠人的記憶，就能得到救贖。這是他們唯一的希望。";
		sleep 5000;
		npctalk "凱瑟琳．澤塔．瓊斯: 請你到處找一下線索，我去一下與玩具匠人最後留下回憶的地方。";
		sleep 5000;
		npctalk "凱瑟琳．澤塔．瓊斯: 很抱歉，我不能幫到你。";
	}
	sleep 6000;
	disablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#21");
	enablenpc instance_npcname("制服箱子#4");
	donpcevent instance_npcname("#fac2ct") + "::OnStart";

	for ( .@i = 1; .@i <= 10; .@i++ )
		enablenpc instance_npcname( "作業員#"+ .@i );
	disablenpc instance_npcname("#bgm06");
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,185,100,0	script	#bgm06	-1,9,9,{
OnTouch:
	playBGM "99";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,185,94,6	script	制服箱子#4	4_NONMYSTCASE,{
	progressbar "ffff00",1;
	playBGM "128";
	.@mob_id_transform = getstatus(SC_MONSTER_TRANSFORM,1);
	if (.@mob_id_transform == 1246 || .@mob_id_transform == 1249 || .@mob_id_transform < 1) {
		mes "^0000ff已換上工作服，工作執行中可隨時來此再次變身。^000000";
		transform 1246,300000;// COOKIE_XMAS
		close;
	}
	mes "^ff0000你已換上工作服。^000000";
	close;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#fac2ct	CLEAR_NPC,{
	end;
OnEnd:
	killmonster 'xm_d_map$, instance_npcname("#fac2ct") + "::OnMyMobDead";
	end;

OnStart:
	.@event$ = instance_npcname("#fac2ct") + "::OnMyMobDead";                   
	killmonster 'xm_d_map$, .@event$;                                           
	areamonster 'xm_d_map$,140,18,240,120, "未領到禮物的幽靈",2993,19, .@event$;
	areamonster 'xm_d_map$,140,18,240,120, "包裝好的箱子",2991,16, .@event$;	
	areamonster 'xm_d_map$,140,18,240,120, "聖誕泰迪貝爾",2995,22, .@event$;	
	areamonster 'xm_d_map$,140,18,240,120, "聖誕南瓜魂",2992,16, .@event$;		
	end;                                                                        
OnMyMobDead:
	end;
	
OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,2,3	script	#fac2wpc	CLEAR_NPC,{
	end;
OnStart:
	.@fac_open = 'worker[1] + 'worker[2] + 'worker[3] + 'worker[4] + 'worker[5] + 'worker[6] + 'worker[7] + 'worker[8] + 'worker[9] + 'worker[10];
	if (.@fac_open == 10) {
		enablenpc instance_npcname("#fac3wp");
		enablenpc instance_npcname("#fac3wp2");
		donpcevent instance_npcname("#fac2ct") + "::OnEnd";
		mapannounce 'xm_d_map$, "工廠宣布: 每個人都回家了。 該生產線關閉，員工休息室開放。", bc_map, "0x00ff44";
		for ( .@i = 90; .@i <= 104; .@i++ )
			disablenpc instance_npcname( "alert#"+ .@i );
	}
	else
		mapannounce 'xm_d_map$, "工廠宣布: 值勤人員確認中，目前包裝線上有 " + (10 - .@fac_open) + " 名作業員，辛苦了!", bc_map, "0x00ff44";
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,155,98,3	script	作業員#1	4_M_COOKIE,{
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 1246) {
		.@num = atoi(strnpcinfo(2));
		mes "[作業員]";
		mes "嗯? 有什麼事嗎? ";
		next;
		if (select( "沒事啦", "你知道玩具匠人的消息嗎?" ) == 1) {
			mes "[作業員]";
			if (.@num == 8)
				mes "我現在正在工作!我很忙!";
			else
				mes "我的工作很忙，所以沒事可以不要打擾我嗎? ";
			close;
		}
		close2;
		pcblockmove getcharid(3),1;// todo : not able to talk to npc
		pcblockskill getcharid(3),1;
		switch(.@num) {
		case 1:
			npctalk "作業員: 能短暫看到活著爺爺是賦予基米生命的第一件事。";
			sleep2 3000;
			npctalk "作業員: 但那也是最後一次見到爺爺，非常痛心...";
			break;
		case 2:
			npctalk "作業員: 基米沒殺害爺爺! 基米是想救爺爺! 人們都怕基米! 基米很乖! 基米是好孩子!";
			sleep2 3000;
			npctalk "作業員: 爺爺，好想您...爺爺...爺爺...";
			break;
		case 3:
			npctalk "作業員: 本來爺爺的心臟就不好，過世的那天好像突然發作，是基米發現後試圖要救活...";
			sleep2 3000;
			npctalk "作業員: 啊...不過你我做了什麼? 感覺心情很奇妙...";
			break;
		case 4:
			npctalk "作業員: 當爺爺過世後基米就像玩具般呆坐了好一陣子。";
			sleep2 3000;
			npctalk "作業員: 似乎連自己的靈魂都放棄了一樣，就像我...我...怎麼會這樣?";
			break;
		case 5:
			npctalk "作業員: 基米，可憐的小孩，誤以為爺爺過世是因為看到自己恐怖的臉孔所導致。";
			sleep2 3000;
			npctalk "作業員: 爺爺看到基米能動多麼開心...";
			break;
		case 6:
			npctalk "作業員: 啊! 那位爺爺嗎? 他人很好，每天都會幫我們保養呢!";
			sleep2 3000;
			npctalk "作業員: 希望他還活著就好了。";
			break;
		case 7:
			npctalk "作業員: 據說爺爺賦予基米生命之前，基米就已經感受到爺爺的聲音..";
			sleep2 3000;
			npctalk "作業員: 也許是爺爺的愛讓基米動的吧...突然有點睏...";
			break;
		case 8:
			npctalk "作業員: 爺爺是好人!基米很可憐!基米愛爺爺!爺爺死了我很傷心!";
			sleep2 3000;
			break;
		case 9:
			npctalk "作業員: 真相是看到睜眼的基米後過於激動而發生了不幸。";
			sleep2 3000;     
			npctalk "作業員: 不過我的身體好像飄在空中一樣，很奇怪。";
			break;
		case 10:
			npctalk "作業員: 你說匠人爺爺是被基米殺害的? 是誰說的? 那是誤傳，基米是想救爺爺!";
			sleep2 3000;
			npctalk "作業員: 不...過我的身體怎麼會...";
			break;
		}
		sleep2 3000;
		disablenpc instance_npcname( strnpcinfo(0) );
		if ('worker[.@num] == 0) {
			'worker[.@num] = 1;
			donpcevent instance_npcname("#fac2wpc") + "::OnStart";
		}
		pcblockmove getcharid(3),0;
		pcblockskill getcharid(3),0;
		end;
	}
	mes "[作業員]";
	mes "什麼!?  你不是我們中的一員!!";
	donpcevent instance_npcname( strnpcinfo(0) ) + "::OnAlert";
	close;

OnAlert:
	.@npc_name$ = instance_npcname( strnpcinfo(0) );
	killmonster 'xm_d_map$, .@npc_name$ + "::OnMyMobDead";
	.@num = atoi(strnpcinfo(2));
	if (.@num == 8)
		npctalk "作業員: 警衛!警衛!!";
	else
		npctalk "作業員: 警衛，警衛!你在哪裡? !人類在這裡!!";
	sleep 3000;
	switch(.@num) {
	case 9:
		setarray .@coord[0],233,27;
		break;
	case 10:
		setarray .@coord[0],209,27;
		break;
	default:
		getmapxy .@map$, .@coord[0], .@coord[1], BL_NPC;
		break;
	}
	areamonster 'xm_d_map$,(.@coord[0]-8),(.@coord[1]-8),(.@coord[0]+8),(.@coord[1]+8), "玩具工廠警報兵",2990,21, .@npc_name$ + "::OnMyMobDead";
	disablenpc .@npc_name$;
	if ('worker[.@num] == 0) {
		'worker[.@num] = 1;
		donpcevent instance_npcname("#fac2wpc") + "::OnStart";
	}
	initnpctimer;
	end;
OnTimer60000:
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	stopnpctimer;
	end;

OnMyMobDead:
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@xm_d,130,72,3	duplicate(作業員#1)	作業員#2	4_M_COOKIE
1@xm_d,134,34,1	duplicate(作業員#1)	作業員#3	4_M_COOKIE
1@xm_d,195,28,3	duplicate(作業員#1)	作業員#4	4_M_COOKIE
1@xm_d,228,30,1	duplicate(作業員#1)	作業員#5	4_M_COOKIE
1@xm_d,203,55,3	duplicate(作業員#1)	作業員#6	4_M_COOKIE
1@xm_d,132,52,1	duplicate(作業員#1)	作業員#7	4_M_COOKIE
1@xm_d,162,52,1	duplicate(作業員#1)	作業員#8	4_M_COOKIE
1@xm_d,242,17,5	duplicate(作業員#1)	作業員#9	4_M_COOKIE
1@xm_d,209,15,3	duplicate(作業員#1)	作業員#10	4_M_COOKIE


// Note : aegis script have OnClick part
1@xm_d,131,208,0	script	被抓的聖誕老人#2	4_M_SANTA,10,10,{
	end;
OnTouch_:
	disablenpc instance_npcname("被抓的聖誕老人#2");
	enablenpc instance_npcname("被抓的聖誕老人#3");
	donpcevent instance_npcname("安東尼奧#1") + "::OnStart";
	enablenpc instance_npcname("#bgm04");
	end;
}

1@xm_d,131,208,8	script	被抓的聖誕老人#3	4_M_SANTA,{
	mes "[被抓的聖誕老人]";
	mes "即使這裡的禮物不屬於任何人，你也不能偷走她。";
	close;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,131,213,4	script	安東尼奧#1	4_M_ANTONIO,{
	mes "[安東尼奧]";
	mes "嘿，聖誕老人。別悶了，你已經老了。";
	close;

OnStart:
	.@antonio$ = instance_npcname("安東尼奧#1");
	.@santa$ = instance_npcname("被抓的聖誕老人#3");
	sleep 3000;
	npctalk "安東尼奧: 老頭子我跟你說~ 我喜歡這間工廠。", .@antonio$;
	sleep 3000;
	npctalk "安東尼奧: 這裡的老闆不再，又有一堆我喜歡的禮物。", .@antonio$;
	sleep 3000;
	npctalk "被抓的聖誕老人: 你這不懂事的傢伙! 我就知道你又偷了東西。", .@santa$;
	sleep 4000;
	npctalk "安東尼奧: 我只是分享沒有人要的禮物，有這麼嚴重嗎? 我不覺得耶~", .@antonio$;
	sleep 4000;
	npctalk "被抓的聖誕老人: 唉... 你想過當孩子們收到禮物時的心情嗎?", .@santa$;
	sleep 4000;
	npctalk "被抓的聖誕老人: 萬一他們知道收到的禮物是偷來的會開心嗎?", .@santa$;
	sleep 4000;
	npctalk "安東尼奧: 嗯...", .@antonio$;
	sleep 2000;
	npctalk "安東尼奧: 我會覺得開心!! 反正就是禮物嘛!", .@antonio$;
	sleep 5000;
	npctalk "被抓的聖誕老人: 重點不是禮物! 這是偷竊行為，給我住手安東尼奧!", .@santa$;
	sleep 5000;
	mapannounce 'xm_d_map$, "工廠宣布: 3號工廠的發送準備已經完成。", bc_map, "0x00ff44";
	sleep 5000;
	mapannounce 'xm_d_map$, "工廠宣布: 配送組職員請準備一下。", bc_map, "0x00ff44";
	sleep 3000;
	npctalk "安東尼奧: 啊!禮物因該堆積如山了吧! 今天的派對準備開始囉!!", .@antonio$;
	sleep 4000;
	npctalk "安東尼奧: 嘿，人類。如果你願意幫忙的話，你可以跟我來!", .@antonio$;
	sleep 4000;
	disablenpc .@antonio$;
	sleep 2000;
	npctalk "被抓的聖誕老人: 你這是非不分的傢伙...", .@santa$;
	sleep 4000;
	npctalk "被抓的聖誕老人: 請你把安東尼奧趕走好嗎?", .@santa$;
	sleep 5000;
	npctalk "被抓的聖誕老人: 偷竊行為就是不對!", .@santa$;

	disablenpc instance_npcname("#bgm04");
	enablenpc instance_npcname("#fac4wp");
	enablenpc instance_npcname("#fac4wp2");
	donpcevent instance_npcname("#fac3ct") + "::OnStart";
	donpcevent instance_npcname("#fac3ct2") + "::OnStart";
	donpcevent instance_npcname("#fac3ct3") + "::OnStart";
	sleep 3000;
	enablenpc instance_npcname("#bgm05");
	end;
}

1@xm_d,131,208,0	script	#bgm04	-1,9,9,{
OnTouch:
	playBGM "54";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,131,208,0	script	#bgm05	-1,9,9,{
OnTouch:
	playBGM "105";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#fac3ct	CLEAR_NPC,{
	end;
OnEnd:
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	end;
OnStart:
	.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	areamonster 'xm_d_map$,13,144,121,248, "尚未包裝的玩偶",2994,37, .@event$;	// XM_MARIONETTE
	areamonster 'xm_d_map$,13,144,121,248, "驚悚的裝飾樹",2987,31, .@event$;	// XM_TREE
	areamonster 'xm_d_map$,13,144,121,248, "聖誕泰迪貝爾",2995,43, .@event$;// XM_TEDDY_BEAR
	areamonster 'xm_d_map$,13,144,121,248, "聖誕南瓜魂",2992,31, .@event$;		// XM_LUDE
	end;
OnMyMobDead:
	end;
OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#fac3ct2	CLEAR_NPC,{
	end;
OnEnd:
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	end;
OnStart:
	.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	areamonster 'xm_d_map$,159,215,241,247, "未領到禮物的幽靈",2993,13, .@event$;	// XM_HYLOZOIST
	areamonster 'xm_d_map$,159,215,241,247, "驚悚的裝飾樹",2987,11, .@event$;	// XM_TREE
	areamonster 'xm_d_map$,159,215,241,247, "聖誕泰迪貝爾",2995,15, .@event$;	// XM_TEDDY_BEAR
	areamonster 'xm_d_map$,159,215,241,247, "聖誕南瓜魂",2992,11, .@event$;			// XM_LUDE
	end;
OnMyMobDead:
	end;
OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#fac3ct3	CLEAR_NPC,{
	end;
OnStart:
	if (rand(1,10) > 3)
		areamonster 'xm_d_map$,13,144,121,248 ,"安東尼奧",2988,1, instance_npcname("#fac3ct3")+"::OnMyMobDead";// ANTONIO
	else
		areamonster 'xm_d_map$,159,215,241,247, "安東尼奧",2988,1, instance_npcname("#fac3ct3")+"::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount( 'xm_d_map$, instance_npcname("#fac3ct3") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;

OnTimer1000:
OnEnd:
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	donpcevent instance_npcname("#fac3ct") + "::OnEnd";
	donpcevent instance_npcname("#fac3ct2") + "::OnEnd";
	donpcevent instance_npcname("#finalbs") + "::OnStart";
	disablenpc instance_npcname("被抓的聖誕老人#3");
	mapannounce 'xm_d_map$, "???: 只要靜靜地離開，就不會受到傷害。", bc_map, "0xff8800";
	stopnpctimer;
	end;

OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#finalbs	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#5");
	enablenpc instance_npcname("席琳基米#0");
	enablenpc instance_npcname("#fac5wp");
	// enablenpc instance_npcname("#fac5wp2");// never enabled
	enablenpc instance_npcname("#jeton1");
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

/*
// never enabled
1@xm_d,160,208,0	script	#fac5wp2	WARPNPC,2,2,{
OnTouch:
	warp 'xm_d_map$,145,208;
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
*/

1@xm_d,233,183,3	script	席琳基米#0	4_F_KIMI,{
	mes "[席琳基米]";
	mes "你也來過這裡嗎?  為什麼所有的人都急於摧毀我們建造的東西? ";
	close;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,222,183,0	script	#jeton1	-1,7,7,{
OnTouch_:
	disablenpc instance_npcname("#jeton1");
	donpcevent instance_npcname("凱瑟琳．澤塔．瓊斯#5") + "::OnStart";
	enablenpc instance_npcname("#bgm02");
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,222,183,0	script	#bgm02	-1,9,9,{
OnTouch:
	playBGM "101";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,222,183,6	script	凱瑟琳．澤塔．瓊斯#5	10032,{
	mes "[凱瑟琳．澤塔．瓊斯]";
	mes "哇..小心! 基米不是正常狀態.";
	close;

OnStart:
	.@kimi$ = instance_npcname("席琳基米#0");
	sleep 2000;
	npctalk "凱瑟琳．澤塔．瓊斯: 基米!你聽我說，我不是來責怪你的。";
	sleep 3000;
	npctalk "席琳基米: 大家都討厭我!...你也一樣吧? 恐懼的外貌加上玩偶....", .@kimi$;
	sleep 4000;
	npctalk "凱瑟琳．澤塔．瓊斯: 基米，我從娃娃那裡聽說了完整的故事。其實玩具匠人很愛你!";
	sleep 4000;
	mapannounce 'xm_d_map$, "幻影大喊：騙人!", bc_map, "0xff8800";
	sleep 1000;
	npctalk "席琳基米: 不要騙我。如果你說的是真的，那為什麼在他眼中看不見他對我的愛? ", .@kimi$;
	sleep 4000;
	npctalk "席琳基米: 為什麼他不願叫我的名字? 基米~ 基米! 我好想聽到爺爺的聲音...", .@kimi$;
	sleep 4000;
	mapannounce 'xm_d_map$, "幻影大喊：沒錯! 爺爺看了你的外貌非常驚嚇!!", bc_map, "0xff8800";
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 不，他說謊，基米! 他真的很珍惜你.";
	sleep 3000;
	npctalk "席琳基米: 他......珍惜我嗎?", .@kimi$;
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 是的，當你開始動起來時，他是如此開心...";
	sleep 1000;
	mapannounce 'xm_d_map$, "幻影的大喊：是被你嚇到猝死的，是你殺了他! ", bc_map, "0xff8800";
	sleep 3000;
	npctalk "席琳基米: ..我......殺了他? ", .@kimi$;
	sleep 2000;
	npctalk "凱瑟琳．澤塔．瓊斯: 不，基米!我知道。大家都誤解了你。爺爺他只是生病了!";
	sleep 3000;
	npctalk "席琳基米: .....我......殺了......他? ", .@kimi$;
	sleep 2000;
	mapannounce 'xm_d_map$, "幻影大喊：看你的樣子，基米~去看看鏡子! ", bc_map, "0xff8800";
	sleep 3000;
	npctalk "席琳基米: 我......我......", .@kimi$;
	sleep 1000;
	mapannounce 'xm_d_map$, "幻影大喊：你的樣子不恐怖嗎? 沒有人愛你基米~", bc_map, "0xff8800";
	sleep 3000;
	npctalk "席琳基米: 因為我，爺爺已經死了...", .@kimi$;
	specialeffect EF_MAPPILLAR2, AREA, .@kimi$;
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 他變得憤怒了。基米看起來非常痛苦。在這樣下去情況將會失控...";
	specialeffect EF_MAPPILLAR2, AREA, .@kimi$;
	sleep 1000;
	mapannounce 'xm_d_map$, "幻影大喊：憤怒吧~崩潰吧~ 沒有人會為你悲傷的基米~", bc_map, "0xff8800";
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 我會找到逃生的緊急出口。逃跑，冒險家!";
	sleep 3000;
	disablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#5");
	sleep 2000;
	disablenpc .@kimi$;
	donpcevent instance_npcname("#finalbs2") + "::OnStart";
	disablenpc instance_npcname("#bgm02");
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#finalbs2	CLEAR_NPC,{
	end;
OnStart:
	stopnpctimer;
	enablenpc instance_npcname("#bgm03");
	.@event$ = instance_npcname("#finalbs2") + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	monster 'xm_d_map$,231,184, "席琳．基米",2996,1, .@event$;// XM_CELINE_KIMI
	'celene_id = $@mobid[0];
	monster 'xm_d_map$,226,190, "席琳．基米的幻影",2997,1, .@event$;// G_XM_CELINE_KIMI
	'phantom_id = $@mobid[0];
	setunitdata 'celene_id, UMOB_HP,20000000;
	setunitdata 'phantom_id,UMOB_HP,20000000;

	unittalk 'celene_id, "我不想被拋棄...我不想被拋棄...";
	initnpctimer;
	end;

OnMyMobDead:
	if (mobcount( 'xm_d_map$, instance_npcname("#finalbs2") + "::OnMyMobDead" ) < 1)
		donpcevent instance_npcname("#finalbs2") + "::OnEnd";
	end;

OnEnd:
	stopnpctimer;
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	disablenpc instance_npcname("#bgm03");
	donpcevent instance_npcname("#finalbs_e") + "::OnStart";
	end;

OnTalk:
	.@chat_r = rand(1,10);
	if (.@chat_r == 1)
		unittalk 'celene_id, "我會用地獄般的火焰將你燃燒殆盡.";
	else if (.@chat_r == 2)
		unittalk 'celene_id, "你能承受這種火焰嗎?!";
	else if (.@chat_r == 3)
		unittalk 'celene_id, "我不應該這樣做，但.....";
	else if (.@chat_r == 4)
		unittalk 'celene_id, "燃燒吧!如果可以....";
	else if (.@chat_r == 5)
		unittalk 'celene_id, "盡可能的呼吸吧。這也許將是你最後一口氣.";
	else if (.@chat_r == 6)
		unittalk 'celene_id, "坦率地說，我不喜歡火.";
	else
		unittalk 'celene_id, "每個人都害怕我!我做錯了什麼?!";
	end;

OnTimer1000:
	getunitdata 'celene_id, .@data;
	if ((.@data[UMOB_X] < 211 || .@data[UMOB_X] > 241 || .@data[UMOB_Y] < 166 || .@data[UMOB_Y] > 201) && (.@data[UMOB_X] > 0 || .@data[UMOB_Y] > 0)) {
		mapannounce 'xm_d_map$, "席琳基米: 沒有! 我應該保住我的位置!",bc_map,"0xff6666",FW_NORMAL,15;
		donpcevent instance_npcname("#finalbs2") + "::Onfail";
	}
	end;

Onfail:
	stopnpctimer;
	killmonster 'xm_d_map$, instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	enablenpc instance_npcname("席琳基米#2");
	disablenpc instance_npcname("#bgm03");
	enablenpc instance_npcname("#kimion1");
	end;

OnTimer5000:
	donpcevent instance_npcname("#bssk01") + "::OnStart";
	end;

OnTimer10000:
	if (mobcount( 'xm_d_map$, instance_npcname("#finalbs2") + "::OnMyMobDead" ) > 1) {
		.@npc_name$ = instance_npcname("#finalbs2");
		getunitdata 'celene_id, .@MOB_HP1;
		getunitdata 'phantom_id, .@MOB_HP2;
		if (.@MOB_HP1[UMOB_HP] > .@MOB_HP2[UMOB_HP]) {
			setarray .@mob_hp[0], .@MOB_HP1[UMOB_HP], .@MOB_HP2[UMOB_HP];
			setarray .@string$[0],
				"你與我本就是一體的!我會幫助你!",
				"席琳基米恢復了自己與幻影的生命。";
			.@talk = 'celene_id;
		}
		else if (.@MOB_HP2[UMOB_HP] > .@MOB_HP1[UMOB_HP]) {
			setarray .@mob_hp[0], .@MOB_HP2[UMOB_HP], .@MOB_HP1[UMOB_HP];
			setarray .@string$[0],
				"我會恢復你的!!",
				"席琳基米的幻影恢復了自己與主人的生命。";
			.@talk = 'phantom_id;
		}
		.@diff_hp = .@mob_hp[0] - .@mob_hp[1];
		if (.@diff_hp > 100000) {
			.@set_bs_hp = (.@diff_hp * 5) / 10;
			.@MOB_HP3 = .@mob_hp[0] + .@set_bs_hp;
			if (.@MOB_HP3 > 66666666)
				.@MOB_HP3 = 66666666;
			setunitdata 'celene_id, UMOB_HP, .@MOB_HP3;
			setunitdata 'phantom_id, UMOB_HP, .@MOB_HP3;
			donpcevent instance_npcname("#eff_f01") + "::OnStart";
			unittalk .@talk, .@string$[0];
			sleep 1000;
			mapannounce 'xm_d_map$,  .@string$[1] + .@set_bs_hp +" HP已經恢復.", bc_map, "0xff6666";
			donpcevent instance_npcname("#heal_c") + "::OnStart";
		}
		initnpctimer;
	}
	end;

OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,216,193,3	script	#eff_f01	CLEAR_NPC,{
	end;
OnStart:
	for ( .@i = 1; .@i < 10; .@i++ )
		specialeffect EF_HEARTCASTING, AREA, instance_npcname( "#eff_f0"+ .@i );
	end;
OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,226,193,3	script	#eff_f02	CLEAR_NPC,{
	end;
OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}
1@xm_d,236,193,3	duplicate(#eff_f02)	#eff_f03	CLEAR_NPC
1@xm_d,216,183,3	duplicate(#eff_f02)	#eff_f04	CLEAR_NPC
1@xm_d,226,183,3	duplicate(#eff_f02)	#eff_f05	CLEAR_NPC
1@xm_d,236,183,3	duplicate(#eff_f02)	#eff_f06	CLEAR_NPC
1@xm_d,216,173,3	duplicate(#eff_f02)	#eff_f07	CLEAR_NPC
1@xm_d,226,173,3	duplicate(#eff_f02)	#eff_f08	CLEAR_NPC
1@xm_d,236,173,3	duplicate(#eff_f02)	#eff_f09	CLEAR_NPC

1@xm_d,1,5,3	script	#bssk01	CLEAR_NPC,{
	end;
OnStart:
	.@r = rand(1,3);
	if (.@r == 1)
		donpcevent instance_npcname("#bssk02") + "::OnStart";
	else if (.@r == 2)
		donpcevent instance_npcname("#bssk03") + "::OnStart";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#bssk02	CLEAR_NPC,{
	end;
OnStart:
	donpcevent instance_npcname("#finalbs2") + "::OnTalk";
	for ( .@i = 1; .@i < 5; .@i++ )
		donpcevent instance_npcname( "#crssk"+ .@i ) + "::OnStart";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#bssk03	CLEAR_NPC,{
	end;
OnStart:
	.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	while (1) {
		getunitdata 'celene_id, .@data;
		.@x = .@data[UMOB_X] + rand(1,20) - 10;
		.@y = .@data[UMOB_Y] + rand(1,20) - 10;
		monster 'xm_d_map$,.@x,.@y, "#f_w_1",3038,1, .@event$;// HIDDEN_MOB7
		.@mon_num++;
		if (.@mon_num > 20)
			break;
		sleep 200;
	}
	sleep 6000;
	killmonster 'xm_d_map$, .@event$;
	end;

OnMyMobDead:
	end;

OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#crssk1	CLEAR_NPC,{
	end;
OnStart:
	.@event$ = instance_npcname( strnpcinfo(0) ) + "::OnMyMobDead";
	killmonster 'xm_d_map$, .@event$;
	getunitdata 'celene_id, .@data;
	setarray .@coord[0], .@data[UMOB_X], .@data[UMOB_Y];
	.@num = atoi(strnpcinfo(2));
	.@index = ( .@num > 2 ? 1 : 0 );// x or y
	.@signe = pow(-1,.@num+1);
	while(1) {
		.@coord[.@index] = .@coord[.@index] + (2 * .@signe);
		.@coord[!.@index] = .@coord[!.@index] + rand(0,2) - 1;
		monster 'xm_d_map$,.@coord[0], .@coord[1], "#f_w_1",3038,1, .@event$;
		if (.@coord[0] < 211 || .@coord[0] > 241 || .@coord[1] < 166 || .@coord[1] > 201)
			break;
		sleep 200;
	}
	sleep 6000;
	killmonster 'xm_d_map$, .@event$;
	end;
OnMyMobDead:
	end;

OnInstanceInit:
	hideonnpc instance_npcname( strnpcinfo(0) );
	end;
}
1@xm_d,1,5,3	duplicate(#crssk1)	#crssk2	CLEAR_NPC
1@xm_d,1,5,3	duplicate(#crssk1)	#crssk3	CLEAR_NPC
1@xm_d,1,5,3	duplicate(#crssk1)	#crssk4	CLEAR_NPC

1@xm_d,233,183,0	script	#kimion1	-1,7,7,{
OnTouch_:
	disablenpc instance_npcname("#kimion1");
	donpcevent instance_npcname("席琳基米#2") + "::OnStart";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,233,183,3	script	席琳基米#2	4_F_KIMI,{
	mes "[席琳基米]";
	mes "你也來......來殺我?";
	close;

OnStart:
	npctalk "席琳基米: 你也來......來殺我?";
	specialeffect EF_MAPPILLAR2;
	sleep 5000;
	disablenpc instance_npcname("席琳基米#2");
	donpcevent instance_npcname("#finalbs2") + "::OnStart";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,228,183,0	script	#bgm03	-1,25,25,{
OnTouch:
	playBGM "123";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#heal_c	CLEAR_NPC,{
	end;
OnStart:
	if (rand(1,10) > 4)
		initnpctimer;
	end;
OnTimer3000:
	mapannounce 'xm_d_map$, "席琳基米和她的幻影分享他們的力量。 他們將比以往更強大!",bc_map,"0xff6666";
	stopnpctimer;
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,1,5,3	script	#finalbs_e	CLEAR_NPC,{
	end;
OnStart:
	mapannounce 'xm_d_map$, "席琳基米:打敗我是沒有用的! 因為我會一直重生!!",bc_map,"0xff6666",FW_NORMAL,15;
	enablenpc instance_npcname("#fac6wp");
	enablenpc instance_npcname("#jeton2");
	enablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#6");
	for ( .@i = 1; .@i <= 10; .@i++ )
		enablenpc instance_npcname("包裝好的禮物#"+ .@i);
	sleep 6000;
	mapannounce 'xm_d_map$, "凱瑟琳．澤塔．瓊斯大喊：請你往南邊的逃生口避難!",bc_map,"0xffff00";
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,218,145,0	script	#jeton2	-1,4,4,{
OnTouch_:
	disablenpc instance_npcname("#jeton2");
	donpcevent instance_npcname("凱瑟琳．澤塔．瓊斯#6") + "::OnStart";
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,218,145,5	script	凱瑟琳．澤塔．瓊斯#6	10032,{
	end;
OnStart:
	sleep 1000;
	npctalk "凱瑟琳．澤塔．瓊斯: 很可惜! 說服基米失敗了!";
	sleep 3000;
	npctalk "凱瑟琳．澤塔．瓊斯: 幻影的聲音是什麼? 為什麼它如此嚴厲地折磨基米...";
	sleep 4000;
	npctalk "凱瑟琳．澤塔．瓊斯: 我猜我臉上的詛咒形成了不明的聲音!";
	sleep 4000;
	npctalk "凱瑟琳．澤塔．瓊斯: 不過懷恨大家的基米也對這裡充滿回憶呢!";
	sleep 6000;
	npctalk "凱瑟琳．澤塔．瓊斯: 為了不讓基米的靈魂回來後感到失望，最好保管好東西!!";
	sleep 5000;
	npctalk "凱瑟琳．澤塔．瓊斯: 幸好真相得以解開，如果下次有機會讓基米得到救贖，記得告訴我一下。";
	sleep 5000;
	npctalk "凱瑟琳．澤塔．瓊斯: 我會打開出口。請你跟我一起出去吧!";
	sleep 3000;
	disablenpc instance_npcname("凱瑟琳．澤塔．瓊斯#6");
	enablenpc instance_npcname("時空裂縫#exwp1");
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,218,150,5	script	時空裂縫#exwp1	PORTAL,{
	mes "你要退出嗎? ?";
	next;
	if (select( "還沒", "離開" ) == 1) {
		mes "停止機器.";
		close;
	}
	close2;
	warp "egg1",214,167;
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}

1@xm_d,210,141,3	script	包裝好的禮物#1	4_TREASURE_BOX,{
	specialeffect EF_COIN;
	disablenpc instance_npcname( strnpcinfo(0) );
	initnpctimer;
	end;

OnTimer200:
	switch( atoi(strnpcinfo(2)) ) {
	case 1:
		.@num = rand(4,8);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(208,212), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 150)
			makeitem 644,1, 'xm_d_map$,209,141;// Gift_Box;
		if (rand(1,1000) > 600)
			makeitem 617,1, 'xm_d_map$,210,141;// Old_Violet_Box;
		if (rand(1,1000) > 900)
			makeitem 22534,1, 'xm_d_map$,211,141;// Closedmind_Box
		break;
	case 2:
		.@num = rand(3,7);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(212,216), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 400)
			makeitem 603,1, 'xm_d_map$,213,141;// Old_Blue_Box
		if (rand(1,1000) > 700)
			makeitem 616,1, 'xm_d_map$,214,141;// Old_Card_Album
		if (rand(1,1000) > 950)
			makeitem 13442,1, 'xm_d_map$,215,141;// Old_Parasol
		break;
	case 3:
		.@num = rand(2,6);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(216,220), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 850)
			makeitem 7229,1, 'xm_d_map$,217,141;// Silver_Bullion
		if (rand(1,1000) > 800)
			makeitem 12246,1, 'xm_d_map$,218,141;// Magic_Card_Album
		if (rand(1,1000) > 950)
			makeitem 2486,1, 'xm_d_map$,219,141;// Shadow_Walk_
		break;
	case 4:
		.@num = rand(4,8);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(220,224), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 700)
			makeitem 7228,1, 'xm_d_map$,221,141;// Gold_Bullion
		if (rand(1,1000) > 600)
			makeitem 617,1, 'xm_d_map$,222,141;// Old_Violet_Box
		if (rand(1,1000) > 900)
			makeitem 22534,1, 'xm_d_map$,223,141;// Closedmind_Box
		break;
	case 5:
		.@num = rand(3,7);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(224,228), rand(139,143);// Bloody_Coin
		if (rand(1,1000) > 150)
			makeitem 644,1, 'xm_d_map$,225,141;// Gift_Box
		if (rand(1,1000) > 700)
			makeitem 616,1, 'xm_d_map$,226,141;// Old_Card_Album
		if (rand(1,1000) > 950)
			makeitem 2976,1, 'xm_d_map$,227,141;// Red_Lantern
		break;
	case 6:
		.@num = rand(2,6);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 2976,1, 'xm_d_map$, rand(208,212), rand(134,138);// Red_Lantern
		if (rand(1,1000) > 400)
			makeitem 603,1, 'xm_d_map$,209,136;// Old_Blue_Box
		if (rand(1,1000) > 800)
			makeitem 12246,1, 'xm_d_map$,210,136;// Magic_Card_Album
		if (rand(1,1000) > 950)
			makeitem 2977,1, 'xm_d_map$,211,136;// Hurt_Mind
		break;
	case 7:
		.@num = rand(4,8);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(212,216), rand(134,138);// Bloody_Coin
		if (rand(1,1000) > 850)
			makeitem 7229,1, 'xm_d_map$,213,136;// Silver_Bullion
		if (rand(1,1000) > 600)
			makeitem 617,1, 'xm_d_map$,214,136;// Old_Violet_Box
		if (rand(1,1000) > 900)
			makeitem 22534,1, 'xm_d_map$,215,136;// Closedmind_Box
		break;
	case 8:
		.@num = rand(3,7);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(216,220), rand(134,138);// Bloody_Coin
		if (rand(1,1000) > 700)
			makeitem 7228,1, 'xm_d_map$,217,136;// Gold_Bullion
		if (rand(1,1000) > 700)
			makeitem 616,1, 'xm_d_map$,218,136;// Old_Card_Album
		if (rand(1,1000) > 950)
			makeitem 2978,1, 'xm_d_map$,219,136;// KindHeart
		break;
	case 9:
		.@num = rand(2,6);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(220,224), rand(134,138);// Bloody_Coin
		if (rand(1,1000) > 150)
			makeitem 644,1, 'xm_d_map$,221,136;// Gift_Box
		if (rand(1,1000) > 800)
			makeitem 12246,1, 'xm_d_map$,222,136;// Magic_Card_Album
		if (rand(1,1000) > 950)
			makeitem 18848,1, 'xm_d_map$,223,136;// Lush_Rose
		break;
	case 10:
		.@num = rand(4,8);
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 7642,1, 'xm_d_map$, rand(224,228), rand(134,138);// Bloody_Coin
		if (rand(1,1000) > 400)
			makeitem 603,1, 'xm_d_map$,225,136;// Old_Blue_Box
		if (rand(1,1000) > 600)
			makeitem 617,1, 'xm_d_map$,226,136;// Old_Violet_Box
		if (rand(1,1000) > 900)
			makeitem 22534,1, 'xm_d_map$,227,136;// Closedmind_Box
		break;
	}
	stopnpctimer;
	end;

OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@xm_d,214,141,3	duplicate(包裝好的禮物#1)	包裝好的禮物#2	4_TREASURE_BOX
1@xm_d,218,141,3	duplicate(包裝好的禮物#1)	包裝好的禮物#3	4_TREASURE_BOX
1@xm_d,222,141,3	duplicate(包裝好的禮物#1)	包裝好的禮物#4	4_TREASURE_BOX
1@xm_d,226,141,3	duplicate(包裝好的禮物#1)	包裝好的禮物#5	4_TREASURE_BOX
1@xm_d,210,136,3	duplicate(包裝好的禮物#1)	包裝好的禮物#6	4_TREASURE_BOX
1@xm_d,214,136,3	duplicate(包裝好的禮物#1)	包裝好的禮物#7	4_TREASURE_BOX
1@xm_d,218,136,3	duplicate(包裝好的禮物#1)	包裝好的禮物#8	4_TREASURE_BOX
1@xm_d,222,136,3	duplicate(包裝好的禮物#1)	包裝好的禮物#9	4_TREASURE_BOX
1@xm_d,226,136,3	duplicate(包裝好的禮物#1)	包裝好的禮物#10	4_TREASURE_BOX

// Warps
//==========================================
1@xm_d,79,129,0	warp2	#fac1wp2	2,2,1@xm_d,73,129
1@xm_d,184,109,0	warp2	#fac2wp2	2,2,1@xm_d,170,129
1@xm_d,130,178,0	warp2	#fac3wp	2,2,1@xm_d,130,193
1@xm_d,130,184,0	warp2	#fac3wp2	2,2,1@xm_d,129,173
1@xm_d,107,208,0	warp2	#fac4wp	2,2,1@xm_d,87,208
1@xm_d,95,208,0	warp2	#fac4wp2	2,2,1@xm_d,115,208
1@xm_d,152,208,0	warp2	#fac5wp	2,2,1@xm_d,167,208
1@xm_d,205,159,0	warp2	#fac6wp	2,2,1@xm_d,205,147

// Instance GM Functions :: mx_xm_ad
//==========================================
1@xm_d,1,1,3	script	#adsw	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		mes "[時間經理]";
		mes "你什麼時候回來?";
		next;
		switch (select("Cancel:Factory No.1 Complete:Factory No.2 Complete:Factory No.3 Complete:Boss Start:Boss Complete")) {
		case 1:
			break;
		case 2:
			transform 1246,180000;
			enablenpc instance_npcname("作業主任#fac1bs");
			mapannounce 'xm_d_map$, "作業主任: 咦? 人都在哪裡!!",bc_map,"0xff8800";
			warp 'xm_d_map$,70,125;
			break;
		case 3:
			for ( .@i = 1; .@i <= 10; ++.@i )
				disablenpc instance_npcname( "作業員#" + .@i );
			donpcevent instance_npcname("#fac2wpc") + "::OnStart";
			warp 'xm_d_map$,131,210;
			break;
		case 4:
			donpcevent instance_npcname("#fac3ct3") + "::OnEnd";
			warp 'xm_d_map$,131,210;
			break;
		case 5:
			donpcevent instance_npcname("#finalbs2") + "::OnStart";
			warp 'xm_d_map$,215,182;
			break;
		case 6:
			donpcevent instance_npcname("#finalbs2") + "::OnEnd";
			warp 'xm_d_map$,215,182;
			break;
		}
	}
	close;
}

1@xm_d,3,1,3	script	#adsw2	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		getunitdata 'Antonio, .@mob;
		mapannounce 'xm_d_map$, "工廠宣布：在 " + .@mob[UMOB_X] + " - " + .@mob[UMOB_Y] + ".",bc_map,"0x00ff44";
	}
	end;
}
